//================= Hercules Script =======================================
//=       _   _                     _
//=      | | | |                   | |
//=      | |_| | ___ _ __ ___ _   _| | ___  ___
//=      |  _  |/ _ \ '__/ __| | | | |/ _ \/ __|
//=      | | | |  __/ | | (__| |_| | |  __/\__ \
//=      \_| |_/\___|_|  \___|\__,_|_|\___||___/
//================= License ===============================================
//= This file is part of Hercules.
//= http://herc.ws - http://github.com/HerculesWS/Hercules
//=
//= Copyright (C) 2012-2015  Hercules Dev Team
//= Copyright (C)  Lupus
//= Copyright (C)  $ephiroth
//=
//= Hercules is free software: you can redistribute it and/or modify
//= it under the terms of the GNU General Public License as published by
//= the Free Software Foundation, either version 3 of the License, or
//= (at your option) any later version.
//=
//= This program is distributed in the hope that it will be useful,
//= but WITHOUT ANY WARRANTY; without even the implied warranty of
//= MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//= GNU General Public License for more details.
//=
//= You should have received a copy of the GNU General Public License
//= along with this program.  If not, see <http://www.gnu.org/licenses/>.
//=========================================================================
//= Official Skill Resetter Event in Yuno (kRO & idRO)
//================= Description ===========================================
//= Skill Reset with payment, minimum BaseLv.60 to reset skill.
//= Each 1 BaseLv need 20.000 Zeny, so be careful to use skill.
//================= Current Version =======================================
//= 1.02
//=========================================================================

yuno,138,187,4	script	Hypnotist Teacher	4_F_TELEPORTER,{
	@npcname$ = "[^D5A500Hypnotist^000000]";
	mes @npcname$;

	if(MISC_QUEST & 1024){
		mes "I already told you that you may only complete this event once.";
		close;
	}
	if(BaseLevel < 60) {
		mes "Please return when you reach BaseLv 60 or higher.";
		close;
	}
	if(SkillPoint != 0) {
		mes "You will need to use up all of your skill points if you want me to continue.";
		mes "Please come again soon!";
		close;
	}
	if(Weight || checkfalcon() || checkcart() || checkmount()) {
		mes "Would you like to reset skills?";
		mes "I'm sorry, but...";
		next;
		mes @npcname$;
		if(Weight) {
			mes "You cannot reset skills";
			mes "when you keep";
			mes "any items.";
		} else if(checkcart())
			mes "Please, drop your cart and we'll continue.";
		else if(checkfalcon())
			mes "Please, free your Falcon and we'll continue.";
		else if(checkmount())
			mes "Please, free your mount and we'll continue.";
		next;
		mes @npcname$;
		mes "Come back soon!";
		close;
	}
	mes "Hello, I am the Skill Resetter.";
	mes "Your name is ^0080FF"+strcharinfo(PC_NAME)+"^000000.";
	mes "How can I help you?";
	next;
	switch (select("^009500Information about Reset skills.^000000","^00B6FFReset skills.^000000","^000088Nevermind^000000")) {
	case 1: // Information
		mes @npcname$;
		mes "This skill reset is not FREE OF CHARGE!!";
		mes "Expense for the reset of skill is ^D5A50020000 Zeny x BaseLv^000000.";
		mes "Yeah ...each One BaseLv costs 20000 Zeny to reset skill.";
		next;
		mes @npcname$;
		mes "Oh yeah, one more thing!";
		mes "Any carts, falcons or pecos you have equiped";
		mes "will be removed if you reset your skills.";
		next;
		mes @npcname$;
		mes "Just one time does again to shake the skill point";
		mes "Careful with your skills from here on.";
		close;
	case 2: // Reset
		mes @npcname$;
		mes "Before skill reset in starting.";
		mes "You shall have to first tell me your Base Level.";
		next;
		mes "^D5A500[" + strcharinfo(PC_NAME) + "]^000000";
		mes "My Base level is ^AA00AALevel " + BaseLevel + "BaseLv.^000000";
		next;
		@zeny = BaseLevel*20000;
		mes @npcname$;
		mes "Total zeny to the reset of skill amount ^529DFF" + @zeny + "Zeny^000000  for the skill reset service.";
		next;
		if (Zeny < @zeny) {
			mes @npcname$;
			mes "It seems that you don't have enough money.";
			mes "In addition we wait for the opportunity.";
			emotion e_hmm;
			close;
		}
		Zeny -= @zeny;
		MISC_QUEST |= 1024;
		resetskill;
		mes @npcname$;
		mes "Thank you.";
		emotion e_thx;
		logmes "SKILL RESET EVENT";
		close;
	case 3: // Nevermind
		mes @npcname$;
		mes "You know where to find me,";
		mes "if you ever want a reset!!";
		close;
	}
}
