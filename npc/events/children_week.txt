//===== Hercules Script ======================================
//= Children Week Event
//===== By: ==================================================
//= Samuray22
//===== Current Version: =====================================
//= 1.1
//===== Description: =========================================
//= [Official Conversion]
//= Indian Ro: Children Week Event
//===== Additional Comments: =================================
//= 1.1 First Beta. [Samuray22]
//============================================================

prontera,146,91,5	script	Pandit chacha#child07	1_M_PUBMASTER,{
	if (BaseLevel > 50) {
		mes "[Pandit chacha]";
		mes "Hahaha~";
		mes "It is special day, comes only one time in a year.";
		mes "If you see little adventurers around you send to me~";
		mes "I will give the special gift.";
		close;
	} else if (BaseLevel < 20) {
		mes "[Pandit chacha]";
		mes "Hahaha~";
		mes "You are a baby adventurer not little adventurer.";
		mes "When you more grow up, come back again. hahaha.";
		close;
	} else if (oversea_event9 < 1) {
		mes "[Pandit chacha]";
		mes "Hahaha~";
		mes "Welcome, little adventurers!";
		mes "Today is special day, isn't it~?";
		mes "I would like to give the small gift to little adventurer...";
		mes "What about you? Do you want to take it?";
		next;
		if(select("No. I will take it later.:Sure, I want.") == 1) {
			mes "[Pandit chacha]";
			mes "That's too bad.... hum...";
			mes "I gathered some stuffs from far a way world to make it....";
			mes "Whenever come back again if you want it...";
			close;
		}
		oversea_event9 = 1;
		getitem 11705,10; //Special_White_Potion
		mes "[Pandit chacha]";
		mes "Look. This is a child Potion.";
		mes "The weight is just 1 but recover much HP.";
		mes "If you want to get more, bring the 1 Wedding Bouquet and 1 Witherless Rose.";
		close;
	} else if (oversea_event9 == 1) {
		if (countitem(745) > 0 && countitem(748) > 0) {
			mes "[Pandit chacha]";
			mes "Ahha!!";
			mes "You have remembered my beautiful composition.";
			mes "You did good work.";
			mes "Could you give me 1 Wedding Bouquet and 1 Witherless Rose? ";
			next;
			if(select("Not yet.:Sure, take it.") == 1) {
				mes "[Pandit chacha]";
				mes "If you are not prepared yet, call me when you ready.";
				close;
			}
			mes "[Pandit chacha]";
			mes "Hahaha~";
			mes "Oh~ you get it.";
			mes "Here, I will exchange to the 50 child potion.";
			mes "Once you get this 50 child potion, I won't give any more.";
			close2;
			delitem 745,1; //Wedding_Bouquet
			delitem 748,1; //Witherless_Rose
			oversea_event9 = 2;
			getitem 11705,50; //Special_White_Potion
			end;
		}
		mes "[Pandit chacha]";
		mes "Little adventurers, you should bring the 1 Wedding Bouquet and 1 Witherless Rose.";
		mes "If you bring these stuffs I will exchange them for child Potion. ";
		close;
	}
	mes "[Pandit chacha]";
	mes "Hahaha~";
	mes "Are you enjoying children week~?";
	close;
}
