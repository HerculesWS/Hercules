//================= Hercules Script =======================================
//=       _   _                     _
//=      | | | |                   | |
//=      | |_| | ___ _ __ ___ _   _| | ___  ___
//=      |  _  |/ _ \ '__/ __| | | | |/ _ \/ __|
//=      | | | |  __/ | | (__| |_| | |  __/\__ \
//=      \_| |_/\___|_|  \___|\__,_|_|\___||___/
//================= License ===============================================
//= This file is part of Hercules.
//= http://herc.ws - http://github.com/HerculesWS/Hercules
//=
//= Copyright (C) 2012-2024 Hercules Dev Team
//= Copyright (C) Euphy
//= Copyright (C) L0ne_W0lf
//=
//= Hercules is free software: you can redistribute it and/or modify
//= it under the terms of the GNU General Public License as published by
//= the Free Software Foundation, either version 3 of the License, or
//= (at your option) any later version.
//=
//= This program is distributed in the hope that it will be useful,
//= but WITHOUT ANY WARRANTY; without even the implied warranty of
//= MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//= GNU General Public License for more details.
//=
//= You should have received a copy of the GNU General Public License
//= along with this program.  If not, see <http://www.gnu.org/licenses/>.
//=========================================================================
//= Isle of Bios
//================= Description ===========================================
//= Bios Island is part of the episode 14.3 and is the prerequisite of all 
//  the other instances found in the Flame Cave.
//===== Changelogs: =======================================================
//= 1.0 First version. [Capuche]
//= 1.1 Convert to Hercules. [Fiction]
//=========================================================================
moro_cav,50,64,5	script	Yellow Seed#bios	CLEAR_NPC,{
		
	if (BaseLevel < 160) {
		mes("^ff0000Characters below Level 160 cannot enter this dungeon.^000000");
		close;
	}
	if (questprogress(15006) == 0) {
		mes("^ff0000You first must meet Wandering Old Man to gain access to this dungeon.^000000");
		close;
	}
	switch( questprogress(15005,PLAYTIME) ) {
	case 0:
		if(select("Create Instance", "Cancel") == 0 ) { close();	}
			if( questprogress(15008,PLAYTIME) == 2) {
				mes("^0000ffThis Memorial Dungeon cannot be accessed for 23 hours after your last visit.^000000");
				close();
			}
			if (getcharid(CHAR_ID_CHAR) != getpartyleader(.@party_id, 2)) {
				mes("^ff0000The leader needs to generate the dungeon.^000000");
				close();
			}
			.@party_id = getcharid(CHAR_ID_PARTY);
			.@instance = instance_create(_("Isle of Bios"), .@party_id);
			.@p_name$ = getpartyname(.@party_id);
			.@md_name$ = _("Isle of Bios");
			if (.@instance >= 0) {
				if (instance_attachmap("1@dth1", .@instance) == "" || instance_attachmap("1@dth2", .@instance) == "" || instance_attachmap("1@dth3", .@instance) == "" ) {
					mesf("Party Name: %s", .@p_name$);
					mesf("Party Leader: %s", strcharinfo(PC_NAME));
					mesf("^0000FF%s^000000 - Reservation Failed!", .@md_name$);
					instance_destroy(.@instance);
					close();
				}
				instance_set_timeout(3600, 300, .@instance);
				instance_init(.@instance);
				mes("^ff0000Created the Memorial dungeon.^000000");
				mes("^ff0000Please click and press Enter again.^000000");
				close;
				}
	case 1:
			if( questprogress(15007,PLAYTIME) == 2) {
				mes("^0000ffThis Memorial Dungeon cannot be accessed for 23 hours after your last visit.^000000");
				close();
			} 
			if (has_instance("1@dth1") == "" || has_instance("1@dth2") == "" || has_instance("1@dth3") == "" ) {
				mes("^ff0000Your party leader has not yet created a Crack of Time.^000000");
				close;
			}
			if(select("Enter to the instance", "Cancel") == 2) { close(); }
			mapannounce("moro_cav", sprintf(_$("%s of the party, %s, is entering Isle of Bios."), strcharinfo(PC_NAME), getpartyname(getcharid(CHAR_ID_PARTY))), bc_map, C_SPRINGGREEN);
			setquest 15005;// Memorial Dungeon: Isle of Bios
			setquest 15007;// Memorial Dungeon: Isle of Bios
			warp("1@dth1", 17, 93);			
			end;
			break;
	case 2:
			mes("^00ff00The 23 hours have passed, and the mystical barriers have faded. You are now free to return and explore the Memorial Dungeon once more.^000000");
			erasequest 15005;// Memorial Dungeon: Isle of Bios (Isle of Bios becomes accessible for 23 hours after each exploration)
			erasequest 15007;// Memorial Dungeon: Isle of Bios (Isle of Bios allows reentry for the first 5 minutes after its openin)
			if(questactive(15008)){ erasequest 15008; }// Memorial Dungeon: Isle of Bios (You cleared the dungeon in 5 minutes after you entered it)
			break;
	}
	
OnInit:
	while(true) {
		sleep 500;
		specialeffect EF_ENHANCE;
	}
	end;
}


moro_cav,45,60,5	script	Wandering Old Man#sara	4_M_EINOLD,{
	if (BaseLevel < 160) {
		mes("[Wandering Old Man]");
		mes("...");
		mes("I'm afraid you're not up for this yet.");
		mes("You have to be at least Level 160.");
		close;
	}
	if (questprogress(15006) == 0) {
		mes("[Wandering Old Man]");
		mes("...Where am I?");
		next;
		mes("[Wandering Old Man]");
		mes("I don't know when it began...");
		mes("The same nightmare has been plaguing me for a long time.");
		next;
		mes("[Wandering Old Man]");
		mes("In my nightmare, Demon King Morocc awakens from his century-long slumber and fills the world with darkness and horror.");
		next;
		mes("[Wandering Old Man]");
		mes("I couldn't shake off the vivid images I saw in that nightmare. I've wandered off and around like a madman and finally arrived here.");
		next;
		mes("[Wandering Old Man]");
		mes("I saw this place in my dream. It's where Demon God Morocc awakens from his slumber. My dream was showing me a possible future.");
		next;
		mes("[Wandering Old Man]");
		mes("If my dream really is a premonition, the forces of Demon God Morocc's are within that yellow seed.");
		next;
		mes("[Wandering Old Man]");
		mes("I tried to enter the seed, but to no avail: I'm just too old and weak. But perhaps you can.");
		next;
		mes("[Wandering Old Man]");
		mes("Please investigate that yellow seed.");
		mes("It's too dangerous for you to go alone. You should take at least one comrade with you.");
		setquest 15006;// Isle of Bios Exploration
		completequest 15006;// Isle of Bios Exploration
		close;
	}
	mes("[Wandering Old Man]");
	mes("Please investigate that yellow seed.");
	mes("It's too dangerous for you to go alone. You should take at least one comrade with you.");
	close;
}
// warps
1@dth1,291,102,0	warp	warp1#bios1	2,2,1@dth2,17,93
1@dth2,291,102,0	warp	warp1#bios2	2,2,1@dth3,45,68
// zone 1
1@dth1,74,94,3	script	Grim Reaper Ankou#bios1	GRIM_REAPER_ANKOU,{ end; }
1@dth1,71,97,3	duplicate(Grim Reaper Ankou#bios1)	Zeith#bios1	4_M_GUILLOTINE
1@dth1,67,97,5	duplicate(Grim Reaper Ankou#bios1)	Vrid#bios1	4_M_KNIGHT_BLACK
// 1@dth1,67,97,5	script	Vrid#bios1	4_M_KNIGHT_BLACK,5,7,{// official
// 1@dth1,67,97,5	script	start1#bios1	HIDDEN_WARP_NPC,{// official
1@dth1,67,97,5	script	start1#bios1	HIDDEN_WARP_NPC,5,7,{
	end;
OnTouch_:
	disablenpc instance_npcname("start1#bios1");
	'zeith$ = instance_npcname("Zeith#bios1");
	'vrid$ = instance_npcname("Vrid#bios1");
	'reaper$ = instance_npcname("Grim Reaper Ankou#bios1");
	initnpctimer;
	npctalk(_("Ah, someone else has come to join us."), 'vrid$);
	end;
OnTimer2500:
	npctalk(_("Did you also come to stop Morocc's resurrection?"), 'vrid$);
	end;
OnTimer5000:
	npctalk(_("I'm Vrid and this is Zeith, from Prontera. We've been traveling around the world, fighting for justice."), 'vrid$);
	end;
OnTimer7500:
	npctalk(_("Hey, Zeith. Say something."), 'vrid$);
	end;
OnTimer10000:
	npctalk(_("..."), 'zeith$);
	end;
OnTimer12500:
	npctalk(_("I'm Zeith."), 'zeith$);
	end;
OnTimer15000:
	npctalk(_("..."), 'zeith$);
	end;
OnTimer17500:
	npctalk(_("Don't mind him. He's not so quiet when there are only two of us."), 'vrid$);
	end;
OnTimer19500:
	npctalk(_("...You talk enough for both of us."), 'zeith$);
	end;
OnTimer21000:
	npctalk(_("We only came because the old geezer down below kept rambling about Morocc's resurrection."), 'vrid$);
	end;
OnTimer23500:
	npctalk(_("Err, what's this place? I just touched the seed, and suddenly the scenery changed."), 'vrid$);
	end;
OnTimer26000:
	npctalk(_("Holy cow, are we flying in the sky?!"), 'vrid$);
	end;
OnTimer28500:
	npctalk(_("*Chuckle* Do you want to know where you are?"), 'reaper$);
	end;
OnTimer31000:
	npctalk(_("Who's there?!"), 'zeith$);
	end;
OnTimer33500:
	enablenpc 'reaper$;
	npctalk(_("*Chuckle* This is the Isle of Bios, a place I conjured up in my mind."), 'reaper$);
	end;
OnTimer36000:
	npctalk(_("You've walked into the trap, making yourself willing sacrifices for my Lord Morocc. Aren't you afraid of dying?"), 'reaper$);
	end;
OnTimer38500:
	npctalk(_("Pah! If I was I wouldn't have come willingly, would I?"), 'vrid$);
	end;
OnTimer41000:
	npctalk(_("Whoever you are, we'll kill you before you do!"), 'vrid$);
	end;
OnTimer43500:
	npctalk(_("Don't tempt me, human."), 'reaper$);
	end;
OnTimer46000:
	npctalk(_("I want to watch you die slowly and painfully. *Chuckle*"), 'reaper$);
	end;
OnTimer48500:
	disablenpc 'reaper$;
	end;
OnTimer51000:
	npctalk(_("Who's he? Is he trying to resurrect Morocc?"), 'vrid$);
	end;
OnTimer53500:
	npctalk(_("Let's chase him! Zeith, hurry!"), 'vrid$);
	end;
OnTimer54000:
	disablenpc 'vrid$;
	end;
OnTimer56000:
	npctalk(_("...Ack."), 'zeith$);
	end;
OnTimer58500:
	npctalk(_("I hate running."), 'zeith$);
	end;
OnTimer61000:
	disablenpc 'zeith$;
	end;
OnTimer66000:
	stopnpctimer;
	mapannounce('map_dth1$, "System: Eliminate all the enemies on the island and exit through the door on the other side to move on to the next area.",bc_map,"0xFFFF00");
	'zeith$ = 'vrid$ = 'reaper$ = "";
	end;
}
1@dth1,1,1,0	script	mobs#bios1	HIDDEN_WARP_NPC,{
	end;
OnStart:
	.@label$ = instance_npcname("mobs#bios1") + "::OnMyMobDead";
	for ( .@mob_id = 3010; .@mob_id < 3013; .@mob_id++ ) {
		switch(.@mob_id) {
		case 3010:		// Corrupt Orc Baby
			setarray .@coord[0],
				90,94, 153,76, 121,53, 135,50, 145,107, 132,126, 101,125, 119,144, 124,164, 192,145, 194,116,
				203,131, 214,142, 196,156, 217,129, 227,150, 215,109, 240,130, 252,105, 249,144, 249,155, 232,87,
				316,103, 245,71, 246,71, 210,62, 214,53, 211,88;
			break;
		case 3011:		// Corrupt Baby Desert Wolf
			setarray .@coord[0],
				90,94, 121,69, 153,76, 166,80, 131,101, 145,107, 108,156, 141,143, 165,130, 203,151,
				203,162, 219,151, 203,162, 206,169, 219,171, 227,150, 238,117, 240,140, 274,104, 306,103,
				237,80, 224,62, 233,57;
			break;
		case 3012:		// Corrupt Familiar
			setarray .@coord[0],
				143,49, 156,46, 170,67, 170,67, 131,101, 144,125, 101,125, 108,156, 135,152, 144,164,
				184,130, 204,112, 227,150, 205,98, 225,103, 228,124, 243,106, 225,89, 297,103, 223,72,
				232,69, 225,54, 211,73;
			break;
		}
		.@size = getarraysize(.@coord);
		for ( .@i = 0; .@i < .@size; .@i += 2 )
			monster 'map_dth1$, .@coord[.@i], .@coord[.@i+1], "--ja--", .@mob_id,1, .@label$;
	}
	end;
OnMyMobDead:
	.@count = mobcount( 'map_dth1$,instance_npcname("mobs#bios1") + "::OnMyMobDead" );
	if (.@count == 10 || .@count == 5)
		mapannounce('map_dth1$, "System: " + .@count + " monsters remain on the island.",bc_map,"0xFFFF00");
	else if (.@count == 1)
		mapannounce('map_dth1$, "System: 1 last monster remains on the island.",bc_map,"0xFFFF00");
	else if (.@count == 0) {
		mapannounce('map_dth1$, "System: You have eliminated all the monsters. Use the exit at 3 o'clock to move on to the next area.",bc_map,"0xFFFF00");
		disablenpc instance_npcname("mobs#bios1");
		enablenpc instance_npcname("warp1#bios1");
		enablenpc instance_npcname("start1#bios2");
		donpcevent instance_npcname("mobs#bios2") + "::OnStart";	// spawn at entrance
	}
	end;
}
// zone 2
1@dth2,67,97,0	script	start1#bios2	HIDDEN_WARP_NPC,5,5,{
	end;
OnTouch_:
	disablenpc instance_npcname("start1#bios2");
	mapannounce('map_dth2$, "System: Eliminate all the enemies on the island and exit through the door on the other side to move on to the next area.",bc_map,"0xFFFF00");
	end;
}
1@dth2,1,1,0	script	mobs#bios2	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("mobs#bios2");
	.@label$ = instance_npcname("mobs#bios2") + "::OnMyMobDead";
	for ( .@mob_id = 3013; .@mob_id < 3016; ++.@mob_id ) {
		switch(.@mob_id) {
		case 3013:		// Corrupt Orc Warrior
			setarray .@coord[0],
				90,94, 153,76, 116,66, 133,47, 145,107, 137,134, 101,125, 119,144, 124,164, 194,116,
				203,131, 211,88, 215,109, 214,142, 192,145, 196,156, 217,129, 227,150, 230,89, 210,62,
				214,53, 246,71, 248,81, 252,105, 246,126, 249,144, 249,155, 316,103;
			break;
		case 3014:		// Corrupt Desert Wolf
			setarray .@coord[0],
				90,94, 121,69, 153,76, 165,84, 145,107, 131,101, 108,156, 138,140, 165,130, 203,151,
				203,162, 219,151, 206,169, 203,162, 227,150, 222,164, 238,117, 237,80, 224,62, 233,57,
				236,138, 274,104, 306,103;
			break;
		case 3015:		// Corrupt Phen
			setarray .@coord[0],
				142,54, 156,46, 170,67, 170,67, 131,101, 101,125, 108,156, 135,152, 144,164, 135,131,
				184,130, 204,112, 205,98, 225,103, 225,89, 227,150, 232,69, 223,72, 212,75, 225,54,
				247,110, 232,126, 297,103;
			break;
		}
		.@size = getarraysize(.@coord);
		for ( .@i = 0; .@i < .@size; .@i += 2 )
			monster 'map_dth2$, .@coord[.@i], .@coord[.@i+1], "--ja--", .@mob_id,1, .@label$;
	}
	end;
OnMyMobDead:
	.@count = mobcount( 'map_dth2$,instance_npcname("mobs#bios2") + "::OnMyMobDead" );
	if (.@count == 10 || .@count == 5)
		mapannounce('map_dth2$, "System: " + .@count + " monsters remain on the island.",bc_map,"0xFFFF00");
	else if (.@count == 1)
		mapannounce('map_dth2$, "System: 1 last monster remains on the island.",bc_map,"0xFFFF00");
	else if (.@count == 0) {
		mapannounce('map_dth2$, "System: You have eliminated all the monsters. Use the exit at 3 o'clock to move on to the next area.",bc_map,"0xFFFF00");
		disablenpc instance_npcname("mobs#bios2");
		enablenpc instance_npcname("warp1#bios2");
		enablenpc instance_npcname("Zeith#bios3");
		enablenpc instance_npcname("Vrid#bios3");
		enablenpc instance_npcname("event_boss#bios3");
	}
	end;
}
// zone 3
// 1@dth3,64,74,5	script	Vrid#bios3	4_M_KNIGHT_BLACK,5,7,{// official
1@dth3,64,74,5	script	Vrid#bios3	4_M_KNIGHT_BLACK,{
	if ('reward == 1) {
		mes("[Vrid]");
		mes("I came by");
		mes("this Hero Token by accident.");
		mes("I want to give it to you. You deserve it.");
		next;
		mes("[Vrid]");
		mes("Let's get out of this creepy place!");
		close2;
		getitem (TokenOfHero ,1);// Token Of Hero
		if (ep14_3_isle == 0)
			ep14_3_isle = 1;
		if (questprogress(15007,PLAYTIME) == 0 || questprogress(15007,PLAYTIME) == 1)
			setquest 15008;// Memorial Dungeon: Isle of Bios (You cleared the dungeon in 5 minutes after you entered it)
		warp("moro_cav",45,63);
	}
	end;
}
1@dth3,64,74,5	script	event_boss#bios3	HIDDEN_WARP_NPC,5,7,{
	end;
OnTouch_:
	disablenpc instance_npcname("event_boss#bios3");
	'party_id = getcharid(1);
	donpcevent instance_npcname("freeze#bios3") + "::OnStart";
	end;
}
// 1@dth3,69,67,0	script	freeze#bios3	HIDDEN_WARP_NPC,30,30,{// official
1@dth3,1,1,0	script	freeze#bios3	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("freeze#bios3");
	'reaper3$ = instance_npcname("Grim Reaper Ankou#bios3");
	'zeith3$ = instance_npcname("Zeith#bios3");
	'vrid3$ = instance_npcname("Vrid#bios3");
	initnpctimer;
	npctalk(_("You're lucky to be here. I got totally lost the moment I entered that maze."), 'vrid3$);
	end;
OnTimer2500:
	npctalk(_("If it weren't for Zeith, I'd still be in there, wandering. Thanks, Zeith."), 'vrid3$);
	end;
OnTimer5000:
	npctalk(_("...Something's been bothering me."), 'zeith3$);
	end;
OnTimer7500:
	npctalk(_("What's up?"), 'vrid3$);
	end;
OnTimer10000:
	npctalk(_("I've got the feeling that the monsters were growing stronger with time."), 'zeith3$);
	end;
OnTimer12500:
	npctalk(_("Just like children growing up into adults."), 'zeith3$);
	end;
OnTimer15000:
	npctalk(_("Um, I think you're right."), 'vrid3$);
	end;
OnTimer17500:
	npctalk(_("They're all small in the beginning, and then we faced bigger and stronger ones over time."), 'vrid3$);
	end;
OnTimer20000:
	npctalk(_("To me, that sounds natural. What are you worried about?"), 'vrid3$);
	end;
OnTimer22500:
	npctalk(_("Well, I'm curious about the monster we'll face now."), 'zeith3$);
	end;
OnTimer25000:
	npctalk(_("Whether they're stronger versions of their original selves, or something completely different."), 'zeith3$);
	end;
OnTimer27000:
	enablenpc 'reaper3$;
	end;
OnTimer27500:
	npctalk(_("*Chuckle* At least one of you has a good head on his shoulders."), 'reaper3$);
	end;
OnTimer30000:
	npctalk(_("You again?"), 'vrid3$);
	end;
OnTimer32500:
	npctalk(_("Are you curious about your next adversaries?"), 'reaper3$);
	end;
OnTimer35000:
	npctalk(_("Ahead of you is an army of the immortal."), 'reaper3$);
	end;
OnTimer37500:
	npctalk(_("*Chuckle* Come out, my soldiers!"), 'reaper3$);
	end;
OnTimer37700:
	specialeffect EF_READYPORTAL,AREA,'reaper3$;
	end;
OnTimer40000:
	for ( .@i = 1; .@i <= 3; ++.@i ) {
		for ( .@j = 1; .@j <= 8; ++.@j )
			enablenpc instance_npcname( "Corrupt Orc Zombie#" + .@j + "bio" + .@i );
	}
	end;
OnTimer42000:
	npctalk(_("Shut up, you fiend! We don't have time for your lackeys!"), 'vrid3$);
	end;
OnTimer45000:
	npctalk(_("I'll get rid of you myself!"), 'vrid3$);
	end;
OnTimer47500:
	npctalk(_("Right back at you, bigmouth."), 'reaper3$);
	end;
OnTimer50000:
	npctalk(_("I'll teach you how terrifying it is to be helpless in the face of horror!"), 'reaper3$);
	end;
OnTimer52500:
	specialeffect EF_STORMGUST,AREA,'reaper3$;
	end;
OnTimer52700:
	specialeffect EF_FREEZED,AREA,'zeith3$;
	specialeffect EF_FREEZED,AREA,'vrid3$;
	specialeffect EF_FIREHIT,AREA,'zeith3$;
	specialeffect EF_FIREHIT,AREA,'vrid3$;
	setnpcdisplay( 'vrid3$, 4_M_FROZEN_KN );
	setnpcdisplay( 'zeith3$, 4_M_FROZEN_GC );
	getpartymember(getcharid(CHAR_ID_PARTY), PT_MEMBER_NAME, .@player_name$);
	getpartymember(getcharid(CHAR_ID_PARTY), PT_MEMBER_CHARID, .@char_id );
	getpartymember(getcharid(CHAR_ID_PARTY), PT_MEMBER_ACCID, .@account_id );
	.@count = getpartymember(getcharid(CHAR_ID_PARTY), PT_MEMBER_ACCID, .@account_id);

	for ( .@i = 0; .@i < .@count; .@i++ ) {
		if (isloggedin(.@account_id[.@i],.@char_id[.@i]) == true) {
			getmapxy .@map$,.@x,.@y, UNITTYPE_PC , .@player_name$[.@i];
			if (.@map$ == 'map_dth3$ && distance(.@x,.@y,69,67) < 30)
				sc_start SC_FREEZE,17500,0,10000, (SCSTART_NOAVOID|SCSTART_NOTICKDEF|SCSTART_NORATEDEF), .@account_id[.@i];
		}
	}
	end;
OnTimer55000:
	npctalk(_("Ugh! W-what's going on?! I-I can't move!"), 'vrid3$);
	end;
OnTimer57500:
	npctalk(_("N-neither can I! I can't move my feet!"), 'zeith3$);
	end;
OnTimer60000:
	npctalk(_("On the Isle of Bios, each day is a lifetime."), 'reaper3$);
	end;
OnTimer62500:
	npctalk(_("You must have noticed my soldiers have grown up before the sunset."), 'reaper3$);
	end;
OnTimer65000:
	npctalk(_("After nightfall, they've reached the final stage of their development: life beyond death."), 'reaper3$);
	end;
OnTimer67500:
	npctalk(_("That's right. It's time for you to experience death."), 'reaper3$);
	end;
OnTimer70000:
	npctalk(_("Get ready to feel the fear of death! *Chuckle*"), 'reaper3$);
	// note: unfreeze player
	end;
OnTimer72000:
	disablenpc 'reaper3$;
	end;
OnTimer72500:
	npctalk(_("Damn it, the undead are coming, but I can't fight because I can't move my feet!"), 'vrid3$);
	end;
OnTimer75000:
	npctalk(_("Ack, I don't want to die!"), 'vrid3$);
	end;
OnTimer77500:
	npctalk(_("...Ugh..."), 'zeith3$);
	end;
OnTimer80000:
	stopnpctimer;
	for ( .@i = 1; .@i <= 3; ++.@i ) {
		for ( .@j = 1; .@j <= 8; ++.@j )
			disablenpc instance_npcname( "Corrupt Orc Zombie#" + .@j + "bio" + .@i );
	}
	.@label$ = instance_npcname("freeze#bios3") + "::OnMyMobDead";
	for ( .@y = 80; .@y <= 92; .@y += 6 )
		for ( .@x = 62; .@x <= 78; .@x += 2 )
			monster 'map_dth3$,.@x,.@y,"Corrupt Orc Zombie",3016,1, .@label$;// EP14_3_DEATH_C_MOB1
	end;
OnMyMobDead:
	if (mobcount( 'map_dth3$,instance_npcname("freeze#bios3") + "::OnMyMobDead" ) == 0) {
		disablenpc instance_npcname("freeze#bios3");
		donpcevent instance_npcname("verit#bios3") + "::OnStart";
	}
	end;
}
1@dth3,1,1,0	script	verit#bios3	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("verit#bios3");
	initnpctimer;
	npctalk(_("Wow, you're amazing! Did you kill them all?"), 'vrid3$);
	end;
OnTimer2500:
	npctalk(_("Not yet. More are coming!"), 'zeith3$);
	end;
OnTimer5000:
	stopnpctimer;
	.@label$ = instance_npcname("verit#bios3") + "::OnMyMobDead";
	for ( .@y = 80; .@y <= 92; .@y += 6 )
		for ( .@x = 62; .@x <= 78; .@x += 2 )
			monster 'map_dth3$,.@x,.@y,"Corrupt Verit",3017,1, .@label$;// EP14_3_DEATH_C_MOB2
	end;
OnMyMobDead:
	if (mobcount( 'map_dth3$,instance_npcname("verit#bios3") + "::OnMyMobDead" ) == 0) {
		disablenpc instance_npcname("verit#bios3");
		donpcevent instance_npcname("megalodon#bios3") + "::OnStart";
	}
	end;
}
1@dth3,1,1,0	script	megalodon#bios3	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("megalodon#bios3");
	initnpctimer;
	npctalk(_("I hope that really was the last of them."), 'vrid3$);
	end;
OnTimer2500:
	npctalk(_("Nope, we aren't so lucky--more is on the way!"), 'zeith3$);
	end;
OnTimer5000:
	stopnpctimer;
	.@label$ = instance_npcname("megalodon#bios3") + "::OnMyMobDead";
	for ( .@y = 80; .@y <= 92; .@y += 6 )
		for ( .@x = 62; .@x <= 78; .@x += 2 )
			monster 'map_dth3$,.@x,.@y,"Corrupt Megalodon",3018,1, .@label$;// EP14_3_DEATH_C_MOB3
	end;
OnMyMobDead:
	if (mobcount( 'map_dth3$,instance_npcname("megalodon#bios3") + "::OnMyMobDead" ) == 0) {
		disablenpc instance_npcname("megalodon#bios3");
		donpcevent instance_npcname("boss_prelude#bios3") + "::OnStart";
	}
	end;
}
1@dth3,1,1,0	script	boss_prelude#bios3	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc 'reaper3$;
	initnpctimer;
	end;
OnTimer500:
	npctalk(_("I underestimated you, or my soldiers aren't as strong as I thought."), 'reaper3$);
	end;
OnTimer3000:
	npctalk(_("You're cheating! Release us and fight fairly!"), 'vrid3$);
	end;
OnTimer5500:
	npctalk(_("You're still talking. I'll just kill you myself and get it over with!"), 'reaper3$);
	end;
OnTimer7500:
	disablenpc 'reaper3$;
	end;
OnTimer8000:
	stopnpctimer;
	donpcevent instance_npcname("boss#bios3") + "::OnStart";
	end;
}
1@dth3,1,1,0	script	boss#bios3	HIDDEN_WARP_NPC,{
	end;
OnStart:
	enablenpc instance_npcname("boss#bios3");
	monster 'map_dth3$,70,67,"Grim Reaper Ankou",3029,1, instance_npcname("boss#bios3") + "::OnMyBossDead";
	'boss_id = $@mobid[0];
	donpcevent instance_npcname("talk_friends#bios3") + "::OnStart";
	donpcevent instance_npcname("talk_boss#bios3") + "::OnStart";
	end;
OnMyBossDead:
	'boss_id = 0;
	donpcevent instance_npcname("talk_friends#bios3") + "::OnStop";
	donpcevent instance_npcname("talk_boss#bios3") + "::OnStop";
	enablenpc 'reaper3$;
	enablenpc 'zeith3$;
	enablenpc 'vrid3$;
	initnpctimer;
	end;
OnTimer3000:
	npctalk(_("You're strong..."), 'reaper3$);
	end;
OnTimer6500:
	npctalk(_("But my goal has been accomplished... Don't think this is over."), 'reaper3$);
	end;
OnTimer8000:
	npctalk(_("Soon Lord Morocc will return. *Chuckle* The fear of death will follow you wherever you go."), 'reaper3$);
	end;
OnTimer10500:
	disablenpc 'reaper3$;
	specialeffect EF_ICECRASH,AREA,'vrid3$;
	specialeffect EF_ICECRASH,AREA,'zeith3$;
	setnpcdisplay( 'vrid3$, 4_M_KNIGHT_BLACK );
	setnpcdisplay( 'zeith3$, 4_M_GUILLOTINE );
	end;
OnTimer13000:
	npctalk(_("Ah, finally I can move!"), 'vrid3$);
	end;
OnTimer16500:
	npctalk(_("Hmpf, I'm ashamed of myself for being so weak..."), 'zeith3$);
	end;
OnTimer18000:
	npctalk(_("Whew, that reaper was much stronger than me and Zeith combined."), 'vrid3$);
	end;
OnTimer20500:
	npctalk(_("We could have died if it weren't for you."), 'vrid3$);
	end;
OnTimer23000:
	stopnpctimer;
	npctalk(_("I want to give you something for saving our lives. Talk to me again when you're ready to accept it."), 'vrid3$);
	'reward = 1;
	disablenpc instance_npcname("boss#bios3");
	end;
}
1@dth3,1,1,0	script	talk_friends#bios3	HIDDEN_WARP_NPC,{
	end;
OnStart:
	initnpctimer;
	end;
OnTimer15000:
	.@r = rand(7);
	if (.@r == 0)
		npctalk(_("Ugh... I feel so helpless..."), 'zeith3$);
	else if (.@r == 1)
		npctalk(_("That guy Ankou smells dangerous. You better move once he starts using a skill."), 'zeith3$);
	else if (.@r == 2)
		npctalk(_("Ack, I'm sorry we can't help."), 'vrid3$);
	else if (.@r == 3)
		npctalk(_("You cheater, fight fairly!"), 'vrid3$);
	else if (.@r == 4)
		npctalk(_("Damn it, I can't move..."), 'vrid3$);
	initnpctimer;
	end;
OnStop:
	stopnpctimer;
	end;
}
1@dth3,1,1,0	script	talk_boss#bios3	HIDDEN_WARP_NPC,{
	end;
OnStart:
	initnpctimer;
	end;
OnTimer13000:
	.@r = rand(6);
	if (.@r == 0)
		unittalk 'boss_id, "Grim Reaper Ankou: *Chuckle*";
	else if (.@r == 1)
		unittalk 'boss_id, "Grim Reaper Ankou: Feel the fear of death!";
	else if (.@r == 2)
		unittalk 'boss_id, "Grim Reaper Ankou: Lord Morocc will return soon.";
	else if (.@r == 3)
		unittalk 'boss_id, "Grim Reaper Ankou: Is this the best you can do? Such weak creatures, you humans.";
	initnpctimer;
	end;;
OnStop:
	stopnpctimer;
	end;
OnInstanceInit:
	'reward = 'boss_id = 0;
	'map_dth1$ = instance_mapname("1@dth1");
	'map_dth2$ = instance_mapname("1@dth2");
	'map_dth3$ = instance_mapname("1@dth3");
	// warps
	disablenpc instance_npcname("warp1#bios1");
	disablenpc instance_npcname("warp1#bios2");
	// zone 1
	disablenpc instance_npcname("Grim Reaper Ankou#bios1");
	donpcevent instance_npcname("mobs#bios1") + "::OnStart";	// spawn at entrance
	// zone 2
	disablenpc instance_npcname("start1#bios2");
	disablenpc instance_npcname("mobs#bios2");
	// zone 3
	disablenpc instance_npcname("event_boss#bios3");
	disablenpc instance_npcname("freeze#bios3");
	disablenpc instance_npcname("verit#bios3");
	disablenpc instance_npcname("megalodon#bios3");
	disablenpc instance_npcname("boss_prelude#bios3");
	disablenpc instance_npcname("boss#bios3");
	disablenpc instance_npcname("talk_friends#bios3");
	disablenpc instance_npcname("talk_boss#bios3");
	disablenpc instance_npcname("Vrid#bios3");
	disablenpc instance_npcname("Zeith#bios3");
	disablenpc instance_npcname("Grim Reaper Ankou#bios3");
	for ( .@i = 1; .@i <= 3; ++.@i ) {
		for ( .@j = 1; .@j <= 8; ++.@j )
			disablenpc instance_npcname( "Corrupt Orc Zombie#" + .@j + "bio" + .@i );
	}
	end;
}
1@dth3,72,74,3	duplicate(Grim Reaper Ankou#bios1)	Zeith#bios3	4_M_GUILLOTINE
1@dth3,70,67,3	duplicate(Grim Reaper Ankou#bios1)	Grim Reaper Ankou#bios3	GRIM_REAPER_ANKOU
1@dth3,62,80,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#1bio1	EP14_3_DEATH_C_MOB1
1@dth3,64,80,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#2bio1	EP14_3_DEATH_C_MOB1
1@dth3,66,80,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#3bio1	EP14_3_DEATH_C_MOB1
1@dth3,68,80,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#4bio1	EP14_3_DEATH_C_MOB1
1@dth3,72,80,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#5bio1	EP14_3_DEATH_C_MOB1
1@dth3,74,80,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#6bio1	EP14_3_DEATH_C_MOB1
1@dth3,76,80,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#7bio1	EP14_3_DEATH_C_MOB1
1@dth3,78,80,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#8bio1	EP14_3_DEATH_C_MOB1
1@dth3,62,86,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#1bio2	EP14_3_DEATH_C_MOB1
1@dth3,64,86,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#2bio2	EP14_3_DEATH_C_MOB1
1@dth3,66,86,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#3bio2	EP14_3_DEATH_C_MOB1
1@dth3,68,86,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#4bio2	EP14_3_DEATH_C_MOB1
1@dth3,72,86,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#5bio2	EP14_3_DEATH_C_MOB1
1@dth3,74,86,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#6bio2	EP14_3_DEATH_C_MOB1
1@dth3,76,86,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#7bio2	EP14_3_DEATH_C_MOB1
1@dth3,78,86,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#8bio2	EP14_3_DEATH_C_MOB1
1@dth3,62,92,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#1bio3	EP14_3_DEATH_C_MOB1
1@dth3,64,92,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#2bio3	EP14_3_DEATH_C_MOB1
1@dth3,66,92,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#3bio3	EP14_3_DEATH_C_MOB1
1@dth3,68,92,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#4bio3	EP14_3_DEATH_C_MOB1
1@dth3,72,92,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#5bio3	EP14_3_DEATH_C_MOB1
1@dth3,74,92,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#6bio3	EP14_3_DEATH_C_MOB1
1@dth3,76,92,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#7bio3	EP14_3_DEATH_C_MOB1
1@dth3,78,92,3	duplicate(Grim Reaper Ankou#bios1)	Corrupt Orc Zombie#8bio3	EP14_3_DEATH_C_MOB1
