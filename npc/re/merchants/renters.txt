//================= Hercules Script =======================================
//=       _   _                     _
//=      | | | |                   | |
//=      | |_| | ___ _ __ ___ _   _| | ___  ___
//=      |  _  |/ _ \ '__/ __| | | | |/ _ \/ __|
//=      | | | |  __/ | | (__| |_| | |  __/\__ \
//=      \_| |_/\___|_|  \___|\__,_|_|\___||___/
//================= License ===============================================
//= This file is part of Hercules.
//= http://herc.ws - http://github.com/HerculesWS/Hercules
//=
//= Copyright (C) 2012-2015  Hercules Dev Team
//= Copyright (C)  rAthena Dev Team
//= Copyright (C)  Euphy
//= Copyright (C)  Daegaladh
//= Copyright (C)  eAthena Dev Team
//=
//= Hercules is free software: you can redistribute it and/or modify
//= it under the terms of the GNU General Public License as published by
//= the Free Software Foundation, either version 3 of the License, or
//= (at your option) any later version.
//=
//= This program is distributed in the hope that it will be useful,
//= but WITHOUT ANY WARRANTY; without even the implied warranty of
//= MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//= GNU General Public License for more details.
//=
//= You should have received a copy of the GNU General Public License
//= along with this program.  If not, see <http://www.gnu.org/licenses/>.
//=========================================================================
//= Renters (Renewal)
//================= Description ===========================================
//= Renewal-specific breeder NPCs.
//================= Current Version =======================================
//= 2.1
//=========================================================================

//== Dragon Breeder ========================================
job3_rune01,88,62,5	script	Dragon Breeder	8W_SOLDIER,{
	mes "[Dragon Breeder]";
	if (BaseThird == Job_Rune_Knight) {
		mes "Welcome. Would you like to rent a Dragon?";
		next;
		switch(select("Yes", "No")) {
		case 1:
			//if (!getskilllv(RK_DRAGONTRAINING)) {
			if (!getskilllv(KN_RIDING)) {
				mes "[Dragon Breeder]";
				mes "Please learn how to ride a Dragon first.";
				close;
			} else if (checkmount()) {
				mes "[Dragon Breeder]";
				mes "You already have a Dragon.";
				close;
			} else if(hascashmount()) {
				mes "[Dragon Breeder]";
				mes "Please remove your cash mount.";
				close;
			}
			setmount(MOUNT_DRAGON);
			close;
		case 2:
			mes "[Dragon Breeder]";
			mes "I see. Then have a great day.";
			close;
		}
	}
	mes "What are you doing here?";
	mes "Only Rune Knights can rent a Dragon.";
	close;
}

//== Dragon/Gryphon Master =================================
prontera,130,213,5	script	Riding Creature Master	8W_SOLDIER,{
	mes "[Riding Creature Master]";
	if (BaseThird == Job_Rune_Knight) {
		mes "Welcome. Would you like to rent a Dragon?";
		next;
		switch(select("Yes", "No")) {
		case 1:
			//if (!getskilllv(RK_DRAGONTRAINING)) {
			if (!getskilllv(KN_RIDING)) {
				mes "[Riding Creature Master]";
				mes "Please learn how to ride a Dragon first.";
				close;
			} else if (checkmount()) {
				mes "[Riding Creature Master]";
				mes "You already have a Dragon.";
				close;
			} else if(hascashmount()) {
				mes "[Riding Creature Master]";
				mes "Please remove your cash mount.";
				close;
			}
			setmount(MOUNT_DRAGON);
			close;
		case 2:
			mes "[Riding Creature Master]";
			mes "I see. Then have a great day.";
			close;
		}
	}
	if (BaseThird == Job_Royal_Guard) {
		mes "Welcome. Would you like to rent a Gryphon?";
		next;
		switch(select("Yes", "No")) {
		case 1:
			if(!getskilllv(KN_RIDING)) {
				mes "[Riding Creature Master]";
				mes "Please learn how to ride a Gryphon first.";
				close;
			} else if (checkmount()) {
				mes "[Riding Creature Master]";
				mes "You already have a Gryphon.";
				close;
			} else if(hascashmount()) {
				mes "[Riding Creature Master]";
				mes "Please remove your cash mount.";
				close;
			}
			setmount(MOUNT_PECO);
			close;
		case 2:
			mes "[Riding Creature Master]";
			mes "I see. Then have a great day.";
			close;
		}
	}
	mes "I'm here to provide Rune Knights and Royal Guards with riding creatures.";
	close;
}

geffen,100,55,3	duplicate(Riding Creature Master)	Riding Creature Master#2	8W_SOLDIER
payon,166,102,5	duplicate(Riding Creature Master)	Riding Creature Master#3	8W_SOLDIER
aldebaran,133,109,5	duplicate(Riding Creature Master)	Riding Creature Master#4	8W_SOLDIER
yuno,171,187,3	duplicate(Riding Creature Master)	Riding Creature Master#5	8W_SOLDIER
rachel,106,130,5	duplicate(Riding Creature Master)	Riding Creature Master#6	8W_SOLDIER

//== Peco removing NPC =====================================
prontera,125,208,5	script	Peco Peco Remover	8W_SOLDIER,{
	mes "[Soldier]";
	mes "If you're unable to dismount from a Peco Peco";
	mes "for some unknown reason,";
	mes "please feel free to use my services.";
	if (checkmount() == MOUNT_PECO) {
		next;
		mes "[Soldier]";
		mes "You're riding a Peco Peco.";
		mes "Would you like to dismount?";
		next;
		switch(select("Yes", "No")) {
		case 1:
			setmount(MOUNT_NONE);
			mes "[Soldier]";
			mes "Say, how does it feel to";
			mes "step on the ground on your own";
			mes "feet again?";
			close;
		case 2:
			mes "[Soldier]";
			mes "I see. Please feel free to ask me";
			mes "if you change your mind.";
			close;
		}
	}
	close;
}

//== Magic Gear Renter =====================================
-	script	::mgm	FAKE_NPC,{
	mes "[Magic Gear Master]";
	if (BaseThird == Job_Mechanic) {
		mes "Welcome, Mechanic.";
		mes "Would you like to rent a Pushcart or";
		mes "ride a Magic Gear?";
		next;
		switch(select("Rent a Pushcart", "Ride a Magic Gear", "Cancel")) {
		case 1:
			if (checkcart()) {
				mes "[Magic Gear Master]";
				mes "I'm sorry, but you already";
				mes "have a Pushcart.";
				close;
			}
			setcart;
			mes "[Magic Gear Master]";
			mes "There you go!";
			close;
		case 2:
			if (checkmount() == MOUNT_MADO) {
				mes "[Magic Gear Master]";
				mes "I'm sorry, but you're already";
				mes "riding a Magic Gear.";
				close;
			} else if (!getskilllv(NC_MADOLICENCE)) {
				mes "[Magic Gear Master]";
				mes "Please learn the skill to get the Magic Gear License first.";
				close;
			} else if(hascashmount()) {
				mes "[Magic Gear Master]";
				mes "Please remove your cash mount.";
				close;
			}
			setmount(MOUNT_MADO);
			mes "[Magic Gear Master]";
			mes "Have fun, and please come again!";
			close;
		case 3:
			close;
		}
	}
	mes "How may I help you?";
	mes "Magic Gears are only available for Mechanics.";
	close;
}
prontera,163,178,3	duplicate(mgm)	Magic Gear Master#prt	8W_SOLDIER
geffen,103,55,5	duplicate(mgm)	Magic Gear Master#gef	8W_SOLDIER
payon,166,106,5	duplicate(mgm)	Magic Gear Master#pay	8W_SOLDIER
aldebaran,133,112,5	duplicate(mgm)	Magic Gear Master#alde	8W_SOLDIER
yuno,167,187,3	duplicate(mgm)	Magic Gear Master#yuno	8W_SOLDIER
rachel,106,134,5	duplicate(mgm)	Magic Gear Master#ra	8W_SOLDIER
dicastes01,187,207,3	duplicate(mgm)	Magic Gear Master#dic	8W_SOLDIER
manuk,273,212,5	duplicate(mgm)	Magic Gear Master#man	8W_SOLDIER
splendide,180,174,5	duplicate(mgm)	Magic Gear Master#spl	8W_SOLDIER
mid_camp,242,243,3	duplicate(mgm)	Magic Gear Master#mid	8W_SOLDIER
