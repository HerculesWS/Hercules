//===== Hercules Script ======================================
//= Renters
//===== By: ==================================================
//= Hercules Dev Team
//===== Current Version: =====================================
//= 2.1
//===== Description: =========================================
//= [Official Conversion]
//= Renewal-specific breeder NPCs.
//===== Additional Comments: =================================
//= 2.1 Moved some renters to a separate renewal file. [Daegaladh]
//= 2.2 Updated to match the official scripts. [Euphy]
//============================================================

// Dragon Breeder
//============================================================
job3_rune01,88,62,5	script	Dragon Breeder	8W_SOLDIER,{
	mes "[Dragon Breeder]";
	if (Class == Job_Rune_Knight || Class == Job_Rune_Knight_T || Class == Job_Baby_Rune) {
		mes "Welcome. Would you like to rent a Dragon?";
		next;
		switch(select("Yes:No")) {
		case 1:
			//if (!getskilllv("RK_DRAGONTRAINING")) {
			if (!getskilllv("KN_RIDING")) {
				mes "[Dragon Breeder]";
				mes "Please learn how to ride a Dragon first.";
				close;
			}
			else if (checkriding()) {
				mes "[Dragon Breeder]";
				mes "You already have a Dragon.";
				close;
			}
			else if(ismounting()) {
				mes "[Dragon Breeder]";
				mes "Please remove your cash mount.";
				close;
			}
			setdragon;
			close;
		case 2:
			mes "[Dragon Breeder]";
			mes "I see. Then have a great day.";
			close;
		}
	}
	mes "What are you doing here?";
	mes "Only Rune Knights can rent a Dragon.";
	close;
}

// Dragon/Gryphon Master
//============================================================
prontera,130,213,5	script	Riding Creature Master	8W_SOLDIER,{
	mes "[Riding Creature Master]";
	if (Class == Job_Rune_Knight || Class == Job_Rune_Knight_T || Class == Job_Baby_Rune) {
		mes "Welcome. Would you like to rent a Dragon?";
		next;
		switch(select("Yes:No")) {
		case 1:
			//if (!getskilllv("RK_DRAGONTRAINING")) {
			if (!getskilllv("KN_RIDING")) {
				mes "[Riding Creature Master]";
				mes "Please learn how to ride a Dragon first.";
				close;
			}
			else if (checkriding()) {
				mes "[Riding Creature Master]";
				mes "You already have a Dragon.";
				close;
			}
			else if(ismounting()) {
				mes "[Riding Creature Master]";
				mes "Please remove your cash mount.";
				close;
			}
			setdragon;
			close;
		case 2:
			mes "[Riding Creature Master]";
			mes "I see. Then have a great day.";
			close;
		}
	}
	if (Class == Job_Royal_Guard || Class == Job_Royal_Guard_T || Class == Job_Baby_Guard) {
		mes "Welcome. Would you like to rent a Gryphon?";
		next;
		switch(select("Yes:No")) {
		case 1:
			if(!getskilllv("KN_RIDING")) {
				mes "[Riding Creature Master]";
				mes "Please learn how to ride a Gryphon first.";
				close;
			}
			else if (checkriding()) {
				mes "[Riding Creature Master]";
				mes "You already have a Gryphon.";
				close;
			}
			else if(ismounting()) {
				mes "[Riding Creature Master]";
				mes "Please remove your cash mount.";
				close;
			}
			setriding;
			close;
		case 2:
			mes "[Riding Creature Master]";
			mes "I see. Then have a great day.";
			close;
		}
	}
	mes "I'm here to provide Rune Knights and Royal Guards with riding creatures.";
	close;
}

// Peco removing NPC
//============================================================
prontera,125,208,5	script	Peco Peco Remover	8W_SOLDIER,{
	mes "[Soldier]";
	mes "If you're unable to dismount from a Peco Peco";
	mes "for some unknown reason,";
	mes "please feel free to use my services.";
	if (checkriding()) {
		next;
		mes "[Soldier]";
		mes "You're riding a Peco Peco.";
		mes "Would you like to dismount?";
		next;
		switch(select("Yes:No")) {
		case 1:
			setriding 0;
			mes "[Soldier]";
			mes "Say, how does it feel to";
			mes "step on the ground on your own";
			mes "feet again?";
			close;
		case 2:
			mes "[Soldier]";
			mes "I see. Please feel free to ask me";
			mes "if you change your mind.";
			close;
		}
	}
	close;
}

// Magic Gear Renter
//============================================================
-	script	::mgm	-1,{
	mes "[Magic Gear Master]";
	if (Class == Job_Mechanic || Class == Job_Mechanic_T || Class == Job_Baby_Mechanic) {
		mes "Welcome, Mechanic.";
		mes "Would you like to rent a Pushcart or";
		mes "ride a Magic Gear?";
		next;
		switch(select("Rent a Pushcart:Ride a Magic Gear:Cancel")) {
		case 1:
			if (checkcart()) {
				mes "[Magic Gear Master]";
				mes "I'm sorry, but you already";
				mes "have a Pushcart.";
				close;
			}
			setcart;
			mes "[Magic Gear Master]";
			mes "There you go!";
			close;
		case 2:
			if (checkmadogear()) {
				mes "[Magic Gear Master]";
				mes "I'm sorry, but you're already";
				mes "riding a Magic Gear.";
				close;
			}
			else if (!getskilllv("NC_MADOLICENCE")) {
				mes "[Magic Gear Master]";
				mes "Please learn the skill to get the Magic Gear License first.";
				close;
			}
			else if(ismounting()) {
				mes "[Magic Gear Master]";
				mes "Please remove your cash mount.";
				close;
			}
			setmadogear;
			mes "[Magic Gear Master]";
			mes "Have fun, and please come again!";
			close;
		case 3:
			close;
		}
	}
	mes "How may I help you?";
	mes "Magic Gears are only available for Mechanics.";
	close;
}
prontera,163,178,3	duplicate(mgm)	Magic Gear Master#prt	8W_SOLDIER
geffen,103,55,5	duplicate(mgm)	Magic Gear Master#gef	8W_SOLDIER
payon,166,106,5	duplicate(mgm)	Magic Gear Master#pay	8W_SOLDIER
aldebaran,133,112,5	duplicate(mgm)	Magic Gear Master#alde	8W_SOLDIER
yuno,167,187,3	duplicate(mgm)	Magic Gear Master#yuno	8W_SOLDIER
rachel,106,134,5	duplicate(mgm)	Magic Gear Master#ra	8W_SOLDIER
dicastes01,187,207,3	duplicate(mgm)	Magic Gear Master#dic	8W_SOLDIER
manuk,273,212,5	duplicate(mgm)	Magic Gear Master#man	8W_SOLDIER
splendide,180,174,5	duplicate(mgm)	Magic Gear Master#spl	8W_SOLDIER
mid_camp,242,243,3	duplicate(mgm)	Magic Gear Master#mid	8W_SOLDIER
