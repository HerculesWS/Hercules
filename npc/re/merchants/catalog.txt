//===== rAthena Script =======================================
//= Catalog Magician
//===== By: ==================================================
//= skyiing, Scriptor
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: =====================================
//= rAthena SVN
//===== Description: =========================================
//= Catalogs allow players to easily search for items in-game,
//= and to buy/sell directly from the catalog interface.
//===== Additional Comments: =================================
//= 1.0 Moved from root directory. [Euphy]
//============================================================ 

moc_para01,22,16,5	script	Catalog Magician#catal01	704,{
	if (!checkweight(1201,2)) {
		mes "- You have too many items. Please make space. -";
		close;
	}
	set .@ticket_cost, 200;
	mes "[Catalog Magician]";
	mes "Look... the Magic Academy";
	mes "in Geffen is now directly";
	mes "selling the brand new";
	mes "^007777Universal Catalog Silver^000000!";
	next;
	mes "He is looking at the words";
	mes "written on his hand.";
	mes "Seems like he's too busy";
	mes "to look at you.";
	next;
	mes "[Catalog Magician]";
	mes "A brand new catalog";
	mes "has just come out!";
	mes "You can check what";
	mes "items can be traded";
	mes "and what items are on";
	mes "sale from the vendor.";
	next;
	mes "[Catalog Magician]";
	mes "^007777Universal Catalog Silver^000000~";
	mes "It costs only 200z each!";
	mes "You can buy it up to 50";
	mes "pieces at once!";
	next;
	switch(select("Buy ^007777Universal Catalog Silver^000000:I don't need it!:You don't look like a vendor...")) {
	case 1:
		mes "[Catalog Magician]";
		mes "You... want all 50 pieces?";
		mes "Or how many do you need?";
		next;
		while (1) {
			input .@input;
			mes "[Catalog Magician]";
			if (.@input == 0) {
				mes "Trade has been stopped!";
				mes "I don't know what to do next...";
				mes "What should I do?";
				emotion e_swt2,1;
				close;
			} else if (.@input < 1 || .@input > 50) {
				mes "It should be less than 50 pieces.";
				next;
			} else
				break;
		}
		set .@sell, .@ticket_cost * .@input;
		mes "Total number of catalog(s) that";
		mes "you're trying to purchase is";
		mes .@input + " pieces and it costs " + .@sell + " z.";
		if (Zeny < .@sell) {
			mes "It seems you don't have enough money.";
			mes "You can sell your equips";
			mes "to make more money... if you want.";
			close;
		}
		mes "Okie, here you go.";
		set Zeny, Zeny - .@sell;
		getitem 12580,.@input; //Vending_Search_Scroll
		close;
	case 2:
		mes "[Catalog Magician]";
		mes "Argh... you really need this";
		mes "item... Do you want me to";
		mes "explain again? I don't know";
		mes "what to do next...";
		mes "What should I do?";
		emotion e_swt2,1;
		close;
	case 3:
		mes "[Catalog Magician]";
		mes "My real job is a magician.";
		mes "I was going to ask the merchant";
		mes "guild to sell them, but they were";
		mes "trying use them as an appendix";
		mes "into broom sales. That's why";
		mes "I'm selling these by myself.";
		next;
		mes "[Catalog Magician]";
		mes "There are many adventurers";
		mes "here coming and going. I'm";
		mes "pretty sure that I can sell them";
		mes "all soon. Don't you think so?";
		mes "So... let's start!! Buy more of them, please! PLEASE...!";
		close;
	}
}
