//================= Hercules Script =======================================
//=       _   _                     _
//=      | | | |                   | |
//=      | |_| | ___ _ __ ___ _   _| | ___  ___
//=      |  _  |/ _ \ '__/ __| | | | |/ _ \/ __|
//=      | | | |  __/ | | (__| |_| | |  __/\__ \
//=      \_| |_/\___|_|  \___|\__,_|_|\___||___/
//================= License ===============================================
//= This file is part of Hercules.
//= http://herc.ws - http://github.com/HerculesWS/Hercules
//=
//= Copyright (C) 2013-2015  Hercules Dev Team
//= Copyright (C)  Euphy
//=
//= Hercules is free software: you can redistribute it and/or modify
//= it under the terms of the GNU General Public License as published by
//= the Free Software Foundation, either version 3 of the License, or
//= (at your option) any later version.
//=
//= This program is distributed in the hope that it will be useful,
//= but WITHOUT ANY WARRANTY; without even the implied warranty of
//= MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//= GNU General Public License for more details.
//=
//= You should have received a copy of the GNU General Public License
//= along with this program.  If not, see <http://www.gnu.org/licenses/>.
//=========================================================================
//= Hypnotist (Renewal)
//================= Description ===========================================
//= Stat and skill resetter for expanded first class under base level 50 and
//= normal first class.
//================= Current Version =======================================
//= 1.1
//=========================================================================

izlude,127,175,3	script	Hypnotist#novice	4_F_TELEPORTER,{
	mes "[Hypnotist]";
	mes "Greetings, adventurer.";
	mes "I'm a member of the Hypnotist";
	mes "Academy sent here to Izlude";
	mes "to provide Skill Reset services";
	mes "to certain First Class characters for a really good price: free!";
	next;
	mes "[Hypnotist]";
	mes "Although I offer unlimited";
	mes "stat/skill resets for now, I have";
	mes "two conditions that must be";
	mes "fulfilled. First, you must be";
	mes "lower than ^FF0000Base Level 50^000000.";
	next;
	mes "[Hypnotist]";
	mes "Second, you must be a";
	mes "^FF0000Swordman, Acolyte, Mage,";
	mes "Thief, Archer, Merchant,";
	mes "Taekwon Boy, or Taekwon Girl,";
	mes "Gunslinger and Ninja^000000";
	mes "Job character to qualify.";
	mes "Now, do you have any questions?";
	next;
	switch(select("^FF0000Stat/Skill Reset?", "Reset stats", "Reset skills^000000", "Cancel")) {
	case 1:
		mes "[Hypnotist]";
		mes "Stat/Skill Resets allow adventuers";
		mes "to redistribute their Skill";
		mes "Points if they are unhappy";
		mes "with their current skills.";
		next;
		mes "[Hypnotist]";
		mes "Before proceeding with";
		mes "a Stat/Skill Reset, you must";
		mes "reduce all of the weight";
		mes "of all carried items on your";
		mes "character to 0. You can put";
		mes "extra items in Kafra Storage.";
		next;
		mes "[Hypnotist]";
		mes "Ah, it's also important";
		mes "to remove your ^FF0000Pushcart^000000";
		mes "if you have one equipped.";
		mes "Otherwise, hypnosis won't";
		mes "work, or will backfire...";
		close;
	case 2:
		.@str$ = "Stat";
	case 3:
		if (.@str$ == "")
			.@str$ = "Skill";
		mes "[Hypnotist]";
		mes "Are you sure that you";
		mes "want to proceed with";
		mes "my ^FF0000"+.@str$+" Reset^000000 service?";
		next;
		if(select("Yes", "Cancel") == 1)
			break;
	case 4:
		mes "[Hypnotist]";
		mes "Thank you, and good";
		mes "luck on your adventures.";
		mes "Please travel in safety~";
		close;
	}
	if ((Class >= Job_Swordman && Class <= Job_Thief) || Class == Job_Taekwon || Class == Job_Gunslinger || Class == Job_Ninja) {
		if (checkcart()) {
			mes "[Hypnotist]";
			mes "Oh! Please remove your";
			mes "Pushcart before proceeding";
			mes "with the "+.@str$+" Reset service.";
			mes "Thanks for cooperating~";
			close;
		}
		if (Weight != 0) {
			mes "[Hypnotist]";
			mes "If you're here for my "+.@str$;
			mes "Reset service, please";
			mes "remember that you can't";
			mes "reset your "+.@str$+"s until the";
			mes "^FF0000weight of your carried items in";
			mes "your Inventory is reduced to 0^000000.";
			next;
			mes "[Hypnotist]";
			mes "Why don't you place your";
			mes "things into the Kafra Storage";
			mes "for now? That way, you can";
			mes "safely keep all of your goods.";
			close;
		}
		if (BaseLevel >= 50 && (Class == Job_Taekwon || Class == Job_Gunslinger || Class == Job_Ninja)) {
			mes "[Hypnotist]";
			mes "I'm sorry, but characters";
			mes "with Base Levels higher";
			mes "than 50 are ineligible for the";
			mes .@str$+" Reset service I provide.";
			close;
		}
		mes "[Hypnotist]";
		mes "Thank you for using";
		mes "my "+.@str$+" Redistribution";
		mes "services. Oh, and best";
		mes "of luck to you on your";
		mes "travels, adventurer.";
		if (.@str$ == "Stat")
			resetstatus;
		else
			resetskill;
		close;
	} else {
		mes "[Hypnotist]";
		mes "I'm sorry, but your";
		mes "Job Class doesn't qualify";
		mes "for the "+.@str$+" Reset service";
		mes "that I provide. I can only";
		mes "offer "+.@str$+" Resets to the";
		mes "following Jobs...";
		next;
		mes "[Hypnotist]";
		mes "^FF0000Swordman, Acolyte,";
		mes "Mage, Thief, Archer,";
		mes "Merchant, Taekwon";
		mes "Boy, Taekwon Girl,";
		mes "Gunslinder and Ninja^000000.";
		close;
	}
}
izlude_a,127,175,3	duplicate(Hypnotist#novice)	Hypnotist#novice_a	4_F_TELEPORTER
izlude_b,127,175,3	duplicate(Hypnotist#novice)	Hypnotist#novice_b	4_F_TELEPORTER
izlude_c,127,175,3	duplicate(Hypnotist#novice)	Hypnotist#novice_c	4_F_TELEPORTER
izlude_d,127,175,3	duplicate(Hypnotist#novice)	Hypnotist#novice_d	4_F_TELEPORTER
