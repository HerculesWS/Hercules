//================= Hercules Script =======================================
//=       _   _                     _
//=      | | | |                   | |
//=      | |_| | ___ _ __ ___ _   _| | ___  ___
//=      |  _  |/ _ \ '__/ __| | | | |/ _ \/ __|
//=      | | | |  __/ | | (__| |_| | |  __/\__ \
//=      \_| |_/\___|_|  \___|\__,_|_|\___||___/
//================= License ===============================================
//= This file is part of Hercules.
//= http://herc.ws - http://github.com/HerculesWS/Hercules
//=
//= Copyright (C) 2012-2015  Hercules Dev Team
//= Copyright (C)  Euphy
//= Copyright (C)  Joseph
//= Copyright (C)  Masao
//= Copyright (C)  Gepard
//= Copyright (C)  Muad_Dib
//=
//= Hercules is free software: you can redistribute it and/or modify
//= it under the terms of the GNU General Public License as published by
//= the Free Software Foundation, either version 3 of the License, or
//= (at your option) any later version.
//=
//= This program is distributed in the hope that it will be useful,
//= but WITHOUT ANY WARRANTY; without even the implied warranty of
//= MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//= GNU General Public License for more details.
//=
//= You should have received a copy of the GNU General Public License
//= along with this program.  If not, see <http://www.gnu.org/licenses/>.
//=========================================================================
//= Sura Job Quest
//================= Description ===========================================
//= Job change Quest from Monk / Champion -> Sura.
//================= Current Version =======================================
//= 1.3
//=========================================================================

ve_in,237,125,0	script	King Crab#job_shu	1_M_PAY_ELDER,{
	if (job_shu == 0) {
		mes "[King Crab]";
		mes "Khh ha ha ha ha ha ha.";
		mes "That little rookie was knocked out by my one blow!";
		next;
		mes "[Sludge Worm]";
		mes "Those arrogant fellows deserve a bitter lesson.";
		mes "How dare a little rookie attack you, boss!";
		next;
		if (BaseJob != Job_Monk || BaseLevel < 99 || JobLevel < 50) {
			mes "[King Crab]";
			mes "Khhha ha ha ha ha ha!!!!";
			mes "One more cup of beer, here!!!";
			mes "I will drink today!!!";
			next;
			mes "[Sludge Worm]";
			mes "King Crab!!! I will follow you forever!!";
			close;
		}
		mes "[King Crab]";
		mes "Ha ha ha ha ha ha ha!!!!";
		next;
		mes "[Sludge Worm]";
		mes "Hey! Right here!!! One more cup of beer!!!";
		mes "What are you doing? My boss's cup is already empty! Come quickly!";
		next;
		mes "[Waitress]";
		mes "Yes, I will bring it right away.";
		next;
		mes "[Sludge Worm]";
		mes "Too slow!!! Who do you think my boss is? Huh!?";
		next;
		mes "[Waitress]";
		mes "I... I'm really sorry, sir...";
		next;
		mes "[King Crab]";
		mes "K k k k k. That's enough.";
		mes "That cute girl seems to be scared of you!";
		mes "Hey lady~ We are not scary people~~";
		next;
		mes "[Waitress]";
		mes "Aaaaaaaaaaak! Don't do this!!";
		next;
		mes "- Bang -";
		donpcevent "???#bcmd::OnEnable";
		next;
		mes "[???]";
		mes "Hey, there.";
		mes "I've been watching you, making a ruckus like you own this place.";
		next;
		mes "[???]";
		mes "It's too noisy. So if you want to say that nonsense again, go home.";
		next;
		mes "[Sludge Worm]";
		mes "What?!";
		mes "I don't know who you are but you must've lost your mind!";
		mes "Do you have any idea who my boss is?!!";
		next;
		mes "[Sludge Worm]";
		mes "This is the famous King Crab!!!";
		mes "Who can decimate entire armies with his fist!!!";
		next;
		mes "[???]";
		mes "You do all the dirty things like your dirty face.";
		mes "You're just a thug satisfying your desires.";
		next;
		mes "[King Crab]";
		mes "What?";
		mes "How dare you say those things?";
		mes "Just because you're a girl doesn't mean I'll just let it go.";
		next;
		mes "[Sludge Worm]";
		mes "Huh! The boss is really mad!";
		mes "I can beat you up with one blow...";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Hey! How dare you use violence against a lady!?";
		next;
		mes "[???]";
		mes "Move back, you little thing...";
		next;
		mes "[???]";
		mes "Oh, I'm glad to hear that.";
		mes "I won't attack first because I'm in the middle of training.";
		next;
		mes "[King Crab & Sludge Worm]";
		mes "What?";
		next;
		mes "[King Crab & Sludge Worm]";
		mes "Uhhhh?!";
		next;
		mes "[King Crab & Sludge Worm]";
		mes "Aaaaaahhhhhhhhk!!!!!!!!";
		specialeffect EF_FIRESPLASHHIT,AREA,"King Crab#job_shu";
		specialeffect EF_FIRESPLASHHIT,AREA,"Sludge Worm#job_shu";
		next;
		mes "- What is that amazing power? -";
		mes "- I've never seen this -";
		mes "- kind of skill before... -";
		next;
		mes "[???]";
		mes "Well, the mood is spoiled...";
		next;
		mes "[Waitress]";
		mes "Tha... Thank you so much...";
		mes "How can I repay you?";
		next;
		mes "[???]";
		mes "It's not a big deal. It's okay.";
		next;
		mes "[Waitress]";
		mes "Are you going back to your lodging?";
		mes "If it's okay with you, can I deliver you a meal?";
		next;
		mes "[???]";
		mes "Oh, you don't really need to.";
		mes "Just a slice of bread and warm soup will be enough for me.";
		mes "Then, I've got to say goodbye. Bye.";
		donpcevent "???#bcmd::OnDisable";
		next;
		mes "[Waitress]";
		mes "Bye...";
		mes "...";
		mes ".....";
		mes "........(cheeks glowing)";
		job_shu = 1;
		setquest 11155;
		close;
	} else {
		if (strnpcinfo(0) == "King Crab#job_shu") {
			mes "[King Crab]";
			mes "How come a little rookie knocked me out! Me! The great King Crab...";
		} else {
			mes "[Sludge Worm]";
			mes "Boss... Are we really past our prime now?";
			mes "Sob... sob...";
		}
		close;
	}
}
ve_in,241,128,4	duplicate(King Crab#job_shu)	Sludge Worm#job_shu	4_M_MINISTER

ve_in,244,126,3	script	Waitress#job_shu	1_F_04,{
	mes "[Waitress]";
	if (job_shu == 0) {
		mes "Oh no~";
		mes "I hate those noisy and impolite customers!";
		mes "I hope there is someone who can scold them for me.";
		close;
	} else if (job_shu == 1) {
		if (BaseJob == Job_Monk) {
			mes "Oh dear~";
			mes "I really appreciate you, "+jobname(Class)+", for helping me a while ago.";
			next;
		}
		mes "Ah, the person we saw before?";
		mes "She has been staying in this village for days,";
		mes "Isn't she so great?";
		next;
		mes "[Waitress]";
		mes "Although she is a woman like me, she was so cool!";
		mes "Aaahh~ That beautiful and imposing figure...";
		next;
		mes "[Waitress]";
		mes "Oh my!!";
		mes "I forgot to do something!";
		mes "I should cook right away and then bring the meal to the Inn. Aaahh~";
		next;
		mes "[Waitress]";
		mes "If you still don't know where to stay, please use ^f57d7dthe Inn at the upper side of this building^000000.";
		close;
	}
	mes "Now that the customers have become a little quiet.";
	mes "Ahhhh, I feel better now!!";
	close;
}

ve_in,240,131,0	script	???#bcmd	4_F_SURA,{
	end;
OnInit:
	disablenpc "???#bcmd";
	end;
OnEnable:
	enablenpc "???#bcmd";
	initnpctimer;
	end;
OnDisable:
	disablenpc "???#bcmd";
	stopnpctimer;
	end;
OnTimer600000:
	donpcevent "???#bcmd::OnDisable";
	stopnpctimer;
	end;
}

ve_in,97,149,0	script	#Suraryokan	HIDDEN_WARP_NPC,3,3,{
OnTouch:
	if (job_shu == 1) {
		.@n$ = "["+strcharinfo(0)+"]";
		mes "[???]";
		mes "Who is it?!";
		next;
		mes .@n$;
		mes "I am the person who was at the pub a while ago.";
		mes "I have something to ask.";
		next;
		mes "[???]";
		mes "Don't bother me, just go away!";
		next;
		mes .@n$;
		mes "Please, I just want to talk.";
		next;
		mes "[???]";
		mes "...";
		next;
		mes .@n$;
		mes "Don't ignore me, just talk to me.";
		next;
		mes .@n$;
		mes "Just for a moment, isn't it okay?";
		next;
		mes .@n$;
		mes "I'm not here to hurt you.";
		next;
		mes .@n$;
		mes "How could you treat someone who came to your door so badly?";
		next;
		mes .@n$;
		mes "You're going too far.";
		next;
		mes .@n$;
		mes "After I saw you fighting...";
		next;
		mes .@n$;
		mes "I thought that you must have practiced martial arts and I came here to meet you.";
		next;
		mes .@n$;
		mes "But seeing that you stood me up outside like this...";
		next;
		mes .@n$;
		mes "You must be advanced in martial arts...";
		next;
		mes "[???]";
		mes "How persistent...";
		next;
		mes "[???]";
		mes "What the hell do you want?";
		job_shu = 2;
		close;
	}
	end;
}

ve_in,98,159,0	script	Fighter#job_shu	4_F_SURA,{
	if (job_shu < 2) {
		mes "[Fighter]";
		mes "Ahhhh~ I'm so tired.";
		mes "Training is so tough and I have a long way to go!";
		close;
	} else if (job_shu == 2) {
		mes "["+strcharinfo(0)+"]";
		mes "I want to know about the skill you used a while ago.";
		next;
		mes "[???]";
		mes "...";
		mes "......";
		mes ".........";
		mes "............ A rookie like you doesn't need to know.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Rookie?!";
		mes "I have steadily practiced martial arts.";
		mes "I am confident that I will never lose.";
		next;
		mes "[???]";
		mes "So you can beat me, too?";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "No...";
		mes "I guess that's too much...";
		mes "Frankly speaking, I was quite amazed by you.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "I had thought that I was strong enough but after seeing you fight, I realized that I was full of conceit.";
		next;
		mes "[???]";
		mes "Huuuh~ No matter how strong you are,";
		mes "you cannot avoid reaching for your true limit.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "You are just the person that I expected.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "I think you would know";
		mes "how to overcome this limitation.";
		next;
		mes "[???]";
		mes "Stop it.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "What? Why?";
		next;
		mes "[???]";
		mes "Are you a human?";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "What?";
		mes "Of course, I am a human.";
		next;
		mes "[???]";
		mes "To become stronger is too hard for a mere human.";
		mes "If you want to, just buy better equipment.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Then, what are you?";
		mes "You mean you're not a human being?";
		next;
		mes "[???]";
		mes "To overstep the limit...";
		mes "is impossible for a human.";
		mes "It's a ghost's way...";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Ghost?";
		next;
		mes "[???]";
		mes "I gave up being a human after I chose this way.";
		mes "The way to become a Sura abandoning divine protection.";
		next;
		mes "[???]";
		mes "You won't even have time to take a rest if you choose this way.";
		mes "You'll become a puppet for war...";
		next;
		switch(select("But I still want to know more.", "Well, I quit then.")) {
		case 1:
			mes "[???]";
			mes "Aren't you an idiot?";
			mes "Why are you so happy about becoming a ghost?";
			next;
			mes "[???]";
			mes "You'll regret it, so just be satisfied with your present life.";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "Well, now that I've met you, I won't live my life this way anymore!";
			next;
			mes "[???]";
			mes "Hah... you're really making me tired.";
			next;
			mes "[???]";
			mes "Okay, then^f57d7d go and kill 100 Desert Wolves.^000000";
			mes "After that, I'll think about it again.";
			job_shu = 3;
			changequest 11155,11156;
			close;
		case 2:
			mes "[???]";
			mes "Well, good choice.";
			mes "You don't need to look for trouble.";
			close;
		}
	} else if (job_shu == 3) {
		if (questprogress(11156,HUNTING) == 2) {
			mes "[???]";
			mes "Wow~";
			mes "Did you really kill all those wolves?";
			mes "I figured you'd have given up.";
			next;
			mes "[???]";
			mes "A woman's word is her bond!!";
			mes "A promise is a promise.";
			next;
			mes "[???]";
			mes "Go to ^f57d7dEl Mes Gorge Southeast of Juno^000000.";
			mes "My colleague ^f57d7dBuddy^000000 will be training himself near the center of that area.";
			next;
			mes "[Bruno]";
			mes "Tell him that 'Bruno' sent you there and after that, just suit yourself.";
			job_shu = 4;
			changequest 11156,11157;
			close;
		}
		mes "[???]";
		mes "Hah... you're really making me tired.";
		next;
		mes "[???]";
		mes "Okay, then^f57d7d go and kill 100 Desert Wolves.^000000";
		mes "Then I'll think about it again.";
		close;
	} else if (job_shu == 4) {
		mes "[Bruno]";
		mes "Go to ^f57d7dEl Mes Gorge Southeast of Juno^000000.";
		mes "My colleague ^f57d7dBuddy^000000 will be training himself near the center of that area.";
		next;
		mes "[Bruno]";
		mes "Tell him that 'Bruno' sent you there and after that, just suit yourself.";
		close;
	} else if (job_shu == 100) {
		mes "[Bruno]";
		mes "Hey~ !";
		mes "How are you doing?";
		next;
		mes "[Bruno]";
		mes "Don't you have any presents for your teacher?";
		close;
	}
	mes "[Bruno]";
	mes "I recommend you to reconsider being a Sura.";
	mes "Sura is strong and cool, but that's not all.";
	next;
	mes "[Bruno]";
	mes "Me?";
	mes "If I need to choose a job again, I will definitely choose Sura.";
	close;
}

yuno_fild07,254,176,7	script	Buddy#job_shu	4_M_MONK,{
	if (job_shu < 4) {
		mes "[Buddy]";
		mes "Haaap!!! Haaaap!!! Whoo!! Haahhp!!!";
		next;
		mes "[Buddy]";
		mes "I don't know why you're here but please don't stand behind me unless you want to die.";
		close;
	} else if (job_shu == 4) {
		mes "[Buddy]";
		mes "Haaap!!! Haaaap!!! Whoo!! Haahhp!!!";
		next;
		mes "[Buddy]";
		mes "I don't know why you're here but please don't stand behind me unless you want to die.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "'Bruno' sent me here.";
		next;
		mes "[Buddy]";
		mes "Ah! My colleague introduced me to you!";
		mes "Anyway, why are you here to see me?";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "I asked Bruno about her strength, and she recommended me to you.";
		next;
		mes "[Buddy]";
		mes "That difficult girl... no, just kidding.";
		mes "You must be a special person for her to send you to me.";
		next;
		mes "[Buddy]";
		mes "Well, I can't answer your question because I'm in the process of self-training, so I will take you to our master.";
		next;
		mes "[Buddy]";
		mes "My master doesn't like noise, so please ^f57d7dwait in the living room silently.^000000";
		next;
		mes "[Buddy]";
		mes "This way...";
		job_shu = 5;
		changequest 11157,11158;
		close2;
		warp "sword_1-1",215,244;
		end;
	} else if (job_shu == 100) {
		mes "[Buddy]";
		mes "Ah, long time no see, "+strcharinfo(0)+".";
		mes "Are you here to meet my master?";
		next;
		switch(select("Yes", "No")) {
		case 1:
			mes "[Buddy]";
			mes "I'll take you right away.";
			close2;
			warp "sword_1-1",216,168;
			end;
		case 2:
			mes "[Buddy]";
			mes "Then, why are you here?";
			mes "You are not here to see me, aren't you?";
			close;
		}
	}
	mes "[Buddy]";
	mes strcharinfo(0)+", didn't you meet the master yet?";
	next;
	mes "[Buddy]";
	mes "My master doesn't like noise, so please ^f57d7dwait in the living room silently.^000000";
	next;
	mes "[Buddy]";
	mes "This way...";
	close2;
	warp "sword_1-1",215,244;
	end;
}

sword_1-1,223,243,4	script	Drawing Room	4_M_MONK,{
	end;
OnInit:
	waitingroom "Drawing Room",20,"Drawing Room::OnStartArena",1;
	enablewaitingroomevent;
	end;
OnStartArena:
	warpwaitingpc "sword_2-1",223,205;
	donpcevent "Buddy#Sura_Salon::OnEnable";
	disablewaitingroomevent;
	end;
OnEnable:
	enablewaitingroomevent;
	end;
OnDisable:
	disablewaitingroomevent;
	end;
}

sword_1-1,223,243,4	script	Buddy#job_shu_Salong	4_M_MONK,{
	mes "[Buddy]";
	mes "My master doesn't like noise, so please ^f57d7dwait in the living room silently.^000000";
	next;
	switch(select("Go to the living room.", "Go outside.")) {
	case 1:
		mes "[Buddy]";
		mes "Please wait in the living room at the upper side.";
		close;
	case 2:
		mes "[Buddy]";
		mes "Then, see you again.";
		close2;
		warp "yuno_fild07",255,178;
		end;
	}
}

sword_2-1,1,1,0	script	Buddy#Sura_Salon	1_F_01,{
	end;
OnInit:
	disablenpc "Buddy#Sura_Salon";
	end;
OnEnable:
	enablenpc "Buddy#Sura_Salon";
	initnpctimer;
	end;
OnReset:
	killmonster "sword_2-1","Buddy#Sura_Salon::OnMyMobDead";
	end;
OnDisable:
	disablenpc "Buddy#Sura_Salon";
	stopnpctimer;
	end;
OnMyMobDead:
	if (mobcount("sword_2-1","Buddy#Sura_Salon::OnMyMobDead") < 1) {
		monster "sword_2-1",219,210,"Hallucination",1479,1,"Buddy#Sura_Salon::OnMyMobDead";
		monster "sword_2-1",228,209,"Hallucination",1483,1,"Buddy#Sura_Salon::OnMyMobDead";
		monster "sword_2-1",228,201,"Hallucination",1479,1,"Buddy#Sura_Salon::OnMyMobDead";
		monster "sword_2-1",219,201,"Hallucination",1483,1,"Buddy#Sura_Salon::OnMyMobDead";
	}
	end;
OnTimer3000:
	mapannounce "sword_2-1","The master is now in the middle of training, and you need to wait in the living room around 5 minutes.",bc_map;
	end;
OnTimer4000:
	mapannounce "sword_2-1","Ah! You may have hallucinations in the living room, so please be careful.",bc_map;
	end;
OnTimer5000:
	mapannounce "sword_2-1","This is the policy of the master, so please don't feel unpleasant.",bc_map;
	end;
OnTimer33000:
	monster "sword_2-1",219,210,"Hallucination",1480,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,210,"It can't be true",1479,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,209,"Hallucination",1446,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,209,"Just Imagination",1483,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,201,"Hallucination",1480,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,201,"Illusion",1479,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,201,"Hallucination",1446,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,201,"Just Imagination",1483,1,"Buddy#Sura_Salon::OnMyMobDead";
	end;
OnTimer93000:
	monster "sword_2-1",219,210,"Hallucination",1480,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,210,"Just Imagination",1479,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,209,"Hallucination",1446,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,209,"Hallucination",1483,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,201,"Hallucination",1480,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,201,"Hallucination",1446,1,"Buddy#Sura_Salon::OnMyMobDead";
	end;
OnTimer153000:
	mapannounce "sword_2-1","If you're bored, may I bring you some magazines?",bc_map;
	monster "sword_2-1",219,210,"Magazine",1478,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,210,"Magazine",1478,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,209,"Magazine",1478,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,209,"Magazine",1478,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,201,"Magazine",1478,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,201,"Magazine",1478,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,201,"Magazine",1478,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,201,"Magazine",1478,1,"Buddy#Sura_Salon::OnMyMobDead";
	end;
OnTimer213000:
	monster "sword_2-1",219,210,"Hallucination",1480,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,210,"Never mind",1479,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,209,"Hallucination",1483,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,201,"Hallucination",1480,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,201,"Can you see me?",1479,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,201,"Hallucination",1483,1,"Buddy#Sura_Salon::OnMyMobDead";
	end;
OnTimer273000:
	monster "sword_2-1",219,210,"",1480,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,209,"Hallucination",1446,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,201,"Hallucination",1480,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",228,201,"Hallucination",1479,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,201,"Hallucination",1446,1,"Buddy#Sura_Salon::OnMyMobDead";
	monster "sword_2-1",219,201,"Hallucination",1483,1,"Buddy#Sura_Salon::OnMyMobDead";
	end;
OnTimer300000:
	mapannounce "sword_2-1","The master has arrived. Soon I'll show you into the master's room.",bc_map;
	end;
OnTimer303000:
	donpcevent "Buddy#job_shuaneh::OnEnable";
	end;
OnTimer305000:
	donpcevent "Buddy#Sura_Salon::OnReset";
	end;
OnTimer315000:
	mapannounce "sword_2-1","Come on here.",bc_map;
	donpcevent "Buddy#job_shuaneh::OnDisable";
	donpcevent "#Sura_garajjom::OnEnable";
	end;
OnTimer320000:
	mapannounce "sword_2-1","I guess you're not ready to meet the master yet...",bc_map;
	mapwarp "sword_2-1","yuno_fild07",255,178;
	donpcevent "#Sura_garajjom::OnDisable";
	donpcevent "Drawing Room::OnEnable";
	donpcevent "Buddy#Sura_Salon::OnDisable";
	end;
}

sword_2-1,223,205,7	script	Buddy#job_shuaneh	4_M_MONK,{
	if (job_shu > 4) {
		mes "I'll show you the way. This way.";
		close2;
		warp "sword_1-1",216,168;
		donpcevent "Buddy#job_shuaneh::OnDisable";
		end;
	}
	mes "Why are you here?";
	close2;
	warp "yuno_fild07",248,179;
	end;
OnInit:
OnDisable:
	disablenpc "Buddy#job_shuaneh";
	end;
OnEnable:
	enablenpc "Buddy#job_shuaneh";
	end;
}

sword_2-1,223,205,0	script	#Sura_garajjom	HIDDEN_WARP_NPC,10,10,{
OnTouch:
	warp "sword_1-1",216,168;
	end;
OnInit:
OnDisable:
	disablenpc "#Sura_garajjom";
	end;
OnEnable:
	enablenpc "#Sura_garajjom";
	end;
}

sword_1-1,222,169,5	script	Bruno#job_shu	4_F_SURA,{
	if (job_shu > 4) {
		mes "[Bruno]";
		mes "Hey~ "+strcharinfo(0)+", what's up?";
		next;
		switch(select("Let's have conversation.", "Go outside.")) {
		case 1:
			if (job_shu == 100) {
				mes "[Bruno]";
				mes "My master?";
				mes "He said he has put the world out of his mind and he is a ghost who gave up being a human but he seems to have a lingering desire for the world.";
				next;
				mes "[Bruno]";
				mes "Well, I am also a Sura but I still enjoy traveling and meeting many people~";
				next;
				mes "[Bruno]";
				mes "In the old days, my master had one close friend but after becoming a Sura,";
				next;
				mes "[Bruno]";
				mes "I heard that he wrote a letter to the friend that he had died.";
				mes "Then he sometimes visits 'the place of memory' secretly.";
				next;
				mes "[Bruno]";
				mes "It would be better not saying that he died and just keep meeting his friend.";
				mes "He's a bit of a stick-in-the-mud.";
				next;
				mes "[Bruno]";
				mes "But he is really cool and awesome.";
				mes "Ho ho.";
				next;
				mes "[Bruno]";
				mes "The reason why I keep traveling is to find 'the precious friend' of my master and not to training myself more.";
				next;
				mes "[Bruno]";
				mes "I feel heavy-hearted when I see my master missing his friend...";
				mes "It's not... not just for my... master!";
				close;
			}
			mes "[Bruno]";
			mes "Wow~ I thought you would be knocked out before seeing my master but you are finally here now?";
			next;
			mes "[Bruno]";
			mes "Anyway you are here now, so I am your immediate superior.";
			next;
			mes "[Bruno]";
			mes "We don't care about your age in our world!";
			close;
		case 2:
			mes "[Bruno]";
			mes "Come again whenever you want to~";
			mes "I will show you the training course of the dead~";
			close2;
			warp "yuno_fild07",255,178;
			end;
		}
	}
	mes "[Bruno]";
	mes "What are you?";
	mes "How did you find this place!";
	mes "Go away!!!";
	close2;
	warp "yuno_fild07",255,178;
	end;
}

sword_1-1,223,167,2	script	Master#job_shu	4_M_SURA,{
	mes "[Master]";
	if (job_shu == 5) {
		mes "You must have bright eyes to see that you finally came to this room.";
		next;
		mes "[Master]";
		if (SkillPoint != 0) {
			mes "You can't become a Sura unless you use all your skill points.";
			close;
		}
		if (checkweight(Knife,1) == 0 || MaxWeight - Weight < 3500) {
			mes "You seem to be heavily burdened. Please remove some of it.";
			close;
		}
		if (BaseLevel < 99 || JobLevel < 50) {
			mes "You don't seem to have reached the limit of a human, you still have far to go in order to become Sura.";
			mes "Please go practice some more.";
			close;
		}
		if (BaseJob != Job_Monk) {
			mes "Hmm?";
			close;
		}
		mes "Of course~";
		mes "I have keen eyes.";
		next;
		mes "[Bruno]";
		mes "Master! This is a wolf fur coat.";
		mes "Put it on when it's cold.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "Ahhk! That is the wolf I... caught... Uhh.";
		next;
		mes "- Whack -";
		next;
		mes "[Bruno]";
		mes "Ho ho ho~";
		mes "Do you have something to say, "+strcharinfo(0)+"?";
		mes "You became a member of our family, let's get along together ~";
		next;
		mes "[Master]";
		mes "Right, I now accept you as my student, too.";
		next;
		mes "[Master]";
		mes "I think you already know it, but to live as a Sura will not be that easy.";
		next;
		mes "[Master]";
		mes "I also shut myself off from the world. If there comes a day when we go to the world again, it must be the day when the world needs ghosts for the wars.";
		next;
		mes "[Master]";
		mes "I hope those days will not come but... I'll willingly be the ghost and kill all enemies.";
		next;
		if (hascashmount()) {
			mes "[Master]";
			mes "So... please get down from your riding pet!";
			mes "It is a loss of etiquette on this situation.";
			close;
		}
		mes "[Master]";
		mes "You should train yourself steadily until that day.";
		job_shu = 100;
		jobchange roclass(eaclass()|EAJL_THIRD);
		completequest 11158;
		getitem Blazing_Soul,1;
		getitem Green_Apple_Ring,1;
		close;
	} else if (job_shu > 5) {
		mes "Once I was also an ordinary young man who traveled a lot and met many friends.";
		next;
		mes "[Master]";
		mes "However, as you might have felt, I finally faced my limitation.";
		next;
		mes "[Master]";
		mes "Sometimes I wondered 'should I have to give up everything to be more powerful...?'";
		next;
		mes "[Master]";
		mes "But what's done is done...";
		close;
	}
	mes "This is not a place where you can stay.";
	mes "Please go back.";
	close2;
	warp "yuno_fild07",255,178;
	end;
}

sword_2-1,1,2,0	script	Button Girl#sura	1_F_01,{
	callfunc "F_GM_NPC";
	switch(select("Turn on arena.", "Turn off arena.", "Turn on the living room.", "Turn on Buddy.", "Turn on Gara.", "Turn off all.", "Close")) {
	case 1:
		mes "Turn on arena.";
		donpcevent "Drawing Room::OnEnable";
		close;
	case 2:
		mes "Turn off arena.";
		donpcevent "Drawing Room::OnDisable";
		close;
	case 3:
		mes "Turn on the living room.";
		donpcevent "Buddy#Sura_Salon::OnEnable";
		close;
	case 4:
		mes "Turn on Buddy.";
		donpcevent "Buddy#job_shuaneh::OnEnable";
		close;
	case 5:
		mes "Turn on Gara.";
		donpcevent "#Sura_garajjom::OnEnable";
		close;
	case 6:
		mes "Turn off all.";
		donpcevent "Drawing Room::OnDisable";
		donpcevent "Buddy#Sura_Salon::OnReset";
		donpcevent "Buddy#Sura_Salon::OnDisable";
		donpcevent "Buddy#job_shuaneh::OnDisable";
		donpcevent "#Sura_garajjom::OnDisable";
		close;
	case 7:
		close;
	}
}
