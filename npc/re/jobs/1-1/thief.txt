//================= Hercules Script =======================================
//=       _   _                     _
//=      | | | |                   | |
//=      | |_| | ___ _ __ ___ _   _| | ___  ___
//=      |  _  |/ _ \ '__/ __| | | | |/ _ \/ __|
//=      | | | |  __/ | | (__| |_| | |  __/\__ \
//=      \_| |_/\___|_|  \___|\__,_|_|\___||___/
//================= License ===============================================
//= This file is part of Hercules.
//= http://herc.ws - http://github.com/HerculesWS/Hercules
//=
//= Copyright (C) 2012-2015  Hercules Dev Team
//= Copyright (C)  Euphy
//= Copyright (C)  Streusel
//= Copyright (C)  Kisuka
//=
//= Hercules is free software: you can redistribute it and/or modify
//= it under the terms of the GNU General Public License as published by
//= the Free Software Foundation, either version 3 of the License, or
//= (at your option) any later version.
//=
//= This program is distributed in the hope that it will be useful,
//= but WITHOUT ANY WARRANTY; without even the implied warranty of
//= MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//= GNU General Public License for more details.
//=
//= You should have received a copy of the GNU General Public License
//= along with this program.  If not, see <http://www.gnu.org/licenses/>.
//=========================================================================
//= Renewal Thief Job Quest
//================= Description ===========================================
//= Job Change to Thief Class
//================= Current Version =======================================
//= 1.2
//=========================================================================

moc_prydb1,39,129,2	script	Thief Guide#thief	1_F_04,{
	if (Upper == 1) {
		if (ADVJOB == Job_Assassin_Cross || ADVJOB == Job_Stalker) {
			if (Class == Job_Novice_High) {
				mes "[Thief Guide]";
				mes "Huh? Do I know you? It's creepy that you seem so familiar. You don't have a twin, do you?";
				next;
				if (getskilllv("NV_BASIC") < 9) {
					mes "[Thief Guide]";
					mes "What, do you want to be a Thief? I'm sorry, but you look like you need more training.";
					next;
					mes "[Thief Guide]";
					mes "Take your time and learn all the Basic Skills, will you? Well then, see you later~!";
					close;
				}
				mes "[Thief Guide]";
				mes "Well, I got this feeling like you've been through a lifetime of fighting, so I'm promoting you to a Thief right this minute. I better give you tough guys what you want...";
				next;
				skill 143,0,0;
				jobchange Job_Thief_High;
				skill 149,1,0;
				skill 150,1,0;
				skill 151,1,0;
				skill 152,1,0;
				mes "[Thief Guide]";
				mes "Since you've become a Thief, live as a Thief. Now, go for it! Next~";
				close;
			}else{
				mes "[Thief Guide]";
				if (Sex == SEX_MALE)
					mes "Hey, dude.";
				else
					mes "Hey, baby~";
				close;
			}
		}else{
			mes "[Thief Guide]";
			if (Sex == SEX_MALE)
				mes "Hey, dude.";
			else
				mes "Hey, baby.";
			mes "...Hey! You look too goody-goody to want to be a Thief!! Now scram, I'm busy. Next!";
			close;
		}
	}
	if(q_job_thief == 1) {
		mes "[Thief Guildsman]";
		mes "Ehh... Seems like a beloved "+ (Sex == SEX_MALE ? "son" : "daughter") +" of a rich family, but why are you here in this dirty place?";
		next;
		mes "[Thief Guildsman]";
		mes "Well, I'm not in charge of making you a Thief. I just accept applications, get it?";
		mes "If you want to become a Thief, ask the sharp-eyed guy next to me.";
		close;
	}
	if(BaseJob != Job_Novice) {
		if(BaseJob == Job_Thief) {
			mes "[Thief Guildsman]";
			mes "Hey~ if you have any trouble, get it out to me anytime, huh?";
			close;
		}else{
			mes "[Thief Guildsman]";
			mes "What the heck...?";
			if (Sex == SEX_MALE) {
				mes "Huh.. you are a "+jobname(Class)+" you blockhead...!";
				next;
				mes "[Thief Guildsman]";
				mes "Hey, brother.";
			}else{
				next;
				mes "[Thief Guildsman]";
				mes "Hey, lady.";
			}
			mes "Why are you here? Go back to your place~ go back~~";
			close;
		}
	}
	mes "[Thief Guildsman]";
	mes "Ehh... Seems like a beloved "+ (Sex == SEX_MALE ? "son" : "daughter") +" of a rich family, but why are you here in this dirty place?";
	next;
	if(select("I want to be a Thief.", "Nothing.") == 2) {
		mes "[Thief Guildsman]";
		mes "You know you cannot be a thief without an application.....";
		mes "What's on your mind..?";
		close;
	}
	mes "[Thief Guildsman]";
	mes "Well, are you that proud of it?";
	mes "You're telling me so proudly that you want to be a Thief! Why don't you go to all the villages and advertise yourself for being a thief?";
	mes "'Ha ha ha! Go put up a banner that says 'I will be a proud thief who steals other people's stuff.'";
	next;
	mes "[Thief Guildsman]";
	mes "Do you want to be a thief so badly?";
	next;
	switch(select("Yes.", "No.", "How about you?")) {
	case 1:
		mes "[Thief Guildsman]";
		mes "Oh, do you...? Huh.. well... I do live and learn to see strange people like you.";
		next;
		break;
	case 2:
		mes "[Thief Guildsman]";
		mes "Then why are you here? Do you think you can become a thief so easily?";
		next;
		break;
	case 3:
		mes "[Thief Guildsman]";
		mes "Eh..? me? me?";
		mes "Well... I just fit well to being a thief... characteristically... I don't mind this silly matter.";
		next;
		break;
	}
	mes "[Thief Guildsman]";
	mes "Anyway, in the outside world, never say that you want to be a Thief!!";
	next;
	mes "[Thief Guildsman]";
	mes "So, do you want to apply for being a Thief?";
	next;
	if(select("Yes, I do.", "No.") == 2) {
		mes "[Thief Guildsman]";
		mes "Well... do what you want to do~ Go your way~";
		close;
	}
	mes "[Thief Guildsman]";
	mes "Alright, tell me your name. Huh? What?";
	mes ""+strcharinfo(PC_NAME)+". So "+strcharinfo(PC_NAME)+", right...?";
	next;
	if (getskilllv("NV_BASIC") < 9) {
		mes "[Thief Guildsman]";
		mes "I can see your strong will to become a Thief......";
		mes "But only with your will, you cannot make it in a real fight, can you?";
		mes "So go and reach at least Basic Skill Level 9.";
		close;
	}
	mes "[Thief Guildsman]";
	mes "Hmm.... I found your interesting criminal records from the data of the detective agency. Well you seem to have what it takes to be a good thief.";
	mes ""+strcharinfo(PC_NAME)+". I now declare that you have passed the job interview!";
	next;
	mes "[Thief Guildsman]";
	mes "Well, I'm not in charge of making you a Thief. I just accept applications, get it?";
	mes "If you want to become a Thief, ask the guy next to me.";
	q_job_thief = 1;
	close;
}

moc_prydb1,42,133,2	script	Thief Guildsman#thief	2_M_THIEFMASTER,{
	if(q_job_thief == 1) {
		mes "[Thief Guildsman]";
		mes "Alright. You must have passed the job interview, huh?";
		mes "Good. I'll accept you.";
		mes "Let's begin the job-changing ceremony of our guild!";
		next;
		mes "[Commander of Thief Guild]";
		mes "'"+strcharinfo(PC_NAME)+".'";
		mes "'For those who are ready, the moonlight shall open the way. Now you are fully ready, I now officially allow you to become a member of the Thief Guild.'";
		callfunc "Job_Change",Job_Thief;
		q_job_thief = 0;
		getitem N_Main_Gauche,1; // N_Main_Gauche
		next;
		mes "[Commander of Thief Guild]";
		mes "'Congratulations on becoming a Thief.'";
		mes "'From now on, keep the rules of our guild and be an honorable member.'";
		mes "'If you bring us any disgrace by breaking our rules, you better watch your back.'";
		mes "'Anyway, I expect you to be a great thief.'";
		next;
		mes "[Brad]";
		mes "Heee~Yaaaa~! Congratulations! My friend.";
		mes "My name is 'Brad'. I'm in charge of human resources here.";
		mes "I'm not sure for now but you'll have more chances to see me later on.";
		next;
		mes "[Brad]";
		mes "Okay, I've done what I can do to you, so go on your way. I'm quite a busy man.";
		mes "See you again.";
		close;
	}
	if(BaseJob != Job_Novice) {
		if(BaseJob == Job_Thief) {
			mes "[Brad]";
			mes "I don't have any special events now. So go on your way and come back later.";
		}else{
			mes "[Thief Guildsman]";
			mes "Hey~ Hey~ You're not a novice or a thief!";
			mes "What are you doing here? You're not welcome to make this place your home~ Hweeeee~ Get outta here~";
		}
		close;
	}
	mes "[Thief Guildsman]";
	mes "Ho? Why is a novice like you visiting here?";
	mes "If you are here to be a Thief, ask the nasty-tempered lady right next to me.";
	close;
}
