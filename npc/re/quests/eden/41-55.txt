//================= Hercules Script =======================================
//=       _   _                     _
//=      | | | |                   | |
//=      | |_| | ___ _ __ ___ _   _| | ___  ___
//=      |  _  |/ _ \ '__/ __| | | | |/ _ \/ __|
//=      | | | |  __/ | | (__| |_| | |  __/\__ \
//=      \_| |_/\___|_|  \___|\__,_|_|\___||___/
//================= License ===============================================
//= This file is part of Hercules.
//= http://herc.ws - http://github.com/HerculesWS/Hercules
//=
//= Copyright (C) 2012-2015  Hercules Dev Team
//= Copyright (C)  Euphy
//= Copyright (C)  Masao
//= Copyright (C)  L0ne_W0lf
//=
//= Hercules is free software: you can redistribute it and/or modify
//= it under the terms of the GNU General Public License as published by
//= the Free Software Foundation, either version 3 of the License, or
//= (at your option) any later version.
//=
//= This program is distributed in the hope that it will be useful,
//= but WITHOUT ANY WARRANTY; without even the implied warranty of
//= MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//= GNU General Public License for more details.
//=
//= You should have received a copy of the GNU General Public License
//= along with this program.  If not, see <http://www.gnu.org/licenses/>.
//=========================================================================
//= Eden Group Quests - Mission [41 - 55]
//================= Description ===========================================
//= Repetable Quests for Players between Baselevel 41 - 55.
//================= Current Version =======================================
//= 1.2
//=========================================================================

moc_para01,40,38,3	script	Mission [41 - 55]	4_BOARD3,{
	if (countitem(Para_Team_Mark) < 1 && countitem(Para_Team_Mark_) < 1) {
		mes "You are not an Eden group member. You are not qualified to access the bulletin board.";
		close;
	}
	if (checkweight(Knife,1) == 0 || (MaxWeight - Weight) < 1000) {
		mes "^008800Wait a sec!!";
		mes "You cannot receive any more items because you're carrying too many items. Please try again after you lighten up.^000000";
		close;
	}
	if (!questprogress(12088)) {
		if (BaseLevel < 41 || BaseLevel > 55) {
			mes "Mission bulletin board for Lv41~55 adventurers.";
			close;
		}
		mes "[Mission Board]";
		mes "Mission bulletin board.";
		next;
		if(select("Check the Missions.", "Cancel.") == 2) {
			mes "^000077Ok. Let's check the missions next time.^000000.";
			close;
		}
		switch(select("Thara Frog Hunting", "Cruiser Hunting", "Kukre Hunting", "Orc Baby Hunting", "Orc Warrior Hunting", "Vadon Hunting", "Megalodon Hunting", "Marse Hunting", "Cornutus Hunting", "Myst Hunting", "Shellfish Hunting", "Marine Sphere Hunting")) {
			case 1: callsub L_Quest,12072,"Have you seen a red frog? It is called Thara Frog and it's very rare. We are trying to verify where it inhabits. So please hunt 20 Thara Frogs.";
			case 2: callsub L_Quest,12073,"I heard of some toys attacking people. It is ridiculous! Let's show them our power! We have to hunt 15 Cruisers!";
			case 3: callsub L_Quest,12074,"Someone got robbed near the beach! The possible suspect is a criminal monster called Kukre. Please hunt 30 Kukre.";
			case 4: callsub L_Quest,12075,"Orc Babies are getting more violent these days. Please hunt 15 Orc Babies.";
			case 5: callsub L_Quest,12076,"Orc Warriors are increasing their aggression. Please hunt 20 Orc Warriors.";
			case 6: callsub L_Quest,12077,"Have you seen crabs with thick shells? They are Vadons. Please hunt 15 Vadons.";
			case 7: callsub L_Quest,12078,"Dead fish are all around the beach and it is getting worse. Please hunt 30 Megalodon.";
			case 8: callsub L_Quest,12079,"Can you hunt Marse? I need 15 Marse for my study. Please help me.";
			case 9: callsub L_Quest,12080,"An idiot Cornutus bit the Mayor's foot. The Mayor is upset so we need to hunt 20 Cornutus to make him happy!";
			case 10: callsub L_Quest,12081,"People are freaking out because of Myst. It looks like a ghost so, it scares people. Please hunt 15 Myst.";
			case 11: callsub L_Quest,12082,"Don't ask why, but please hunt 30 Shellfish. It is a secret mission!";
			case 12: callsub L_Quest,12083,"Marine Sphere? Do you know what a Marine Sphere is? I got a mission for you to hunt 15 Marine Sphere, so please do so.";
			case 13: callsub L_Quest,12084,"The mayor wants to eat a delicious fish dish. Phen will be the perfect ingredient for it. Please hunt 20 Phen!";
			case 14: callsub L_Quest,12085,"Dead Orc monsters got back to our town and they've revived as Orc Skeletons. We are in chaos! Please hunt 15 Orc Skeleton.";
			case 15: callsub L_Quest,12086,"Some weird monsters called Zenorc are attacking us. Please hunt 30 Zenorc.";
			case 16: callsub L_Quest,12087,"Be prepared! It is a cruel mission! Hunt 15 Mummies!! I tried once, but I couldn't succeed. It is too scary!";
		}
		end;
	}
	setarray .@exp[0],9600,7200,15750,7875,11100,8550,17100,9000,12300,9900,20250,10350,13800,11025,22500,11250;
	for(.@i = 0; .@i<16; ++.@i)
		if (questprogress(12072+.@i,HUNTING) == 2) {
			mes "[Mission Board]";
			mes "You have completed the mission.";
			mes "Here is your reward.";
			erasequest 12088;
			erasequest 12072+.@i;
			specialeffect(EF_STEAL, AREA, playerattached());
			getexp .@exp[.@i],0;
			close;
		}
	if (!questprogress(12072,HUNTING) && !questprogress(12073,HUNTING) && !questprogress(12074,HUNTING) && !questprogress(12075,HUNTING)
	 && !questprogress(12076,HUNTING) && !questprogress(12077,HUNTING) && !questprogress(12078,HUNTING) && !questprogress(12079,HUNTING)
	 && !questprogress(12080,HUNTING) && !questprogress(12081,HUNTING) && !questprogress(12082,HUNTING) && !questprogress(12083,HUNTING)
	 && !questprogress(12084,HUNTING) && !questprogress(12085,HUNTING) && !questprogress(12086,HUNTING) && !questprogress(12087,HUNTING)) {
		mes "You may now view the bulletin board.";
		erasequest 12088;
		close;
	}
	mes "There is nothing special to do.";
	close;
L_Quest:
	mes "[Mission Board Memo]";
	mes getarg(1);
	next;
	if(select("Accept the mission.", "Cancel.") == 1) {
		mes "[Mission Board]";
		mes "You have accepted the mission. For more details, please press 'alt + u' keys, you can get information from the Quest Window.";
		setquest 12088;
		setquest getarg(0);
	}
	close;
}
