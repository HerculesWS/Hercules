//===== Hercules Script =======================================
//= Eden Group Quests - Mission [71 - 85]
//===== By: ==================================================
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.2
//===== Description: =========================================
//= Repetable Quests for Players between Baselevel 71 - 85.
//===== Additional Comments: =================================
//= 1.0 First Version.
//= 1.1 Some little optimization here and there. [Masao]
//= 1.2 Optimized. [Euphy]
//============================================================

moc_para01,44,38,3	script	Mission [71 - 85]	857,{
	if (countitem(6219) < 1) {
		mes "- Only members of the -";
		mes "- Eden Group can read -";
		mes "- this bulletin board. -";
		close;
	}
	mes "- Many hunting missions -";
	mes "- are on the bulletin board. -";
	next;
	if ((BaseLevel > 70) && (BaseLevel < 86)) {
		switch (select("Sphinx Dungeon:Glast Heim:Juno Area:Clock Tower:Localized Islands")) {
		case 1:
			mes "- Sphinx Dungeon -";
			mes "- I am Meidi who lives -";
			mes "- in Morocc. I want to hunt -";
			mes "- monsters in the Sphinx -";
			mes "- Dungeon. I will wait for -";
			mes "- you in front of the -";
			mes "- western gate. -";
			callsub L_Quest,10102,1;
			mes "- Good. Now go find Meidi -";
			mes "- at the western gate -";
			mes "- in Morroc. -";
			close;
		case 2:
			mes "- Glast Heim -";
			mes "- I am Ancellia. -";
			mes "- I need some help -";
			mes "- hunting monsters -";
			mes "- in Glast Heim.-";
			mes "- Please help me. -";
			callsub L_Quest,10103,2;
			mes "- Look for Ancellia. -";
			mes "- in Geffen. -";
			close;
		case 3:
			mes "- Juno Area -";
			mes "- I want to go to Juno, -";
			mes "- but the monsters on -";
			mes "- the way there are too -";
			mes "- strong for me. Come -";
			mes "- visit me in Al De Baran. -";
			mes "- My name is Jeanbai. -";
			callsub L_Quest,10104,3;
			mes "- Look for Jeanbai -";
			mes "- in Al De Baran. -";
			close;
		case 4:
			mes "- Clock Tower -";
			mes "- A Clock is the best thing! -";
			mes "- I'm asking you to hunt -";
			mes "- some monster inside of.-";
			mes "- the Clock Tower, -";
			mes "- Risingeter. -";
			callsub L_Quest,10105,4;
			mes "- Look for Risingeter -";
			mes "- in front of the -";
			mes "- Clock Tower. -";
			close;
		case 5:
			mes "- Localized Islands -";
			mes "- I need someone who -";
			mes "- can hunt monsters -";
			mes "- for Funfi in Alberta. -";
			callsub L_Quest,10106,5;
			mes "- Loof for Funfi -";
			mes "- in Alberta. -";
			close;
		}
	}
	mes "- Only members of the -";
	mes "- right level can read -";
	mes "- this bulletin board. -";
	mes "- Try to find a board -";
	mes "- that suits your level. -";
	close;
L_Quest:
	next;
	if(select("Accept mission.:Decline mission.") == 2) {
		mes "- I don't want to do -";
		mes "- a mission right now. -";
		close;
	}
	if (slv_quest && slv_quest != getarg(1)) {
		mes "- You are already on -";
		mes "- another mission. -";
		mes "- You can't do multiple -";
		mes "- missions. Finish your -";
		mes "- current mission first. -";
		close;
	}
	if (checkquest(getarg(0)) <= 0) {
		setquest getarg(0);
		set slv_quest,getarg(1);
		return;
	}
	mes "- You are already on -";
	mes "- or have recently -";
	mes "- completed a mission. -";
	mes "- You can't do anymore -";
	mes "- missions right now. -";
	close;
}

morocc,35,174,5	script	Meidi	729,{

	if ((checkquest(10107) == 2) && (checkquest(10108) == 2) && (checkquest(10109) == 2) && (slv_quest == 6)) {
		set slv_quest,0;
		mes "[Meidi]";
		mes "You have completed the hunting missions.";
		mes "I don't have anymore missions for you.";
		mes "Thanks, you did a great job!";
		close;
	}
	if (checkquest(10107) == 1 || checkquest(10108) == 1 || checkquest(10109) == 1) {
		mes "[Meidi]";
		mes "Wow! How did you come back so fast?";
		mes "What monster did you hunt?";
		next;
		setarray .@monsters$[0],"Requiem","Marduk","Pasana";
		set .@i, select(implode(.@monsters$,":"))-1;
		if (checkquest(10107+.@i,HUNTING) == 2) {
			mes "[Meidi]";
			mes "You've hunted all 10 "+.@monsters$[.@i]+".";
			mes "Thank you very much.";
			next;
			mes "[Meidi]";
			mes "I know it isn't much but here's a small gift.";
			mes "I hope that it will be helpful to you.";
			next;
			completequest 10107+.@i;
			getexp 48000,0;
			mes "[Meidi]";
			mes "Wooohoo!";
			mes "Good luck.";
			close;
		}
		mes "[Meidi]";
		mes "You didn't hunt 10 "+.@monsters$[.@i]+" yet.";
		mes "You should make sure that you hunt all of them.";
		mes "Can you check again?";
		close;
	}
	if (slv_quest == 6) {
		mes "[Meidi]";
		mes "Sphinx Dungeon... many adventurers came back with great injuries.";
		mes "That place is very dangerous.";
		mes "Someone should go inside an make it safer for everyone.";
		mes "Hopefully, very soon!";
		close;
	}
	mes "[Meidi]";
	mes "Sphinx Dungeon... many adventurers came back with great injuries.";
	mes "That place is very dangerous.";
	mes "Someone should go inside an make it safer for everyone.";
	mes "Hopefully, very soon!";
	if (slv_quest == 1) {
		next;
		mes "[Meidi]";
		mes "Oh! Are you from the Eden Group?";
		mes "Are you here for my mission?";
		mes "You came right on time.";
		mes "I was worried that noone would accept the mission that I posted.";
		next;
		mes "[Meidi]";
		mes "So, I need help hunting 3 monsters";
		mes "inside of the Sphinx Dungeon.";
		mes "Got it? Its too dangerous for me so that's why I posted the mission.";
		next;
		mes "[Meidi]";
		mes "Requiem, Marduk and Pasana...";
		mes "I heard they are very dangerous monsters.";
		mes "Can you hunt 10 of those monsters?";
		mes "10 of each sounds good enough, right?";
		next;
		mes "[Meidi]";
		mes "If you succeed, I will give you a small reward for your help.";
		next;
		switch (select("Accept mission.:Decline mission.")) {
		case 1:
			mes "[Meidi]";
			mes "Oh. Thank you so much.";
			mes "How can I pay you backf or your effort?";
			mes "May Freya bless you~";
			next;
			completequest 10102;
			setquest 10107;
			setquest 10108;
			setquest 10109;
			set slv_quest,6;
			mes "[Meidi]";
			mes "Requiem, Marduk and Pasana.";
			mes "Don't forget and good luck.";
			close;
		case 2:
			set slv_quest,0;
			erasequest 10102;
			mes "[Meidi]";
			mes "I see. There is no other way.";
			mes "I will send a message to the Eden Group.";
			mes "Don't worry and nevermind.";
			close;
		}
	}
	close;
}

geffen,146,132,3	script	Ancellia	906,{
	if ((checkquest(10110 == 2)) && (checkquest(10111) == 2) && (checkquest(10112) == 2) && (checkquest(10113) == 2) && (slv_quest == 6)) {
		set slv_quest,0;
		mes "[Ancellia]";
		mes "Wow. You've finished all";
		mes "of my missions. Great!";
		mes "You are of great help.";
		mes "I will inform the Eden Group of your assistance.";
		mes "You can go back, don't worry.";
		close;
	}
	if (checkquest(10110) == 1 || checkquest(10111) == 1 || checkquest(10112) == 1 || checkquest(10113) == 1) {
		mes "[Ancellia]";
		mes "You seem to have hunted all of the monsters.";
		mes "What monster have you hunted?";
		mes "Do you remember its name?";
		next;
		setarray .@monsters$[0],"Dark Frame","Evil Druid","Wraith","Raydric Archer";
		set .@i, select(implode(.@monsters$,":"))-1;
		if (checkquest(10110+.@i,HUNTING) == 2) {
			mes "[Ancellia]";
			mes "Ah, you've hunted 10 "+.@monsters$[.@i]+"s.";
			mes "That was pretty difficult!";
			mes "Thank you very much.";
			next;
			completequest 10110+.@i;
			getexp 48000,0;
			mes "[Ancellia]";
			mes "Perhaps you can hunt the rest as well.";
			mes "I will wait for you here.";
			close;
		}
		mes "[Ancellia]";
		mes "Hmm... maybe you were confused.";
		mes "You didn't hunt all 10 "+.@monsters$[.@i]+".";
		mes "Can you check again?";
		close;
	}
	if (slv_quest == 6) {
		mes "[Ancellia]";
		mes "Go to Glast Heim and kill";
		mes "Dark Frames, Evil Druids,";
		mes "Wraith and Raydric Archers.";
		mes "10 of each should do just fine.";
		close;
	}
	mes "[Ancellia]";
	mes "Glast Heim is very far away.";
	mes "I heared that the castle was cursed by the ancient King of Prontera.";
	mes "There is a another story.";
	next;
	mes "[Ancellia]";
	mes "One day the came out of the castle and attacked a nearby village, it was terrible.";
	mes "That's why I can't stop worrying.";
	if (slv_quest == 2) {
		next;
		mes "[Ancellia]";
		mes "Oh, you got my mission";
		mes "from the Eden Group. Great~!";
		mes "Thank you very much.";
		next;
		mes "[Ancellia]";
		mes "I already explained.";
		mes "So I expect you understand.";
		mes "You need to hurry hunting";
		mes "Dark Frames, Evil Druids,";
		mes "Wraith and Raydric Archesr.";
		next;
		mes "[Ancellia]";
		mes "You should hunt them all.";
		mes "These are dangerous monsters so please take care of yourself.";
		next;
		switch (select("Accept mission.:Decline mission.")) {
		case 1:
			mes "[Ancellia]";
			mes "Thanks a lot.";
			mes "Be careful when you hunt these monsters. Good luck.";
			next;
			completequest 10103;
			setquest 10110;
			setquest 10111;
			setquest 10112;
			setquest 10113;
			set slv_quest,6;
			mes "[Ancellia]";
			mes "Remember, Dark Frames,";
			mes "Evil Druids, Wraiths";
			mes "and Raydric Archers.";
			close;
		case 2:
			set slv_quest,0;
			erasequest 10103;
			mes "[Ancellia]";
			mes "Was the mission too hard?";
			mes "I guess I'll just have to wait for someone else from the Eden Group to help me.";
			close;
		}
	}
	close;
}

aldebaran,144,230,3	script	Jeanbai	900,{
	if ((checkquest(10114) == 2) && (checkquest(10115) == 2) && (checkquest(10116) == 2) && (checkquest(10117) == 2) && (slv_quest == 6)) {
		set slv_quest,0;
		mes "[Jeanbai]";
		mes "You've hunted all of the monsters.";
		mes "I will inform the Eden Group.";
		mes "Thank you.";
		close;
	}
	if (checkquest(10114) == 1 || checkquest(10115) == 1 || checkquest(10116) == 1 || checkquest(10117) == 1) {
		mes "[Jeanbai]";
		mes "Huh? So... fast...";
		mes "You amaze me!";
		mes "So what monster did you hunt?";
		next;
		setarray .@monsters$[0],"Grand Peco","Sleeper","Goat","Harpy";
		set .@i, select(implode(.@monsters$,":"))-1;
		if (checkquest(10114+.@i,HUNTING) == 2) {
			mes "[Jeanbai]";
			mes "You've hunted 20 "+.@monsters$[.@i]+"s. Perfect!";
			mes "Thanks a lot.";
			next;
			completequest 10114+.@i;
			getexp 48000,0;
			mes "[Jeanbai]";
			mes "Please hunt the others as soon as possible.";
			mes "I have faith in you!";
			close;
		}
		mes "[Jeanbai]";
		mes "What? Maybe you were confused.";
		mes "You haven't hunted all 20 "+.@monsters$[.@i]+"s.";
		mes "Please check again.";
		close;
	}
	if (slv_quest == 6) {
		mes "[Jeanbai]";
		mes "Grand Pecos, Sleepers, Goats";
		mes "and Harpies. Hunt 20 of each.";
		mes "When you're finished hunting, come back to me.";
		mes "I hope you finish as soon as you can.";
		close;
	}
	mes "[Jeanbai]";
	mes "I am traveler Jeanbai.";
	mes "As I said I'm a traveler not a adventurer so, I am not good at fighting.";
	mes "That's why I can't travel all over the world.";
	next;
	mes "[Jeanbai]";
	mes "Monsters are increasing in numbers so travelers like me have a hard time getting around.";
	mes "I wish I could travel around without having to worry about that.";
	if (slv_quest == 3) {
		next;
		mes "[Jeanbai]";
		mes "Oh, did you get my mission from the Eden Group?";
		mes "Great~! We don't have a lot of time.";
		mes "Let me explain.";
		next;
		mes "[Jeanbai]";
		mes "The monsters around Juno,";
		mes "Grand Pecos, Sleepers, Goats";
		mes "and Harpies. Please hunt 20 of them.";
		mes "I've been hesitating to visit Juno because of those monsters around there.";
		next;
		mes "[Jeanbai]";
		mes "Will you accept this mission?";
		mes "I hope you are the right person for the job.";
		mes "So what do you say?";
		next;
		switch (select("Accept mission.:Decline mission.")) {
		case 1:
			mes "[Jeanbai]";
			mes "Great!";
			mes "You won't regret it.";
			next;
			completequest 10104;
			setquest 10114;
			setquest 10115;
			setquest 10116;
			setquest 10117;
			set slv_quest,6;
			mes "[Jeanbai]";
			mes "When you're finished hunting, come back to me.";
			mes "I hope you finish as soon as you can.";
			close;
		case 2:
			set slv_quest,0;
			erasequest 10104;
			mes "[Jeanbai]";
			mes "Too bad. I'm disappointed.";
			mes "I should inform the Eden Group again.";
			mes "I see, nevermind.";
			close;
		}
	}
	close;
}

aldebaran,105,116,3	script	Risingeter	869,{
	if ((checkquest(10118) == 2) && (checkquest(10119) == 2) && (checkquest(10120 == 2)) && (slv_quest == 6)) {
		set slv_quest,0;
		mes "[Risingeter]";
		mes "You've completed all of my missions.";
		mes "Amazing~! I will inform the Eden Group.";
		mes "of your assistance. Thanks again!";
		close;
	}
	if (checkquest(10118) == 1 || checkquest(10119) == 1 || checkquest(10120) == 1) {
		mes "[Risingeter]";
		mes "Wow. Are you done?";
		mes "So fast~?! Unbelievable~";
		mes "You were the right person for this mission.";
		mes "Which monster did you hunt?";
		next;
		setarray .@monsters$[0],"Clock","Punk","Rideword";
		set .@i, select(implode(.@monsters$,":"))-1;
		if (checkquest(10118+.@i,HUNTING) == 2) {
			mes "[Risingeter]";
			mes "Good! You've hunted 15 "+.@monsters$[.@i]+"s.";
			mes "Ah, now I can hear the clock sound clearly...";
			mes "Tic!! toc!! tic!! toc!!...";
			next;
			completequest 10118+.@i;
			getexp 48000,0;
			mes "[Risingeter]";
			mes "Thanks a lot!";
			mes "I hope you can hunt the rest as well.";
			mes "Go as soon as you can!";
			close;
		}
		mes "[Risingeter]";
		mes "Uh? Something is wrong.";
		mes "15 "+.@monsters$[.@i]+"s, right?";
		mes "You better check how many you've hunted.";
		close;
	}
	if (slv_quest == 6) {
		mes "[Risingeter]";
		mes "Hunt 15 Clocks, Punks, and Ridewords.";
		mes "Please hurry.";
		close;
	}
	mes "[Risingeter]";
	mes "I like clocks so much.";
	mes "They move with regular rhythm";
	mes "making tic toc sounds.";
	mes "Ahah!! Hail to the clocks!";
	next;
	mes "[Risingeter]";
	mes "That's why I like the Clock Tower so much.";
	mes "It makes a really nice sound.";
	mes "I like it so much!";
	if (slv_quest == 4) {
		next;
		mes "[Risingeter]";
		mes "Uh? Have you come to help me solve my trouble.";
		mes "You already know I like clocks right?";
		mes "That's why I need your help to hunt these monsters.";
		next;
		mes "[Risingeter]";
		mes "There are too many monsters inside the Clock Tower.";
		mes "Their presence is destroying the Clock Tower.";
		next;
		mes "[Risingeter]";
		mes "Anyway. Can you help me?";
		mes "Please hunt 15 Clocks, Punks and Ridewords before it breaks down.";
		next;
		switch (select("Accept mission.:Decline mission.")) {
		case 1:
			completequest 10105;
			setquest 10118;
			setquest 10119;
			setquest 10120;
			set slv_quest,6;
			mes "[Risingeter]";
			mes "Ok! I expect you will do the right thing.";
			mes "Hunt 15 Clocks, Punks and Ridewords.";
			close;
		case 2:
			set slv_quest,0;
			erasequest 10105;
			mes "[Risingeter]";
			mes "You don't understand my love for clocks";
			mes "I will find another brave adventurer.";
			mes "I will inform the Eden Group.";
			close;
		}
	}
	close;
}

alberta,215,111,5	script	Funfi	828,{
	if ((checkquest(10121) == 2) && (checkquest(10122) == 2) && (checkquest(10123) == 2) && (slv_quest == 6)) {
		set slv_quest,0;
		mes "[Funfi]";
		mes "Um! Thank you so much for all your efforts!";
		mes "I will inform, the Eden Group about your assistance, don't worry!";
		mes "Hahahaha...";
		close;
	}
	if (checkquest(10121) == 1 || checkquest(10122) == 1 || checkquest(10123) == 1) {
		mes "[Funfi]";
		mes "Unbelievable~! So fast~!";
		mes "Which monster did you hunt?";
		next;
		setarray .@monsters$[0],"Kikimora","Miyabi Doll","Mi Gao";
		set .@i, select(implode(.@monsters$,":"))-1;
		if (checkquest(10121+.@i,HUNTING) == 2) {
			mes "[Funfi]";
			mes "Ok! You've hunted 15 "+.@monsters$[.@i]+"!";
			mes "You're the best!";
			next;
			completequest 10121+.@i;
			getexp 48000,0;
			mes "[Funfi]";
			mes "Please hunt the others as well.";
			mes "I have faith in you!";
			close;
		}
		mes "[Funfi]";
		mes "Oh~ No no~!!";
		mes "Listen carefully! Hunt 15 "+.@monsters$[.@i]+".";
		close;
	}
	if (slv_quest == 6) {
		mes "[Funfi]";
		mes "Moscovia's Kikimore,";
		mes "Miyabi Dolls from Amatsu";
		mes "and Mi Gaos from Louyang.";
		mes "Please hunt 15 of each of those monsters.";
		close;
	}
	mes "[Funfi]";
	mes "I am a man who wants to experience adventures. But for some reason I can't...";
	next;
	mes "[Funfi]";
	mes "There are a lot of funny things in the world... Wooo!";
	if (slv_quest == 5) {
		next;
		mes "[Funfi]";
		mes "Anyway are you from the Eden Group?";
		mes "You are? Cool! I've been waiting for you~!";
		next;
		mes "[Funfi]";
		mes "No questions! Just do it now!";
		mes "Hunt Kikimora in Moscovia,";
		mes "Miyabi Dolls in Amatsu";
		mes "and Mi Gaos in Louyang!";
		mes "Hunt 15 each of those monsters!";
		next;
		mes "[Funfi]";
		mes "Please~!! haha.";
		mes "Hurry up!";
		next;
		switch (select("Accept mission.:Decline mission.")) {
		case 1:
			completequest 10106;
			setquest 10121;
			setquest 10122;
			setquest 10123;
			set slv_quest,6;
			getitem 7167,10; //Mystery_Iron_Bit
			mes "[Funfi]";
			mes "Ok! Good! Hurry up.";
			mes "Hunt the monsters on each of those 3 islands.";
			next;
			mes "[Funfi]";
			mes "I will give you some materials for use in Moscovia.";
			mes "Go as soon as you can!";
			close;
		case 2:
			set slv_quest,0;
			erasequest 10106;
			mes "[Funfi]";
			mes "What? You don't want to work?! Gosh!";
			mes "I will inform the Eden Group of this!";
			close;
		}
	}
	close;
}
