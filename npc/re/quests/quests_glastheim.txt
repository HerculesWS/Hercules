//================= Hercules Script =======================================
//=       _   _                     _
//=      | | | |                   | |
//=      | |_| | ___ _ __ ___ _   _| | ___  ___
//=      |  _  |/ _ \ '__/ __| | | | |/ _ \/ __|
//=      | | | |  __/ | | (__| |_| | |  __/\__ \
//=      \_| |_/\___|_|  \___|\__,_|_|\___||___/
//================= License ===============================================
//= This file is part of Hercules.
//= http://herc.ws - http://github.com/HerculesWS/Hercules
//=
//= Copyright (C) 2016  Hercules Dev Team
//= Copyright (C) 2016  Ridley
//=
//= Hercules is free software: you can redistribute it and/or modify
//= it under the terms of the GNU General Public License as published by
//= the Free Software Foundation, either version 3 of the License, or
//= (at your option) any later version.
//=
//= This program is distributed in the hope that it will be useful,
//= but WITHOUT ANY WARRANTY; without even the implied warranty of
//= MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//= GNU General Public License for more details.
//=
//= You should have received a copy of the GNU General Public License
//= along with this program.  If not, see <http://www.gnu.org/licenses/>.
//=========================================================================
//= Nightmare Glastheim Dungeon
//================= Description ===========================================
//= NPCs related to Glastheim (Nightmare)
//================= Current Version =======================================
//= 1.0
//=========================================================================
glast_01,195,273,4	script	Hugin's Follower#n_gh	2_F_MAGICMASTER,{
	mes("[Hugin's Follower]");
	mes("I can't move between time and dimension like a master Hugin but I can pretend to move.");
	next;
	mes("[Hugin's Follower]");
	mes("What about this Glast Heim is born with powerful dark force?");
	next;
	if (select("I don't care", "That is interesting, please tell me more") != 1) {
		mes("[Hugin's Follower]");
		mes("As learning ability from Hugin, I just find out the path to Old Glast Heim of more powerful dark force.");
		next;
		mes("[Hugin's Follower]");
		mes("How is it? Do you want to explore there?");
		next;
		if (select("No, I want to stop it", "I really want to do that!") != 1) {
			mes("[Hugin's Follower]");
			mes("Sure that is not free~ I have to get something little bit. Hoho...");
			next;
			mes("[Hugin's Follower]");
			mes("I can send you just two place. How is it? Do you want to try?");
			next;
			.@menu = select("Cancel", "Monastery Cemetery (Lv100~130) - 5000z", "Corridor of 2nd floor (Over Lv140) - 10000z");
			switch (.@menu) {
			case 1:
				mes("[Hugin's Follower]");
				mes("Alright, please come back whenever you change mind. I think you will have lots of thing to talk with me...");
				close;
			case 2:
			case 3:
				if (.@menu == 2) {
					.@map$ = "gl_chyard_";
					.@x = 148;
					.@y = 144;
					.@zeny = 5000;
				} else {
					.@map$ = "gl_cas02_";
					.@x = 104;
					.@y = 31;
					.@zeny = 10000;
				}
				if (Zeny < .@zeny) {
					mes("[Hugin's Follower]");
					mes("What, did you ask me without money?");
					close;
				}
				Zeny -= .@zeny;
				warp .@map$, .@x, .@y;
				end;
			}
		}
	}
	mes("[Hugin's Follower]");
	mes("Well~ If you are not interested in it, never mind.");
	close;
}

gl_chyard_,148,149,4	script	Hugin's Follower#n_gh1	2_F_MAGICMASTER,{
	mes("[Hugin's Follower]");
	mes("You want to break?");
	mes("Ok, let's get out of here.?");
	next;
	if (select("Ah, wrong click... I mean I was not talk to you", "Please let me out from here") == 1) {
		mes("[Hugin's Follower]");
		mes("Alright just tell me whenever you want to leave.");
		close;
	}
	warp "glast_01", 210, 269;
	end;
}

gl_cas02_,104,36,4	duplicate(Hugin's Follower#n_gh1)	Hugin's Follower#n_gh2	2_F_MAGICMASTER
