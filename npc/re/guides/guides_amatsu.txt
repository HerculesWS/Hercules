//===== Hercules Script ======================================
//= Amatsu Guide
//===== By: ==================================================
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.1
//===== Compatible With: =====================================
//= Hercules
//===== Description: =========================================
//= [Official Conversion]
//= Guide for the city of Amatsu.
//===== Additional Comments: =================================
//= 1.0 First version, Renewal guide.
//= 1.1 Optimized. [Euphy]
//============================================================

amatsu,202,91,3	script	Amatsu Guide#amatsu	758,{
	mes "[Amachang]";
	mes "Welcome to ^8B4513Amatsu^000000,";
	mes "the town of kind towners";
	mes "and beautiful cherry blossoms.";
	next;
	mes "[Amachang]";
	mes "I'm Amachang,";
	mes "the 13th Miss Amatsu.";
	mes "I will guide you about town";
	mes "as Miss Amatsu.";
	mes "Please tell me";
	mes "if you want to know something.";
	while (1) {
		next;
		switch(select("[ Kafra Employee ]:[ Bar ]:[ Weapon Dealer ]:[ Tool Dealer ]:[ Amatsu Palace ]:[ Chef Assistant ]:[ Ninja Guild Building ]:[ Sea Captain ]:Remove Marks from Mini-Map:Cancel")) {
		case 1:
			callsub L_Mark,"^0000FFKafra Employee";
			viewpoint 1,102,149,0,0x0A82FF;
			break;
		case 2:
			callsub L_Mark,"^006400Bar";
			viewpoint 1,215,116,1,0xAAFF00;
			break;
		case 3:
			callsub L_Mark,"^008080Weapon Dealer";
			viewpoint 1,129,117,2,0x008080;
			break;
		case 4:
			callsub L_Mark,"^FF1493Tool Dealer";
			viewpoint 1,97,117,3,0xFF1493;
			break;
		case 5:
			callsub L_Mark,"^8B4513Amatsu Palace";
			viewpoint 1,87,235,4,0x8B4513;
			break;
		case 6:
			callsub L_Mark,"^8A2BE2Chef Assistant";
			viewpoint 1,206,150,5,0x8A2BE2;
			break;
		case 7:
			callsub L_Mark,"^4B0082Ninja Guild Building";
			viewpoint 1,148,137,6,0x4B0082;
			break;
		case 8:
			callsub L_Mark,"^00BFFFSea Captain";
			viewpoint 1,195,79,7,0x00BFFF;
			break;
		case 9:
			mes "[Amachang]";
			mes "I'll remove all marks from your mini-map.";
			mes "Is there anything else I can do for you?";
			viewpoint 2,1,1,0,0xFFFFFF;
			viewpoint 2,1,1,1,0xFFFFFF;
			viewpoint 2,1,1,2,0xFFFFFF;
			viewpoint 2,1,1,3,0xFFFFFF;
			viewpoint 2,1,1,4,0xFFFFFF;
			viewpoint 2,1,1,5,0xFFFFFF;
			viewpoint 2,1,1,6,0xFFFFFF;
			viewpoint 2,1,1,7,0xFFFFFF;
			break;
		case 10:
			mes "[Amachang]";
			mes "Enjoy your trip~!!";
			close;
		}
	}
	end;
L_Mark:
	mes "[Amachang]";
	mes "The "+getarg(0)+"^000000 is";
	mes "marked on your mini-map.";
	mes "Is there anything else I can do for you?";
	return;
}
