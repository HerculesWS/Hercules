//===== Hercules Script ======================================
//= Lighthalzen Guides
//===== By: ================================================== 
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= Hercules
//===== Description: ========================================= 
//= [Official Conversion]
//= Guides for the city of Lighthalzen.
//===== Additional Comments: ================================= 
//= 1.0 First version, Renewal guide.
//= 1.1 Optimized. [Euphy]
//============================================================ 

lighthalzen,207,310,5	script	Lighthalzen Guide#lhz01::GuideLighthazlen	852,{
	cutin "ein_soldier",2;
	mes "[Lighthalzen Guide]";
	mes "Welcome to ^8B4513Lighthalzen^000000,";
	mes "the Corporation City-State.";
	mes "If you need any guidance";
	mes "around the city, feel free";
	mes "to ask me and I'll do my";
	mes "very best to help you.";
	next;
	mes "[Lighthalzen Guide]";
	mes "Please be aware that I'm";
	mes "in charge of providing info";
	mes "regarding the West District";
	mes "of ^8B4513Lighthalzen^000000. Now, please";
	mes "select the location that you'd";
	mes "like to learn more about.";
	next;
	while (1) {
		switch(select("[ Main Facilities ]:[ Merchants & Helpers ]:Remove Marks from Mini-Map:Cancel")) {
		case 1:
			set .@loop,1;
			while (.@loop) {
				switch(select("[ Hotel ]:[ Bank ]:[ Airport ]:[ Weapon Shop ]:[ Department Store ]:[ Police Station ]:[ Jewelry Store ]:[ Cool Event Corp. ]:[ Somatology Laboratory ]:[ Alchemist Association ]:Previous Menu")) {
				case 1:
					callsub L_Mark,"^FF0000Hotel";
					viewpoint 1,160,217,0,0xFF0000;
					next;
					break;
				case 2:
					callsub L_Mark,"^FF4500Bank";
					viewpoint 1,203,257,1,0xFF4500;
					next;
					break;
				case 3:
					callsub L_Mark,"^DAA520Airport";
					viewpoint 1,262,75,2,0xDAA520;
					next;
					break;
				case 4:
					callsub L_Mark,"^7B68EEWeapon Shop";
					viewpoint 1,194,35,3,0x7B68EE;
					next;
					break;
				case 5:
					callsub L_Mark,"^B91A4DDepartment Store";
					viewpoint 1,202,106,4,0xB91A4D;
					next;
					break;
				case 6:
					callsub L_Mark,"^008000Police Station";
					viewpoint 1,234,276,5,0x008000;
					next;
					break;
				case 7:
					callsub L_Mark,"^0000FFJewelry Store";
					viewpoint 1,96,110,6,0x0000FF;
					next;
					break;
				case 8:
					callsub L_Mark,"^000080Cool Event Corp.";
					viewpoint 1,238,217,7,0x000080;
					next;
					break;
				case 9:
					callsub L_Mark,"^800080Somatology Laboratory";
					viewpoint 1,101,243,8,0x800080;
					next;
					break;
				case 10:
					callsub L_Mark,"^0A9696Alchemist Association";
					viewpoint 1,54,132,9,0x0A9696;
					next;
					break;
				case 11:
					set .@loop,0;
					break;
				}
			}
			break;
		case 2:
			set .@loop,1;
			while (.@loop) {
				switch(select("[ Armsmith ]:[ Seiayablem ]:[ Pet Groomer ]:[ Kafra Employees ]:[ Cool Event Corp. Staff ]:Previous Menu")) {
				case 1:
					mes "[Lighthalzen Guide]";
					mes "You may go to Fulerr,";
					mes "the ^0A9696Armsmith^000000";
					mes "on the 1st floor of the Weapon Shop.";
					viewpoint 1,194,35,10,0x0A9696;
					next;
					callsub L_Mark,"^0A9696Armsmith";
					next;
					break;
				case 2:
					mes "[Lighthalzen Guide]";
					mes "^4B0082Seiyablem^000000 adds";
					mes "sockets on your items";
					mes "on the 1st floor";
					mes "of the Weapon Shop.";
					viewpoint 1,194,35,11,0x4B0082;
					next;
					callsub L_Mark,"^4B0082Seiyablem",1;
					next;
					break;
				case 3:
					callsub L_Mark,"^FF4500Pet Groomer";
					viewpoint 1,222,191,12,0xFF4500;
					next;
					break;
				case 4:
					callsub L_Mark,"^008080Kafra Employees";
					viewpoint 1,164,99,13,0x008080;
					viewpoint 1,164,99,14,0x008080;
					next;
					break;
				case 5:
					callsub L_Mark,"^FF1493Cool Event Corp. Staff";
					viewpoint 1,94,247,15,0xFF1493;
					next;
					break;
				case 6:
					set .@loop,0;
					break;
				}
			}
			break;
		case 3:
			mes "[Lighthalzen Guide]";
			mes "Sure, I'll remove all marks from your mini-map.";
			mes "Is there anything else I can do for you?";
			viewpoint 2,160,217,0,0xFFFFFF;
			viewpoint 2,203,257,1,0xFFFFFF;
			viewpoint 2,262,75,2,0xFFFFFF;
			viewpoint 2,194,35,3,0xFFFFFF;
			viewpoint 2,202,106,4,0xFFFFFF;
			viewpoint 2,234,276,5,0xFFFFFF;
			viewpoint 2,96,110,6,0xFFFFFF;
			viewpoint 2,238,217,7,0xFFFFFF;
			viewpoint 2,101,243,8,0xFFFFFF;
			viewpoint 2,54,132,9,0xFFFFFF;
			viewpoint 2,194,35,10,0xFFFFFF;
			viewpoint 2,194,35,11,0xFFFFFF;
			viewpoint 2,222,191,12,0xFFFFFF;
			viewpoint 2,164,99,13,0xFFFFFF;
			viewpoint 2,164,99,14,0xFFFFFF;
			viewpoint 2,94,247,15,0xFFFFFF;
			next;
			break;
		case 4:
			mes "[Lighthalzen Guide]";
			mes "Enjoy your adventures!";
			close2;
			cutin "ein_soldier",255;
			end;
		}
	}
	end;
L_Mark:
	mes "[Lighthalzen Guide]";
	mes "I will mark the location";
	mes "of "+((getarg(1,0))?"":"the ")+getarg(0)+"^000000";
	mes "on your mini-map.";
	mes "Would you like to check any other locations?";
	return;
}

lighthalzen,220,310,3	duplicate(GuideLighthazlen)	Lighthalzen Guide#lhz02	852
lighthalzen,154,100,5	duplicate(GuideLighthazlen)	Lighthalzen Guide#lhz03	852
lighthalzen,307,224,3	duplicate(GuideLighthazlen)	Lighthalzen Guide#lhz04	852
