//================= Hercules Script =======================================
//=       _   _                     _
//=      | | | |                   | |
//=      | |_| | ___ _ __ ___ _   _| | ___  ___
//=      |  _  |/ _ \ '__/ __| | | | |/ _ \/ __|
//=      | | | |  __/ | | (__| |_| | |  __/\__ \
//=      \_| |_/\___|_|  \___|\__,_|_|\___||___/
//================= License ===============================================
//= This file is part of Hercules.
//= http://herc.ws - http://github.com/HerculesWS/Hercules
//=
//= Copyright (C) 2012-2016  Hercules Dev Team
//= Copyright (C)  Athena Dev Teams
//=
//= Hercules is free software: you can redistribute it and/or modify
//= it under the terms of the GNU General Public License as published by
//= the Free Software Foundation, either version 3 of the License, or
//= (at your option) any later version.
//=
//= This program is distributed in the hope that it will be useful,
//= but WITHOUT ANY WARRANTY; without even the implied warranty of
//= MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//= GNU General Public License for more details.
//=
//= You should have received a copy of the GNU General Public License
//= along with this program.  If not, see <http://www.gnu.org/licenses/>.
//=========================================================================
//= The Airship System Script
//================= Description ===========================================
//= The Airship System used in the official servers.
//=========================================================================

//=== Domestic Airship Route ===============================
// ... -> Juno -> Einbroch -> Lighthalzen -> Einbroch ->
// -> Juno -> Hugel -> ...
airplane,243,73,0	script	#AirshipWarp-1	WARPNPC,1,1,{
	end;

OnInit:
OnHide:
	specialeffect EF_BASH;
	disablenpc strnpcinfo(NPC_NAME);
	end;

OnUnhide:
	enablenpc strnpcinfo(NPC_NAME);
	specialeffect EF_SUMMONSLAVE;
	end;

OnTouch:
	switch ($@airplanelocation) {
	case 0: warp "yuno",92,260; end;
	case 1: warp "einbroch",92,278; end;
	case 2: warp "lighthalzen",302,75; end;
	case 3: warp "hugel",181,146; end;
	}
}

airplane,243,29,0	duplicate(#AirshipWarp-1)	#AirshipWarp-2	WARPNPC,1,1

airplane,1,1,0	script	Domestic_Airship	FAKE_NPC,{
	end;
OnInit:
	initnpctimer;
	end;
OnTimer20000:
	mapannounce("airplane", _("We are heading to Einbroch."), bc_map, "0x00ff00");
	end;
OnTimer50000:
	mapannounce("airplane", _("We will arrive in Einbroch shortly."), bc_map, "0x00ff00");
	end;
OnTimer60000:
	$@airplanelocation = 1;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce("airplane", _("Welcome to Einbroch. Have a safe trip."), bc_map, "0x00ff00");
	end;
OnTimer70000:
	mapannounce("airplane", _("Currently we are in Einbroch. The Airship will take off shortly."), bc_map, "0x00ff00");
	end;
OnTimer80000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce("airplane", _("The Airship is now taking off. Our next destination is Lighthalzen."), bc_map, "0x70dbdb");
	end;
OnTimer100000:
	mapannounce("airplane", _("We are heading to Lighthalzen."), bc_map, "0x70dbdb");
	end;
OnTimer130000:
	mapannounce("airplane", _("We will arrive in Lighthalzen shortly."), bc_map, "0x70dbdb");
	end;
OnTimer140000:
	$@airplanelocation = 2;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce("airplane", _("Welcome to Lighthalzen. Have a safe trip."), bc_map, "0x70dbdb");
	end;
OnTimer150000:
	mapannounce("airplane", _("Currently we are in Lighthalzen. The Airship will leave shortly."), bc_map, "0x70dbdb");
	end;
OnTimer160000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce("airplane", _("The Airship is leaving the ground. Our next destination is Einbroch."), bc_map, "0x00ff00");
	end;
OnTimer180000:
	mapannounce("airplane", _("We are heading to Einbroch."), bc_map, "0x00ff00");
	end;
OnTimer210000:
	mapannounce("airplane", _("We will arrive in Einbroch shortly."), bc_map, "0x00FF00");
	end;
OnTimer220000:
	$@airplanelocation = 1;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce("airplane", _("Welcome to Einbroch. Have a safe trip."), bc_map, "0x00ff00");
	end;
OnTimer230000:
	mapannounce("airplane", _("Currently we are in Einbroch. The Airship will take off shortly."), bc_map, "0x00ff00");
	end;
OnTimer240000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce("airplane", _("The Airship is now taking off. Our next destination is Juno."), bc_map, "0xff8200");
	end;
OnTimer260000:
	mapannounce("airplane", _("We are heading to Juno."), bc_map, "0xff8200");
	end;
OnTimer290000:
	mapannounce("airplane", _("We will arrive in Juno shortly."), bc_map, "0xff8200");
	end;
OnTimer300000:
	$@airplanelocation = 0;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce("airplane", _("Welcome to Juno. Have a safe trip."), bc_map, "0xff8200");
	end;
OnTimer310000:
	mapannounce("airplane", _("Currently we are in Juno. The Airship will leave shortly."), bc_map, "0xff8200");
	end;
OnTimer320000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce("airplane", _("The Airship is leaving the ground. Our next destination is Hugel."), bc_map, "0xca4bf3");
	end;
OnTimer340000:
	mapannounce("airplane", _("We are heading to Hugel."), bc_map, "0xca4bf3");
	end;
OnTimer370000:
	mapannounce("airplane", _("We will arrive in Hugel shortly."), bc_map, "0xca4bf3");
	end;
OnTimer380000:
	$@airplanelocation = 3;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce("airplane", _("Welcome to Hugel. Have a safe trip."), bc_map, "0xca4bf3");
	end;
OnTimer390000:
	mapannounce("airplane", _("Currently we are in Hugel. The Airship will leave shortly."), bc_map, "0xca4bf3");
	end;
OnTimer400000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce("airplane", _("The Airship is leaving the ground. Our next destination is Juno."), bc_map, "0xff8200");
	end;
OnTimer420000:
	mapannounce("airplane", _("We are heading to Juno."), bc_map, "0xff8200");
	end;
OnTimer450000:
	mapannounce("airplane", _("We will arrive in Juno shortly."), bc_map, "0xff8200");
	end;
OnTimer460000:
	$@airplanelocation = 0;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce("airplane", _("Welcome to Juno. Have a safe trip."), bc_map, "0xff8200");
	end;
OnTimer470000:
	mapannounce("airplane", _("Currently we are in Juno. The Airship will leave shortly."), bc_map, "0xff8200");
	end;
OnTimer480000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce("airplane", _("The Airship is leaving the ground. Our next destination is Einbroch."), bc_map, "0x00ff00");
	stopnpctimer;
	initnpctimer;
}

airplane,240,64,5	script	Exit#airplane1a::ExitAirplane	4_BOARD3,{
	end;
}
airplane,247,64,5	duplicate(ExitAirplane)	Exit#airplane1b	4_BOARD3
airplane,240,40,1	duplicate(ExitAirplane)	Exit#airplane2a	4_BOARD3
airplane,247,40,1	duplicate(ExitAirplane)	Exit#airplane2b	4_BOARD3

airplane,100,69,3	script	Airship Crew#ein-1	4_M_EIN_SOLDIER,{
	mes("[Airship Crew]");
	mes("If we've landed at\r"
		"your destination and\r"
		"you'd like to leave the\r"
		"Airship, please use the\r"
		"stairs up ahead. Thank\r"
		"you for your patronage.");
	close;
}

airplane,64,94,1	script	Umbala Kid#ein_p	4_M_UMKID,{
	emotion e_swt2;
	mes("[Kid]");
	if (event_umbala >= 3) {
		mes("Wow, mom!");
		mes("L-look at this!");
		mes("We're flying! W-we're...");
		mes("We're in the freakin' sky!");
	} else {
		mes("Makumalagu!");
		mes("Saampa joojimbo");
		mes("kaku na jedi Solo.");
		mes("Bwahahahahahahaah!");
	}
	close;
}

airplane,66,93,3	script	Umbala Lady#ein_p	4_F_UMWOMAN,{
	emotion e_dots;
	mes("[Lady]");
	if (event_umbala >= 3) {
		mes("Shush...");
		mes("Honey, behave~");
		mes("Don't act so excited\r"
			"when we're out in a\r"
			"public place like this!");
	} else {
		mes("Chooktu!");
		mes("Sacraup matii!");
		mes("Shaka gurftalfi\r"
			"huntiki manjoo!");
	}
	close;
}

airplane,71,91,7	script	Umbala Man#ein_p	4_M_UMSOLDIER,{
	if (event_umbala >= 3) {
		mes("[Chrmlim]");
		mes("Hey there~");
		mes("From that look on\r"
			"your face, I see that\r"
			"you can understand\r"
			"me. ^333333*Whew...!*^000000");
		next;
		emotion e_pif;
		mes("[Chrmlim]");
		mes("I've been helping the\r"
			"Airship enterprise by\r"
			"having the Airship Crewmen\r"
			"train in Umbala to overcome\r"
			"any acrophobia they might have through bungee jumping. Neat, eh?");
		next;
		mes("[Chrmlim]");
		mes("But...");
		mes("Some of them couldn't\r"
			"overcome their fear of\r"
			"heights. And a few even\r"
			"ended up, um, ^333333in Nifflheim^000000.");
	} else {
		mes("[Chrmlim]");
		mes("Bajoo ga\r"
			"nukta Airship.");
		next;
		mes("[Chrmlim]");
		mes("...");
		mes("......");
		next;
		emotion e_pif;
		mes("[Chrmlim]");
		mes("Shabala moow bajama\r"
			"Airship kulaha googoona\r"
			"salu. Dama, kookoo na nu\r"
			"yukuta. Um, fashuku na ret!");
	}
	close;
}

airplane,250,58,2	script	Airship Staff#airplane	1_F_02,{
	// Hugel quest addition
	if (hg_ma1 == 3) {
		mes("[Airship Staff]");
		mes("Welcome\r"
			"to the Airship.");
		mes("How may I help you?");
		next;
		select("Do you have a passenger named Thierry?");
		mes("[Airship Staff]");
		mes("I am sorry, but I do not think that we have a passenger by that name.");
		close;
	}
	// Hugel quest end
	mes("[Airship Staff]");
	mes("Welcome\r"
		"to the Airship.");
	mes("How may I help you?");
	next;
	switch (select("Using the Airship", "Captain's Cabin", "Facilities", "Cancel")) {
	case 1:
		mes("[Airship Staff]");
		mes("When you see a broadcast\r"
			"announcing that we have\r"
			"arrived at your destination,\r"
			"please use one of the exits\r"
			"located at the north and\r"
			"south ends of the Airship.");
		next;
		mes("[Airship Staff]");
		mes("If you happen to miss\r"
			"your stop, don't worry.\r"
			"The Airship is constantly\r"
			"en route and you'll get\r"
			"another chance to arrive\r"
			"to your intended destination.");
		close;
	case 2:
		mes("[Airship Staff]");
		mes("The Captain's Cabin\r"
			"is located at the front\r"
			"of the Airship. There, you\r"
			"can meet the captain and\r"
			"the pilot of the Airship.");
		close;
	case 3:
		mes("[Airship Staff]");
		mes("The Airship provides\r"
			"various Mini Games for\r"
			"the entertainment of all\r"
			"our passengers. We invite\r"
			"you to try your luck and skills\r"
			"in the Airship's Mini Games~");
		close;
	case 4:
		mes("[Airship Staff]");
		mes("Well, I hope you\r"
			"your flight aboard\r"
			"our Airships. Thank\r"
			"you and have a good day.");
		close;
	}
}

airplane,80,71,2	script	Zerta#01airplane	4_M_BUDDHIST,{
	mes("[Zerta]");
	mes("Oh, hello adventurer.\r"
		"I am currently on a\r"
		"sacred journey, offering\r"
		"prayer for the sake of the\r"
		"Rune-Midgard continent.");
	close;
}

airplane,65,63,4	script	Maelin#01airplane	4_F_06,{
	mes("[Maelin]");
	mes("Um, this Airship is\r"
		"to Lutie, isn't it? I've\r"
		"waiting so long,\r"
		"but I haven't heard any\r"
		"broadcast about Lutie.");
	close;
}

airplane,72,34,6	script	Aanos#01airplane	4_F_GON,{
	mes("[Aanos]");
	mes("Oh wooow~");
	mes("The sky looks\r"
		"so different and\r"
		"pretty from up there!\r");
	close;
}

airplane,221,158,2	script	Pilot#airplane	4_M_EIN_SOLDIER,{
	// Hugel quest addition
	if (hg_ma1 == 3) {
		mes("[Pilot]");
		mes("I wish that I could go drink a cold fresh beer.");
		mes("Drinking is the goal of my life! Drinking gives me energy!");
		mes("I am nothing without drinks!");
		next;
		mes("[Pilot]");
		mes("But! Driving under the influence is not good.");
		mes("But! That makes me want to drink more and more!");
		emotion e_sob;
		next;
		select("Do you know a passenger named Thierry?");
		mes("[Pilot]");
		mes("This uniform is\r"
			"really dapper, but\r"
			"it's way too thick to\r"
			"wear around the Airship.");
		next;
		mes("[Pilot]");
		mes("...");
		mes("......");
		mes("No one ever really\r"
			"comes into this room.");
		mes("And the captain IS a reindeer. I could just strip to my boxers.");
		next;
		emotion e_omg;
		mes("[Pilot]");
		mes("Wah!? Who is it!");
		next;
		mes("- ...He is not listening to you, at all. -");
		close;
	}
	// Hugel quest end
	switch (rand(1,4)) {
	case 1:
		mes("[Pilot]");
		mes("It's been sooo\r"
			"long since I've\r"
			"enjoyed a nice, cold\r"
			"alcoholic brew. But the\r"
			"job requires me to be as\r"
			"clear headed as I can!");
		next;
		mes("[Pilot]");
		mes("Always drink responsibly!");
		mes("Still, I can't remember the\r"
			"last time I had a real vacation\r"
			"or even a day off. Yeap, some\r"
			"booze, some chips, some TV\r"
			"and serius R&R is in order.");
		emotion e_sob;
		close;
	case 2:
		mes("[Pilot]");
		mes("Man, the weather\r"
			"is really nice today.");
		mes("Bright, open skies make\r"
			"for some good visibility\r"
			"and safe, carefree flying.");
		close;
	case 3:
		mes("[Pilot]");
		mes("You know, our captain's a\r"
			"respectable guy. Him and\r"
			"his brother are actually well\r"
			"known in the aircraft industry.");
		mes("Who knew reindeer made\r"
			"such good captains?");
		next;
		mes("[Pilot]");
		mes("Just between you\r"
			"and me, I gotta tell\r"
			"you, that Santa was onto\r"
			"something, getting reindeers\r"
			"and elves to work for him.");
		mes("The man must be a genius!");
		close;
	default:
		mes("[Pilot]");
		mes("You know, this whole\r"
			"piloting thing in the air,\r"
			"it's rather new, you know?");
		mes("Yeah, they got this Airship\r"
			"operation in a hurry.");
		next;
		emotion e_omg;
		mes("[Pilot]");
		mes("Still, they where real\r"
			"serius, really thought\r"
			"ahead. I mean, they had us\r"
			"training while the Airships\r"
			"were still being invented.");
		mes("Isn't that freakin' crazy?!");
		close;
	}
}

airplane,50,66,5	script	Apple Merchant#airplane	4_M_04,{
	mes("[Fruitz]");
	mes("Welcome to Fruitz's\r"
		"Shop where you can\r"
		"purchase Apples or grind\r"
		"them to make Apple Juice.");
	next;
	switch (select("Buy Apples.", "Make Apple Juice.", "Why are you here?", "Cancel.")) {
	case 1:
		mes("[Fruitz]");
		mes("Please enter the amount\r"
			"of Apples that you wish to\r"
			"buy. Each Apple is 15 zeny\r"
			"and you can buy a maximum\r"
			"of 500 at a time. Please enter\r"
			" '0' to cancel your order.");
		next;
		while (1) {
			input .@input;
			.@pay = .@input * 15;
			if (.@input == 0) {
				mes("[Fruitz]");
				mes("Thanks for stopping\r"
					"by my shop. Farewell!");
				mes("Come by anytime when\r"
					"you feel like having an\r"
					"Apple to snack on~");
				close;
			}
			else if (.@input < 1 || .@input > 500) {
				mes("[Fruitz]");
				mes("You've entered a number\r"
					"higher than the maximum\r"
					"value of 500. Please enter\r"
					"the number of Apples you\r"
					"wish to purchase again.");
				next;
			}
			else {
				mes("[Fruitz]");
				mesf("A total of ^FF0000%d^000000 Apples\r"
					"will cost you ^FF0000%d Zeny^000000.",
					.@input, .@pay);
				mes("Would you like to continue?");
				next;
				if (select("Yes", "No") == 2) {
					mes("[Fruitz]");
					mes("Thanks for stopping\r"
						"by my shop. Farewell!");
					mes("Come by anytime when\r"
						"you feel like having an\r"
						"Apple to snack on~");
					close;
				}
				break;
			}
		}
		if (Zeny < .@pay) {
			mes("[Fruitz]");
			mes("I'm sorry, but you don't\r"
				"have enough money to\r"
				"purchase that many Apples.");
			mes("Please check your zeny or\r"
				"purchase fewer Apples.");
			close;
		}
		else if (checkweight(Apple,.@input) == 0) {
			mes("[Fruitz]");
			mes("Hmmm, I don't think\r"
				"you've got enough room in\r"
				"your inventory to carry this\r"
				"many Apples. Why don't you free up some of your inventory space?");
			close;
		}
		else {
			Zeny -= .@pay;
			getitem Apple,.@input;
			mes("[Fruitz]");
			mes("Thanks for stopping by\r"
				"my shop. I hope you enjoy\r"
				"the flavor of these Apples~!");
			close;
		}
	case 2:
		mes("[Fruitz]");
		mes("Okay, I'll need\r"
			"^FF00003 Apples and 1 Empty Bottle^000000\r"
			"to make 1 Apple Juice for you.");
		mes("Would you like to proceed?");
		next;
		switch (select("Yes", "No")) {
		case 1:
			if (countitem(Apple) < 3 || countitem(Empty_Bottle) < 1) {
				mes("[Fruitz]");
				mes("I'm sorry, but you don't\r"
					"have enough materials to\r"
					"create a bottle of Apple Juice.");
				mes("Remember, I need 3 Apples\r"
					"and 1 Empty Bottle to do it.");
				close;
			}
			else {
				mes("[Fruitz]");
				mes("Thank you,\r"
					"please wait\r"
					"just a moment.");
				next;
				mes("^3355FF*Grind grind*");
				mes("*Grind grind*");
				mes("*Clang...!*^000000");
				next;
				delitem Apple,3;
				delitem Empty_Bottle,1;
				getitem Apple_Juice,1;
				mes("[Fruitz]");
				mes("There you go~");
				mes("I hope you enjoy!");
				mes("Please feel free to\r"
					"stop by for your Apple\r"
					"and Apple Juice needs\r"
					"at anytime, adventurer~");
				close;
			}
		case 2:
			mes("[Fruitz]");
			mes("Thanks for stopping\r"
				"by my shop. Farewell!");
			mes("Come by anytime when\r"
				"you feel like having an\r"
				"Apple to snack on~");
			close;
		}
	case 3:
		mes("[Fruitz]");
		mes("I used to be a wandering\r"
			"vagabond when, one day,\r"
			"I took a nap and something\r"
			"struck my head and awoke\r"
			"me from my restful slumber.");
		next;
		mes("[Fruitz]");
		mes("It turns out that I was\r"
			"sleeping beneath an apple\r"
			"tree and that an apple fell\r"
			"and hit me on the head.");
		mes("I was dying of hunger and\r"
			"was about to eat that Apple...");
		next;
		mes("[Fruitz]");
		mes("But suddenly, Kain, my old\r"
			"friend from the mining days,\r"
			"asked me to help him around\r"
			"on the Airship. So I did, and\r"
			"it was there where I found some\r"
			"people playing the Dice game.");
		next;
		mes("[Fruitz]");
		mes("I was bored and curious\r"
			"and ended up wagering that\r"
			"single Apple in a game of\r"
			"dice. But for some reason,\r"
			"I had this incredible lucky\r"
			"streak. One apple became two...");
		next;
		mes("[Fruitz]");
		mes("Two became four and\r"
			"before I knew it, I had\r"
			"cornered the Apple market!");
		mes("I won so many Apples, I just\r"
			"started my own business here\r"
			"on the Airship. Weird, huh?");
		next;
		mes("[Fruitz]");
		mes("So Apples are good\r"
			"for you. They were\r"
			"certainly very good\r"
			"to me. Hahahahaah~!");
		close;
	case 4:
		mes("[Fruitz]");
		mes("Thank you for\r"
			"using my shop.");
		mes("Farewell~");
		close;
	}
}

//== International Airship Route ===========================
// ... -> Izlude -> Juno -> Rachel -> ...
airplane_01,243,73,0	script	#AirshipWarp-3	WARPNPC,1,1,{
	end;

OnTouch:
	switch ($@airplanelocation2) {
	case 0: warp "ra_fild12",292,204; end;
	case 1:
		if (RENEWAL)
			warp "izlude",200,73;
		else
			warp "izlude",200,56;
		end;
	case 2: warp "yuno",12,261; end;
	}

OnInit:
OnHide:
	specialeffect EF_BASH;
	disablenpc strnpcinfo(NPC_NAME);
	end;

OnUnhide:
	enablenpc strnpcinfo(NPC_NAME);
	specialeffect EF_SUMMONSLAVE;
	end;
}

airplane_01,243,29,0	duplicate(#AirshipWarp-3)	#AirshipWarp-4	WARPNPC,1,1

airplane_01,1,1,0	script	International_Airship	FAKE_NPC,{
	end;
OnInit:
OnEnable:
	initnpctimer;
	end;
OnTimer25000:
	mapannounce("airplane_01", _("We are heading to Izlude."), bc_map, "0x00ff00");
	end;
OnTimer50000:
	mapannounce("airplane_01", _("We will arrive in Izlude shortly."), bc_map, "0x00ff00");
	end;
OnTimer60000:
	$@airplanelocation2 = 1;
	donpcevent "#AirshipWarp-3::OnUnhide";
	donpcevent "#AirshipWarp-4::OnUnhide";
	mapannounce("airplane_01", _("Welcome to Izlude. Have a safe trip."), bc_map, "0x00ff00");
	end;
OnTimer70000:
	mapannounce("airplane_01", _("We are currently in Izlude. The Airship will take off shortly."), bc_map, "0x00ff00");
	end;
OnTimer80000:
	donpcevent "#AirshipWarp-3::OnHide";
	donpcevent "#AirshipWarp-4::OnHide";
	mapannounce("airplane_01", _("The Airship is now taking off. Our next destination is Juno."), bc_map, "0x70dbdb");
	end;
OnTimer105000:
	mapannounce("airplane_01", _("We are heading to Juno."), bc_map, "0x70dbdb");
	end;
OnTimer130000:
	mapannounce("airplane_01", _("We will arrive in Juno shortly."), bc_map, "0x70dbdb");
	end;
OnTimer140000:
	$@airplanelocation2 = 2;
	donpcevent "#AirshipWarp-3::OnUnhide";
	donpcevent "#AirshipWarp-4::OnUnhide";
	mapannounce("airplane_01", _("Welcome to Juno. Have a safe trip."), bc_map, "0x70dbdb");
	end;
OnTimer150000:
	mapannounce("airplane_01", _("We are currently in Juno. The Airship will leave shortly."), bc_map, "0x70dbdb");
	end;
OnTimer160000:
	donpcevent "#AirshipWarp-3::OnHide";
	donpcevent "#AirshipWarp-4::OnHide";
	mapannounce("airplane_01", _("The Airship is leaving the ground. Our next destination is Rachel."), bc_map, "0xFF8200");
	end;
OnTimer185000:
	mapannounce("airplane_01", _("We are heading to Rachel."), bc_map, "0xFF8200");
	end;
OnTimer210000:
	mapannounce("airplane_01", _("We will arrive in Rachel shortly."), bc_map, "0xFF8200");
	end;
OnTimer220000:
	$@airplanelocation2 = 0;
	donpcevent "#AirshipWarp-3::OnUnhide";
	donpcevent "#AirshipWarp-4::OnUnhide";
	mapannounce("airplane_01", _("Welcome to Rachel. Have a safe trip."), bc_map, "0xFF8200");
	end;
OnTimer230000:
	mapannounce("airplane_01", _("We are currently in Rachel. The Airship will take off shortly."), bc_map, "0xFF8200");
	end;
OnTimer240000:
	donpcevent "#AirshipWarp-3::OnHide";
	donpcevent "#AirshipWarp-4::OnHide";
	mapannounce("airplane_01", _("The Airship is now taking off. Our next destination is Izlude."), bc_map, "0x00ff00");
	stopnpctimer;
	++.moninv;
	if (.moninv == 7) {
		if (rand(1,3) == 3) {
			donpcevent "Airship#airplane02::OnEnable";
			end;
		}
		.moninv = 0;
	}
	initnpctimer;
	end;
}

airplane_01,240,64,5	script	Exit#airplane_011a::ExitAirplane01	4_BOARD3,{
	end;
}
airplane_01,247,64,5	duplicate(ExitAirplane01)	Exit#airplane_011b	4_BOARD3
airplane_01,240,40,1	duplicate(ExitAirplane01)	Exit#airplane_012a	4_BOARD3
airplane_01,247,40,1	duplicate(ExitAirplane01)	Exit#airplane_012b	4_BOARD3

airplane_01,250,58,2	script	Airship Staff#airplane01	1_F_02,{
	mes("[Airship Staff]");
	mes("Welcome\r"
		"to the Airship.");
	mes("How may I help you?");
	next;
	switch (select("Using the Airship", "Captain's Cabin", "Facilities", "Cancel")) {
	case 1:
		mes("[Airship Staff]");
		mes("When you see a broadcast\r"
			"announcing that we have\r"
			"arrived at your destination,\r"
			"please use one of the exits\r"
			"located at the north and\r"
			"south ends of the Airship.");
		next;
		mes("[Airship Staff]");
		mes("If you happen to miss\r"
			"your stop, don't worry.");
		mes("The Airship is constantly\r"
			"en route and you'll get\r"
			"another chance to arrive\r"
			"to your intended destination.");
		close;
	case 2:
		mes("[Airship Staff]");
		mes("The Captain's Cabin\r"
			"is located at the front\r"
			"of the Airship. There, you\r"
			"can meet the captain and\r"
			"the pilot of the Airship.");
		close;
	case 3:
		mes("[Airship Staff]");
		mes("The Airship provides\r"
			"various Mini Games for\r"
			"the entertainment of all\r"
			"our passengers. We invite\r"
			"you to try your luck and skills\r"
			"in the Airship's Mini Games~");
		close;
	case 4:
		mes("[Airship Staff]");
		mes("Well, I hope you\r"
			"your flight aboard\r"
			"our Airships. Thank\r"
			"you and have a good day.");
		close;
	}
}

airplane_01,50,66,5	script	Apple Merchant#air01	4_M_04,{
	if (checkweight(Knife,1) == 0) {
		mes("- Wait a minute !! -");
		mes("- Currently you're carrying -");
		mes("- too many items with you. -");
		mes("- Please try again -");
		mes("- after you loose some weight. -");
		close;
	}
	mes("[Meltz]");
	mes("Welcome to Meltz's\r"
		"Shop where you can\r"
		"purchase Apples or grind\r"
		"them to make Apple Juice.");
	next;
	switch (select("Buy Apples.", "Make Apple Juice.", "Cancel.")) {
	case 1:
		mes("[Meltz]");
		mes("Please enter the amount\r"
			"of Apples that you wish to\r"
			"buy. Each Apple is 15 zeny\r"
			"and you can buy a maximum\r"
			"of 500 at a time. Please enter\r"
			"'0' to cancel your order.");
		next;
		while (1) {
			input .@input;
			.@pay = .@input * 15;
			if (.@input == 0) {
				mes("[Meltz]");
				mes("Thanks for stopping\r"
					"by my shop. Farewell!");
				mes("Come by anytime when\r"
					"you feel like having an\r"
					"Apple to snack on~");
				close;
			}
			else if (.@input < 1 || .@input > 500) {
				mes("[Meltz]");
				mes("You've entered a number\r"
					"higher than the maximum\r"
					"value of 500. Please enter\r"
					"the number of Apples you\r"
					"wish to purchase again.");
				next;
			}
			else {
				mes("[Meltz]");
				mesf("A total of ^FF0000%d^000000 Apples\r"
					"will cost you ^FF0000%d Zeny^000000.",
					.@input, .@pay);
				mes("Would you like to continue?");
				next;
				if (select("Yes", "No") == 2) {
					mes("[Meltz]");
					mes("Thanks for stopping\r"
						"by my shop. Farewell!");
					mes("Come by anytime when\r"
						"you feel like having an\r"
						"Apple to snack on~");
					close;
				}
				break;
			}
		}
		if (Zeny < .@pay) {
			mes("[Meltz]");
			mes("I'm sorry, you don't have\r"
				"enough money with you.");
			mes("Please check your funds or\r"
				"purchase less Apples.");
			close;
		}
		else if (checkweight(Apple,.@input) == 0) {
			mes("[Meltz]");
			mes("Hmm, I don't think you've\r"
				"got enough room to carry\r"
				"this many Apples. You might\r"
				"want to free up your inventory\r"
				"space.");
			close;
		}
		else {
			Zeny -= .@pay;
			getitem Apple,.@input;
			mes("[Meltz]");
			mes("Thanks for stopping by\r"
				"my shop. I hope you enjoy\r"
				"the flavor of these Apples~!");
			close;
		}
	case 2:
		mes("[Meltz]");
		mes("Okay, I'll need\r"
			"^FF00003 Apples and 1 Empty Bottle^000000\r"
			"to make 1 Apple Juice for you.");
		mes("Would you like to proceed?");
		next;
		switch (select("Yes", "No")) {
		case 1:
			if (countitem(Apple) < 3 || countitem(Empty_Bottle) < 1) {
				mes("[Meltz]");
				mes("I'm sorry, but you don't\r"
					"have enough materials to\r"
					"create a bottle of Apple Juice.");
				mes("Remember, I need 3 Apples\r"
					"and 1 Empty Bottle to do it.");
				close;
			}
			else {
				mes("[Meltz]");
				mes("Thank you, please wait.");
				next;
				mes("^3355FF*Grind* *Grind*");
				mes("*Grind* *Grind*");
				mes("*Clang...!*^000000");
				next;
				delitem Apple,3;
				delitem Empty_Bottle,1;
				getitem Apple_Juice,1;
				mes("[Meltz]");
				mes("There you go~");
				mes("Please come again.");
				close;
			}
		case 2:
			mes("[Meltz]");
			mes("Thanks for stopping\r"
				"by my shop. Farewell!");
			mes("Come by anytime when\r"
				"you feel like having an\r"
				"Apple to snack on~");
			close;
		}
	case 3:
		mes("[Meltz]");
		mes("Thanks for stopping\r"
			"by my shop. Farewell!");
		mes("Come by anytime when\r"
			"you feel like having an\r"
			"Apple to snack on~");
		close;
	}
}

airplane_01,221,158,2	script	Pilot#airplane_01	4_M_EIN_SOLDIER,{
	switch (rand(1,4)) {
	case 1:
		mes("[Pilot]");
		mes("Longitude, 131 degrees east.");
		mes("Latitude, 37 degrees north.");
		mes("We're right on course, captain.");
		close;
	case 2:
		mes("[Pilot]");
		mes("Looks like a really\r"
			"cloudy day. Always hard\r"
			"to navigate when the skies\r"
			"aren't clear. Guess we'll\r"
			"need to amp the radar.");
		close;
	case 3:
		mes("[Pilot]");
		mes("The Captain is a good\r"
			"man and I can't think of\r"
			"a finer person to command\r"
			"this ship. Still, he's pretty\r"
			"tough, a real slave driver.");
		next;
		mes("[^ff0000Tarlock^000000]");
		mes("^ff0000Hey...!^000000");
		mes("^ff0000Less chit-chat^000000");
		mes("^ff0000and more piloting!^000000");
		next;
		mes("[Pilot]");
		mes("R-right away, sir!");
		mes("(See what I mean?)");
		close;
	default:
		mes("[Pilot]");
		mes("This uniform is\r"
			"really dapper, but\r"
			"it's way too thick to\r"
			"wear around the Airship.");
		next;
		mes("[Pilot]");
		mes("...");
		mes("......");
		mes("No one ever really\r"
			"comes into this room.");
		mes("And the captain IS a reindeer.");
		mes("I could just strip to my boxers.");
		next;
		emotion e_omg;
		mes("[Pilot]");
		mes("Oh...! Hello there!");
		mes("E-e-enjoying your flight?!");
		close;
	}
}

airplane_01,83,61,2	script	Dianne#01airplane_01	1_F_MARIA,2,2,{
	mes("[Dianne]");
	mes("It's so weird!");
	mes("I went to visit the\r"
		"Airship Captain and\r"
		"all I saw was this\r"
		"weird reindeer. Oh!");
	mes("Do you think that...");
	close;

OnTouch:
	emotion e_sob;
	end;
}

airplane_01,69,63,2	script	Mendel#01airplane_01	1_M_JOBTESTER,{
	mes("[Mendel]");
	mes("As I expected, the\r"
		"in-flight meals are\r"
		"three star quality at best.");
	mes("*Harrrumph* I really should\r"
		"have brought my chef so that\r"
		"I could enjoy a real meal.");
	close;
}

airplane_01,71,31,2	script	Swordsman Shimizu#air_01	1_M_MOC_LORD,{
	mes("[Swordsman Shimizu]");
	mes("Finally, after five\r"
		"years of waiting...");
	mes("I can have my revenge!");
	next;
	mes("[Swordsman Shimizu]");
	mes("I just...");
	mes("Have to make sure that\r"
		"I don't keep missing my\r"
		"stop. But soon, very soon,\r"
		"vengeance will be mine!");
	close;
}

//=== Typing Challenge =====================================
airplane_01,32,61,4	script	Nils#ein	1_M_03,1,1,{
	mes("[Nils]");
	mes("Welcome to the\r"
		"^ff0000RO Typing Challenge^000000.");
	mes("Would you like to play\r"
		"a quick typing game?");
	next;
	switch (select("Play ^ff0000RO Typing Challenge^000000", "Information", "View Top Records", "Cancel")) {
	case 1:
		mes("[Nils]");
		mes("Okay, we have\r"
			"a new challenger!");
		mes("Enter the following\r"
			"text as quickly as you\r"
			"can without making any\r"
			"mistakes! Let's start~!");
		setarray .@line1_1$[0],	"^3cbcbccallipygian salacius lascivious^000000",
					"^3cbcbcBy the power of^000000",
					"^0000ffthkelfkskeldmsiejdlslehfndkelsheidl^000000",
					"^3cbcbcburrdingdingdingdilidingdingdingphoohudaamb^000000",
					"^3cbcbcCoboman no chikara-yumei na^000000",
					"^3cbcbcI'm the king of All Weirdos! Now^000000",
					"^3cbcbcYou give me no choice. I guess it's^000000";
		setarray .@line1_2$[0],	"^3cbcbclicentious prurient concupiscent^000000",
					"^3cbcbcp-po-poi-po-poi-poin-poing^000000",
					"^3cbcbcskemd^000000",
					"^3cbcbcandoorabambarambambambambamburanbamding^000000",
					"^3cbcbcchikara-daiookii na chikara da ze!^000000",
					"^3cbcbcyou know of my true power. Obey~!^000000",
					"^3cbcbctime for me to reveal my secret...^000000";
		setarray .@line1_3$[0],	"",
					"^3cbcbcGOD-POING. I NEVER LOSE!^000000",
					"",
					"",
					"^3cbcbcCOBO ON^000000",
					"",
					"";
		setarray .@word1$[0],	"callipygian salacius lascivious licentious prurient concupiscent",
					"By the power of p-po-poi-po-poi-poin-poing GOD-POING. I NEVER LOSE!",
					"thkelfkskeldmsiejdlslehfndkelsheidlskemd",
					"burrdingdingdingdilidingdingdingphoohudaambandoorabambarambambambambamburanbamding",
					"Coboman no chikara-yumei na chikara-daiookii na chikara da ze! COBO ON",
					"I'm the king of All Weirdos! Now you know of my true power. Obey~!",
					"You give me no choice. I guess it's time for me to reveal my secret...";
		setarray .@line2_1$[0],	"^3cbcbcuNflAPPaBLe LoVaBLe SeCreTs AnD^000000",
					"^ff1493LiGhTsPeEd RiGhT SPEed LeFT TURn^000000",
					"^ff1493hfjdkeldjsieldjshfjdjeiskdlefvbd^000000",
					"^ff1493burapaphuralanderamduanbatuhiwooi^000000",
					"^ff1493belief love luck grimace sweat rush^000000",
					"^800080opeN, Open!op3n.openOpen0p3nOpEn0pen^000000",
					"^3cbcbcfReeDoM ecstAcy JoUrnaliSm ArMplt^000000";
		setarray .@line2_2$[0],	"^3cbcbcboWLIiNg aGaINST tHe KarMA of YoUtH^000000",
					"^ff1493RiGhT BuRn OrIGInAL GaNgSteR SmACk^000000",
					"",
					"^ff1493kabamturubamdingding^000000",
					"^ff1493folktale rodimus optimus bumblebee^000000",
					"^800080`open'0Pen open? open!111OPENSESAME^000000",
					"^3cbcbcDisCoverY hEaDaChE MoonbeAmS jUsTiCE^000000";
		setarray .@word2$[0],	"uNflAPPaBLe LoVaBLe SeCreTs AnD boWLIiNg aGaINST tHe KarMA of YoUtH",
					"LiGhTsPeEd RiGhT SPEed LeFT TURn RiGhT BuRn OrIGInAL GaNgSteR SmACk",
					"hfjdkeldjsieldjshfjdjeiskdlefvbd",
					"burapaphuralanderamduanbatuhiwooikabamturubamdingding",
					"belief love luck grimace sweat rush folktale rodimus optimus bumblebee",
					"opeN, Open!op3n.openOpen0p3nOpEn0pen`open'0Pen open? open!111OPENSESAME",
					"fReeDoM ecstAcy JoUrnaliSm ArMplt DisCoverY hEaDaChE MoonbeAmS jUsTiCE";
		setarray .@letters[0],	1300,
					1250,
					1180,
					1380,
					1740,
					1440,
					1450;
		.@wordtest = rand(7);
		next;
		mes("[Nils]");
		mes(.@line1_1$[.@wordtest]);
		mes(.@line1_2$[.@wordtest]);
		mes(.@line1_3$[.@wordtest]);
		.@start_time = gettime(GETTIME_HOUR)*60*60 + gettime(GETTIME_MINUTE)*60 + gettime(GETTIME_SECOND);
		next;
		input .@save1$;
		.@end_time = gettime(GETTIME_HOUR)*60*60 + gettime(GETTIME_MINUTE)*60 + gettime(GETTIME_SECOND);
		.@total_time = .@end_time - .@start_time;
		mes("[Nils]");
		mes(.@line2_1$[.@wordtest]);
		mes(.@line2_2$[.@wordtest]);
		.@start_time = gettime(GETTIME_HOUR)*60*60 + gettime(GETTIME_MINUTE)*60 + gettime(GETTIME_SECOND);
		next;
		input .@save2$;
		.@end_time = gettime(GETTIME_HOUR)*60*60 + gettime(GETTIME_MINUTE)*60 + gettime(GETTIME_SECOND);
		.@total_time = .@total_time + (.@start_time - .@end_time);
		.@tasoo = (.@letters[.@wordtest] / .@total_time) * 6;
		if ((.@save1$ == .@word1$[.@wordtest]) && (.@save2$ == .@word2$[.@wordtest])) {
			mes("[Nils]");
			mesf("Your record is ^ff0000%d seconds^000000 and\r"
				"the total letters are %d.",
				.@total_time, .@tasoo);
			next;
			if (.@tasoo >= 1300) {
				mes("[Nils]");
				mes("Hmmm, this record isn't\r"
					"humanly possible unless you\r"
					"copy and paste the whole\r"
					"sentence. Please play fairly\r"
					"next time.");
				close;
			}
			if (.@tasoo >= $050320_ein_typing) {
				mes("[Nils]");
				mesf("The previous top record was\r"
					"made by ^0000ff%s^000000\r"
					"with the total ^0000ff%d^000000 letters.",
					$050320_minus1_typing$, $050320_ein_typing);
				mesf("However, ^ff0000%s^000000,\r"
					"you made the new top record\r"
					"this time. Congratulations!",
					strcharinfo(PC_NAME));
				$050320_minus1_typing$ = strcharinfo(PC_NAME);
				$050320_ein_typing = .@tasoo;
				close;
			}
			else {
				mes("[Nils]");
				mesf("^0000ff%s^000000\r"
					"is the current\r"
					"record holder with\r"
					"a letter total of ^0000ff%d^000000\r"
					"characters. Try to beat\r"
					"that record next time~",
					$050320_minus1_typing$, $050320_ein_typing);
				close;
			}
		}
		else {
			mes("[Nils]");
			mes("Oooh...");
			mes("I'm sorry, but\r"
				"you entered the\r"
				"text incorrectly...");
			close;
		}
	case 2:
		mes("[Nils]");
		mes("The ^ff0000RO Typing Challenge^000000\r"
			"is a game where you enter\r"
			"the given text as quickly as you\r"
			"can. The name of the top player\r"
			"is recorded for posterity. If you\r"
			"want fame, here's your chance!");
		next;
		mes("[Nils]");
		mes("I'd just like to let\r"
			"you know that you type\r"
			"all the text that you see\r"
			"in the single input line that\r"
			"you're given. So don't press\r"
			"the enter key, just click 'OK.'");
		close;
	case 3:
		mes("[Nils]");
		mesf("^0000ff%s^000000\r"
			"is the current\r"
			"record holder with\r"
			"a letter total of ^0000ff%d^000000\r"
			"characters. Try to beat\r"
			"that record next time~",
			$050320_minus1_typing$, $050320_ein_typing);
		close;
	case 4:
		mes("[Nils]");
		mes("Feel free to take on the\r"
			"^ff0000RO Typing Challenge^000000\r"
			"anytime. I'll be here~");
		close;
	}
}

//== Apple Gambling ========================================
airplane_01,33,68,4	script	Clarice	1_F_MERCHANT_02,{
	mes("[Clarice]");
	mes("Hi, I'm Clarice~");
	mes("How would you like\r"
		"to wager some Apples\r"
		"in a friendly game of Dice?");
	next;
	callfunc("applegamble", _("Clarice"));
	end;
}

function	script	applegamble	{
	.@n$ = sprintf(_$("[%s]"), getarg(0));
	switch (select("Play Dice Game", "Learn Dice Game Rules", "Cancel")) {
	case 3:
		mes(.@n$);
		mes("I'm up for a game of\r"
			"dice whenever you feel\r"
			"like it. Just talk to me if\r"
			"you ever get hit with the\r"
			"sudden urge to gamble, kay?");
		close;
	case 2:
		mes(.@n$);
		mes("The rules for the Dice game\r"
			"are pretty simple. First, you\r"
			"place a bet by wagering Apples.");
		mes("You can bet a maximum of 50\r"
			"Apples at a time. To keep things\r"
			"legal, I can only accept Apples.");
		next;
		mes(.@n$);
		mes("But hey, if all that zeny\r"
			"is burning a hole in your\r"
			"pocket, head over to Fruitz\r"
			"and you can buy as many\r"
			"Apples as you want, playah~");
		next;
		mes(.@n$);
		mes("Now, we begin with me\r"
			"rolling two 6-sided dice.");
		mes("When it's your turn, you'll\r"
			"roll two 6-sided dice. After\r"
			"that, both of us will have the\r"
			"option of rolling a third die.");
		next;
		mes(.@n$);
		mes("Now here's the important\r"
			"thing. If your total is higher\r"
			"than 12, you'll bust, meaning\r"
			"that you lose. Otherwise, the\r"
			"person with the higher total\r"
			"is the winner. Got it?");
		next;
		mes(.@n$);
		mes("Now, you'll be the first\r"
			"to decide whether or not\r"
			"you'll roll the third die. Then,\r"
			"depending on your result, I'll\r"
			"roll my third die... Or maybe not.");
		next;
		mes(.@n$);
		mes("When you win, you'll\r"
			"receive twice as many\r"
			"Apples as you wagered.");
		mes("But if we happen to tie, you\r"
			"get the Apples that you bet\r"
			"returned to you. Fair, right?");
		close;
	case 1:
		break;
	}
	mes(.@n$);
	mes("Ooh, so you'll play with\r"
		"me? Great! How many\r"
		"Apples would you like to bet?");
	mes("Remember, you can wager\r"
		"up to 50 Apples. If you'd like\r"
		"to cancel, please enter '0'.");
	next;
	while(1) {
		input .@amount;
		if (.@amount == 0) {
			mes(.@n$);
			mes("Changed your mind?");
			mes("I understand. Well then,\r"
				"I hope we can play sometime.");
			close;
		}
		else if (.@amount < 1 || .@amount > 50) {
			mes(.@n$);
			mes("You can't bet more than\r"
				"50 Apples. Remember, we\r"
				"need to keep these stakes\r"
				"reasonable. Please enter\r"
				"a value no greater than 50.");
			next;
			continue;
		}
		mes(.@n$);
		mesf("So you'll be\r"
			"betting ^FF0000%d^000000 Apples.",
			.@amount);
		mes("Is that right?");
		next;
		if (select("Yes", "No") == 2) {
			mes(.@n$);
			mes("Mm, made a mistake?");
			mes("Alright, please enter the\r"
				"number of Apples you\r"
				"wish to place in this bet");
			next;
			continue;
		}
		if (countitem(Apple) <.@amount) {
			mes(.@n$);
			mes("I'm sorry, but you\r"
				"don't seem to have\r"
				"enough Apples for this\r"
				"bet... You can't gamble\r"
				"if you can't play, you know.");
			next;
			continue;
		}
		delitem 512,.@amount;
		mes(.@n$);
		mes("Good!");
		mes("Now we can start\r"
			"this game! I'll roll first~");
		break;
	}
	mes("^3355FF*Rolling and rumbling*^000000");
	next;
	.@giveapple = .@amount*2;
	.@table1 = rand(1,6);
	.@table2 = rand(1,6);
	.@tablesub = .@table1 + .@table2;
	.@tabletotal = .@tablesub;
	mes(.@n$);
	mesf("I got a ^0000FF%d^000000 and a ^0000FF%d^000000.", .@table1, .@table2);
	mesf("That's a total of ^0000FF%d^000000.", .@tablesub);
	mesf("^FF0000%s^000000, now it's your turn.", strcharinfo(PC_NAME));
	next;
	select("Cast Dice.");
	mes("^3355FF*Rolling and rumbling*^000000");
	.@player1 = rand(1,6);
	.@player2 = rand(1,6);
	.@playersub = .@player1 + .@player2;
	if (.@playersub > 9 && .@amount > 39) {
		.@player1 = rand(1,6);
		.@player2 = rand(1,6);
		.@playersub = .@player1 + .@player2;
	}
	.@playertotal = .@playersub;
	next;
	mes(.@n$);
	mesf("^FF0000%s^000000, you have ^FF0000%d^000000 and ^FF0000%d^000000. The total is ^FF0000%d^000000.",
		strcharinfo(PC_NAME), .@player1, .@player2, .@playersub);
	next;
	mes(.@n$);
	if(.@playersub == .@tablesub) {
		mesf("Currently my total is ^0000FF%d^000000 and ^FF0000%s^000000, your total is ^FF0000%d^000000. "
			"We are making an even game. Would you like to cast dice again?",
			.@tablesub, strcharinfo(PC_NAME), .@playersub);
	} else if (.@playersub > .@tablesub) {
		mesf("Currently my total is ^0000FF%d^000000 and ^FF0000%s^000000, your total is ^FF0000%d^000000. "
			"^FF0000%s^000000, you are currently winning this game. Would you like to cast dice again?",
			.@tablesub, strcharinfo(PC_NAME), .@playersub, strcharinfo(PC_NAME));
	} else if(.@tablesub > .@playersub) {
		mesf("Currently my total is ^0000FF%d^000000 and ^FF0000%s^000000, your total is ^FF0000%d^000000. "
			"I am winning this game. Would you like to cast dice again?",
			.@tablesub, strcharinfo(PC_NAME), .@playersub);
	}
	next;
	switch (select("Cast dice.", "Cancel.")) {
	case 1:
		mes("^3355FF*Rolling and rumbling*^000000");
		.@player3 = rand(1,6);
		.@playertotal += .@player3;
		next;
		mes(.@n$);
		if (.@playertotal > 12) {
			mesf("^FF0000%s^000000, you got ^FF0000%d^000000 and the total is now ^FF0000%d^000000. "
				"You lost this game. I am sorry but please try again.",
				strcharinfo(PC_NAME), .@player3, .@playertotal);
			close;
		}
		else if (.@playertotal < .@tablesub) {
			mesf("^FF0000%s^000000, you got ^FF0000%d^000000 and the total is now ^FF0000%d^000000. "
				"Even though you casted dice again, still your total is smaller than mine. "
				"You lost the game. I am sorry and please try again.",
				strcharinfo(PC_NAME), .@player3, .@playertotal);
			close;
		}
		else if (.@playertotal == .@tablesub) {
			if (.@tablesub > 8) {
				mesf("^FF0000%s^000000, you got ^FF0000%d^000000 and the total is now ^FF0000%d^000000. "
					"I don't want to take any risk, let's end this game in a draw. "
					"Let's play again some other time~",
					strcharinfo(PC_NAME), .@player3, .@playertotal);
				close2;
				getitem Apple,.@amount;
				end;
			}
			mes("Alright.");
			mes("Let me cast the dice again.");
		}
		else {
			mesf("^FF0000%s^000000, you got ^FF0000%d^000000 and the total is now ^FF0000%d^000000. "
					"Now it is my turn.",
				strcharinfo(PC_NAME), .@player3, .@playertotal);
		}
		break;
	case 2:
		mes(.@n$);
		if (.@playersub > .@tablesub) {
			mes("I see, you don't want to take risk of losing the game. Okay, let me cast dice again.");
		}
		else if (.@playersub == .@tablesub) {
			if (.@tablesub > 8) {
				mes("I see, you don't want to take risk of losing this game. "
					"Neither do I, let's end this game in a draw. "
					"Let's play again some other time~");
				close2;
				getitem Apple,.@amount;
				end;
			}
			mes("Alright.");
			mes("Let me cast the dice again.");
		}
		else {
			mes("It couldn't hurt to try.");
			mes("Well, I win this time.");
			mes("I'm sorry, let's try play\r"
				"again sometime.");
			close;
		}
		break;
	}
	next;
	mes("^3355FF*Rolling and rumbling*^000000");
	.@table3 = rand(1,6);
	.@tabletotal += .@table3;
	next;
	mes(.@n$);
	if (.@tabletotal > 12) {
		mesf("I got ^0000FF%d^000000 and the total is now ^0000FF%d^000000. "
			"I lost this game since my total exceeded 12. Let me give you my apples. "
			"Congratulations, that was a great game.",
			.@table3, .@tabletotal);
		close2;
		getitem Apple,.@giveapple;
		end;
	}
	else if (.@playertotal > .@tabletotal) {
		mesf("I got ^0000FF%d^000000 and the total is now ^0000FF%d^000000. "
			"With total ^FF0000%d^000000 you won this game, ^FF0000%s^000000. Let me give you my apples. "
			"It was a great game and I hope we will play again some other time.",
			.@table3, .@tabletotal, .@playertotal, strcharinfo(PC_NAME));
		close2;
		getitem Apple,.@giveapple;
		end;
	}
	else if (.@playertotal == .@tabletotal) {
		mesf("I got ^0000FF%d^000000 and the total is now ^0000FF%d^000000. "
			"With total ^FF0000%d^000000 this game came out even, ^FF0000%s^000000. "
			"Let me give you your apple back. It was a great game and I hope we will play again some other time.",
			.@table3, .@tabletotal, .@playertotal, strcharinfo(PC_NAME));
		close2;
		getitem Apple,.@amount;
		end;
	}
	else if (.@playertotal < .@tabletotal) {
		mesf("I got ^0000FF%d^000000 and the total is now ^0000FF%d^000000. "
			"With total ^FF0000%d^000000 you lost this game, ^FF0000%s^000000. I am sorry but please try again.",
			.@table3, .@tabletotal, .@playertotal, strcharinfo(PC_NAME));
		close;
	}
}
