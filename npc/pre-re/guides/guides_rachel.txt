//================= Hercules Script =======================================
//=       _   _                     _
//=      | | | |                   | |
//=      | |_| | ___ _ __ ___ _   _| | ___  ___
//=      |  _  |/ _ \ '__/ __| | | | |/ _ \/ __|
//=      | | | |  __/ | | (__| |_| | |  __/\__ \
//=      \_| |_/\___|_|  \___|\__,_|_|\___||___/
//================= License ===============================================
//= This file is part of Hercules.
//= http://herc.ws - http://github.com/HerculesWS/Hercules
//=
//= Copyright (C) 2012-2015  Hercules Dev Team
//= Copyright (C)  Samuray22
//= Copyright (C)  L0ne_W0lf
//=
//= Hercules is free software: you can redistribute it and/or modify
//= it under the terms of the GNU General Public License as published by
//= the Free Software Foundation, either version 3 of the License, or
//= (at your option) any later version.
//=
//= This program is distributed in the hope that it will be useful,
//= but WITHOUT ANY WARRANTY; without even the implied warranty of
//= MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//= GNU General Public License for more details.
//=
//= You should have received a copy of the GNU General Public License
//= along with this program.  If not, see <http://www.gnu.org/licenses/>.
//=========================================================================
//= Rachel Guide (Pre-Renewal)
//================= Description ===========================================
//= Rachel City guide
//================= Current Version =======================================
//= 1.2b
//=========================================================================

rachel,138,146,5	script	Rachel Guide	4_M_RASWORD,{
	mes "[Rachel Guide]";
	mes "Welcome to the capital of";
	mes "Arunafeltz, Rachel where a warm";
	mes "breath of goddess Freya reaches.";
	mes "If this is the first time for you";
	mes "to use the guide services, why";
	mes "don't you check the \"Notice\" menu first?";
	.@loop1 = 1;
	while(.@loop1) {
		next;
		switch(select("Village Guide", "Remove Marks from Mini-Map", "Notice", "Cancel")) {
		case 1:
			mes "[Rachel Guide]";
			mes "I can tell you any building location as long as it is in Rachel.";
			mes "So where do you want to go?";
			if (.@compass_check == 0) {
				mes "Would you like me";
				mes "to mark locations";
				mes "on your Mini-Map?";
				next;
				if (select("Yes", "No") == 1) .@compass_check = 1;
			}
			.@loop2 = 1;
			while(.@loop2) {
				if (.@wait_button_chk == 0) .@wait_button_chk = 1;
				else next;

				switch(select("Cheshrumnir", "Inn", "Weapon Shop", "Tool Shop", "Airport", "Cancel")) {
				case 1:
					mes "[Rachel Guide]";
					mes "Cheshrumnir is a holy ground where pope, the incarnation of goddess Freya stays.";
					mes "Take the road to the norh to find the building.";
					if (.@compass_check == 1) viewpoint 1,150,249,1,0xFF0000;
					break;
				case 2:
					mes "[Rachel Guide]";
					mes "You can rest your fatigue off the journey in the Inn.";
					mes "The left building next to me is the Inn of Rachel.";
					if (.@compass_check == 1) viewpoint 1,115,149,2,0xFF00FF;
					break;
				case 3:
					mes "[Rachel Guide]";
					mes "Do you want to check out the weapons that are sold in Rachel?";
					mes "The weapon shop is located nearby the western gate.";
					if (.@compass_check == 1) viewpoint 1,42,87,3,0x99FFFF;
					break;
				case 4:
					mes "[Rachel Guide]";
					mes "Rachel tool shop sells the best quality potions.";
					mes "It's located nearby the western gate.";
					if (.@compass_check == 1) viewpoint 1,83,78,4,0x0000FF;
					break;
				case 5:
					mes "[Rachel Guide]";
					mes "The Airport is located outside the eastern gate.";
					if (.@compass_check == 1) viewpoint 1,273,125,5,0x00FF00;
					break;
				case 6:
					mes "[Rachel Guide]";
					mes "If you like to get rid of all the location marks on your Mini-Map,";
					mes "just ask me again, and choose \"Remove Marks from Mini-Map\" menu.";
					.@loop2 = 0;
					break;
				}
			}
			break;

		case 2:
			viewpoint 2,150,249,1,0xFF0000;
			viewpoint 2,115,149,2,0xFF00FF;
			viewpoint 2,42,87,3,0x99FFFF;
			viewpoint 2,83,78,4,0x0000FF;
			viewpoint 2,273,125,5,0x00FF00;
			mes "[Rachel Guide]";
			mes "Okay, they are gone now. If you have more locations to ask, just let me know.";
			break;
		case 3:
			mes "[Rachel Guide]";
			mes "When you are using the ''Village Guide'' menu,";
			mes "make sure that building locations will be marked on your mini-map at the upper right side of your screen.";
			mes "If you cannot see your mini-map, use the short cut key ''ctrl+tab'' or press the ''Map'' button on your basic information windows, okay?";
			mes "And you can also zoom out your mini-map by using the ''-'' button in case you cannot view the entire map of the village.";
			break;
		case 4:
			mes "[Rachel Guide]";
			mes "Hope you have a wonderfull journey";
			mes "in Arunafeltz.";
			close2;
			.@loop1 = 0;
			break;
		}
	}
	close;
}
