//================= Hercules Script =======================================
//=       _   _                     _
//=      | | | |                   | |
//=      | |_| | ___ _ __ ___ _   _| | ___  ___
//=      |  _  |/ _ \ '__/ __| | | | |/ _ \/ __|
//=      | | | |  __/ | | (__| |_| | |  __/\__ \
//=      \_| |_/\___|_|  \___|\__,_|_|\___||___/
//================= License ===============================================
//= This file is part of Hercules.
//= http://herc.ws - http://github.com/HerculesWS/Hercules
//=
//= Copyright (C) 2012-2015  Hercules Dev Team
//= Copyright (C)  Masao
//=
//= Hercules is free software: you can redistribute it and/or modify
//= it under the terms of the GNU General Public License as published by
//= the Free Software Foundation, either version 3 of the License, or
//= (at your option) any later version.
//=
//= This program is distributed in the hope that it will be useful,
//= but WITHOUT ANY WARRANTY; without even the implied warranty of
//= MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//= GNU General Public License for more details.
//=
//= You should have received a copy of the GNU General Public License
//= along with this program.  If not, see <http://www.gnu.org/licenses/>.
//=========================================================================
//= iRO Repeatable EXP Quests.
//================= Description ===========================================
//= Repeatable EXP Quest : Kill 50 Mantis. Custom Version.
//================= Current Version =======================================
//= 1.0
//=========================================================================

lou_fild01,240,354,4	script	Mantis Researcher	4_M_OILMAN,{

	if (questprogress(60179)) {
		if (questprogress(60179,HUNTING) != 2) {
			mes "[Mantis Researcher]";
			mes "Have you finished hunting the 50 Mantis?";
			next;
			switch(select("Yes", "No", "I want to quit")) {
			case 1:
				mes "[Mantis Researcher]";
				mes "Hmm, I don't think you've hunted 50 yet.";
				close;
			case 2:
				mes "[Mantis Researcher]";
				mes "Remember, I need help hunting Mantis so go and hunt 50 of them.";
				close;
			case 3:
				mes "[Mantis Researcher]";
				mes "Are you sure that you want to stop hunting?";
				mes "Any progress that you've made will be erased";
				next;
				switch(select("Yes", "No")) {
				case 1:
					mes "[Mantis Researcher]";
					mes "Ok then, well come back here if you change your mind.";
					erasequest 60179;
					close;
				case 2:
					mes "[Mantis Researcher]";
					mes "Please kill 50 Mantis in order to make this a Place were people can go to and gather Experience.";
					close;
				}
			}
		}
		mes "[Mantis Researcher]";
		mes "Amazing, you did that with speed.";
		getexp 18818,7527;
		erasequest 60179;
		close;
	}
	if ((BaseLevel > 34) && (BaseLevel < 71)) {
		mes "[Mantis Researcher]";
		mes "Oh great!";
		mes "You are here to help me right?";
		mes "If not, i don't know what to do with the Monsters in this Place!";
		next;
		switch(select("Sure", "No")) {
		case 1:
			mes "[Mantis Researcher]";
			mes "Great! I knew the moment i saw you that i can count on you!";
			mes "I need you to reduce the Amount of Monsters which are currently rampaging on this Map!";
			mes "There are to many Mantis around, which makes it impossible for the lower level players to gain good experience here.";
			next;
			mes "[Mantis Researcher]";
			mes "So, would you still like to help me?";
			next;
			switch(select("Sure", "Cancel")) {
			case 1:
				if (BaseLevel > 70) {
					mes "[Mantis Researcher]";
					mes "Thank you for your great help with the Mantis!";
					mes "Currently the Situation is stable and i'm not in need of your help anymore to kill them.";
					close;
				}
				mes "[Mantis Researcher]";
				mes "Kill 50 of those Mantis and let me know when you're done. I will reward you for your efforts.";
				setquest 60179;
				close;
			case 2:
				mes "[Mantis Researcher]";
				mes "If you change your mind, please come back.";
				close;
			}
		case 2:
			mes "[Mantis Researcher]";
			mes "If you change your mind, please come back.";
			close;
		}
	}
	mes "[Mantis Researcher]";
	mes "Oh boy, Oh boy!";
	next;
	mes "[Mantis Researcher]";
	mes "Sorry I can't talk right now I am in a world of pain here, these darn Mantis are going to kill me.";
	close;
}
