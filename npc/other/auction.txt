//===== Hercules Script ======================================
//= Auction system
//===== By: ==================================================
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.3
//===== Description: =========================================
//= [Official Conversion]
//= Auction house releated NPCs, and warps
//===== Additional Comments: =================================
//= 1.0 First version, place holders only. [L0ne_W0lf]
//= 1.1 Moved Morroc Auction NPCs to Morroc Ruins. [L0ne_W0lf]
//= 1.2 Warp fixed. Dialog in Ruins/Lighthalzen changed. [Kisuka]
//= 1.3 Duplicates now spawn from floating NPCs. [L0ne_W0lf]
//============================================================

// Auction House warpers
//============================================================
moc_ruins,78,173,6	script	Auction Hall Guide#moc	4W_M_02,{
	callfunc "F_AuctionWarper",1;
}

prontera,218,120,4	script	Auction Hall Guide#prt	4_F_KAFRA1,{
	callfunc "F_AuctionWarper",2;
}

yuno,129,116,0	script	Auction Hall Guide#yuno	4_F_01,{
	callfunc "F_AuctionWarper",3;
}

lighthalzen,205,169,6	script	Auction Hall Guide#lhz	4_M_ZONDAOYAJI,{
	callfunc "F_AuctionWarper",4;
}

function	script	F_AuctionWarper	{
	mes "[Auction Hall Guide]";
	mes "Hello, would you";
	mes "like to enter the";
	mes "Auction Hall?";
	next;
	if (select("Yes:No") == 1) {
		if (getarg(0) == 1 || getarg(0) == 4) {
			mes "[Auction Hall Guide]";
			mes "Great! Well then,";
			mes "I hope you have fun";
			mes "and enjoy the auction~";
		} else {
			mes "[Auction Hall Guide]";
			mes "Enjoy your auction.";
		}
		close2;
		switch(getarg(0)) {
		case 1:
			warp "auction_01",179,53; end;
		case 2:
			warp "auction_01",21,43; end;
		case 3:
			warp "auction_02",151,23; end;
		case 4:
			warp "auction_02",43,24; end;
		}
	}
	mes "[Auction Hall Guide]";
	mes "Alright then,";
	mes "see you later.";
	mes "If you change your";
	mes "mind, please come";
	mes "and enjoy the auctions~";
	close;
}

// Sign posts
//============================================================
-	script	Information Post#dum::AuctionSign	-1,{
	mes "[Information]";
	mes "Auction Warp Guide";
	close;
}

moc_ruins,76,176,6	duplicate(AuctionSign)	Information Post#moc	2_BULLETIN_BOARD
prontera,216,120,4	duplicate(AuctionSign)	Information Post#prt	2_BULLETIN_BOARD
yuno,131,116,0	duplicate(AuctionSign)	Information Post#yuno	2_BULLETIN_BOARD
lighthalzen,207,169,6	duplicate(AuctionSign)	Information Post#lhz	2_BULLETIN_BOARD

// Warps
//============================================================
auction_01,180,49,0	warp	auction_entrance_moc	1,1,moc_ruins,78,171
auction_01,22,37,0	warp	auction_entrance_prt	1,1,prontera,214,120
auction_02,151,17,0	warp	auction_enterance_juno	1,1,yuno,132,119
auction_02,43,17,0	warp	auction_enterance_lhz	1,1,lighthalzen,209,169

// Auction House NPCs
//============================================================
-	script	Auction Broker#dum::AuctionBroker	-1,{
	mes "[Auction Broker]";
	mes "Welcome to the Auction Hall.";
	mes "Would you like to view the goods?";
	next;
	if (select("Yes:No") == 1) {
		mes "[Auction Broker]";
		mes "Very well.";
		mes "Please take";
		mes "a look, and see";
		mes "What's being offered~";
		openauction;
		close;
	}
	mes "[Auction Broker]";
	mes "Very well, then.";
	mes "If you change your";
	mes "mind, then please";
	mes "come and check";
	mes "out the auctions~";
	close;
}

auction_01,182,68,6	duplicate(AuctionBroker)	Auction Broker#moc1	4W_M_02
auction_01,182,75,0	duplicate(AuctionBroker)	Auction Broker#moc2	4W_M_03
auction_01,177,75,2	duplicate(AuctionBroker)	Auction Broker#moc3	4W_M_02
auction_01,177,68,4	duplicate(AuctionBroker)	Auction Broker#moc4	4W_M_03
auction_01,21,74,4	duplicate(AuctionBroker)	Auction Broker#prt1	4_F_KAFRA1
auction_01,27,78,4	duplicate(AuctionBroker)	Auction Broker#prt2	4_F_KAFRA2
auction_01,16,78,4	duplicate(AuctionBroker)	Auction Broker#prt3	4_F_KAFRA3
auction_02,158,47,6	duplicate(AuctionBroker)	Auction Broker#yuno1	4_F_01
auction_02,145,47,2	duplicate(AuctionBroker)	Auction Broker#yuno2	4_F_01
auction_02,151,54,0	duplicate(AuctionBroker)	Auction Broker#yuno3	4_F_01
auction_02,152,41,4	duplicate(AuctionBroker)	Auction Broker#yuno4	4_F_01
auction_02,57,46,2	duplicate(AuctionBroker)	Auction Broker#lhz1	4_M_ZONDAMAN
auction_02,31,46,6	duplicate(AuctionBroker)	Auction Broker#lhz2	4_M_ZONDAMAN
auction_02,43,65,4	duplicate(AuctionBroker)	Auction Broker#lhz3	4_M_ZONDAOYAJI
