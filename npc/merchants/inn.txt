//================= Hercules Script =======================================
//=       _   _                     _
//=      | | | |                   | |
//=      | |_| | ___ _ __ ___ _   _| | ___  ___
//=      |  _  |/ _ \ '__/ __| | | | |/ _ \/ __|
//=      | | | |  __/ | | (__| |_| | |  __/\__ \
//=      \_| |_/\___|_|  \___|\__,_|_|\___||___/
//================= License ===============================================
//= This file is part of Hercules.
//= http://herc.ws - http://github.com/HerculesWS/Hercules
//=
//= Copyright (C) 2012-2015  Hercules Dev Team
//= Copyright (C)  Euphy
//= Copyright (C)  Kisuka
//= Copyright (C)  Samuray22
//= Copyright (C)  L0ne_W0lf
//= Copyright (C)  erKURITA
//= Copyright (C)  kobra_k88
//= Copyright (C)  Playtester
//= Copyright (C)  Darkchild
//=
//= Hercules is free software: you can redistribute it and/or modify
//= it under the terms of the GNU General Public License as published by
//= the Free Software Foundation, either version 3 of the License, or
//= (at your option) any later version.
//=
//= This program is distributed in the hope that it will be useful,
//= but WITHOUT ANY WARRANTY; without even the implied warranty of
//= MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//= GNU General Public License for more details.
//=
//= You should have received a copy of the GNU General Public License
//= along with this program.  If not, see <http://www.gnu.org/licenses/>.
//=========================================================================
//= Inn Npcs
//================= Description ===========================================
//= Inn Npcs, Save and Heal
//================= Current Version =======================================
//= 2.9a
//=========================================================================

//== Prontera ==============================================
//- West Side Inn -
prt_in,244,135,2	script	Inn Employee#Sammy	1_M_INNKEEPER,{

	callfunc "F_InnMaid","[Employee Sammy]","Nenkaras","prt_in",238,130;
	warp "prt_in",247,104; end;
}

//- East Side Inn -
prt_in,61,141,2	script	Inn Employee#Ahlma	1_M_INNKEEPER,{

	callfunc "F_InnMaid","[Employee Ahlma]","Nenkaras","prt_in",64,136;
	warp "prt_in",60,166; end;
}

//== Alberta ===============================================
alberta_in,32,142,3	script	Inn Employee#Jennie	1_M_INNKEEPER,{

	callfunc "F_InnMaid","[Employee Jennie]","'Fisherman Inn'!","alberta_in",26,142;
	warp "alberta_in",18,188; end;
}

//== Geffen ================================================
geffen_in,70,64,5	script	Inn Employee#Cena	1_M_INNKEEPER,{

	callfunc "F_InnMaid","[Employee Cena]","'Ifrit,' the only Inn in the city of Geffen.","geffen_in",70,59;
	warp "geffen_in",31,31; end;
}

//== Payon =================================================
payon_in01,132,62,5	script	Inn Employee#Ahee	1_M_INNKEEPER,{

	callfunc "F_InnMaid","[Employee Ahee]","Payon Inn","payon_in01",136,61;
	warp "payon_in01",132,11; end;
}

//== Morroc ================================================
//- North East -
/*
morocc_in,147,141,3	script	Inn Employee#Hasna	1_M_INNKEEPER,{

	callfunc "F_InnMaid","[Employee Hasna]","Morroc Inn","morocc_in",142,140;
	warp "morocc_in",173,135; end;
}

//- South -
morocc_in,80,100,5	script	Inn Employee#Manar	1_M_INNKEEPER,{

	callfunc "F_InnMaid","[Employee Manar]","Morroc Inn","morocc_in",78,95;
	warp "morocc_in",79,123; end;
}
*/

//== Aldebaran =============================================
aldeba_in,92,58,5	script	Inn Maid#Rilim	1_M_INNKEEPER,{

	callfunc "F_InnMaid","[Rilim]","Al De Baran Inn","aldeba_in",92,50;
	warp "aldeba_in",92,112; end;
}

//== Rachel ================================================
ra_in01,376,69,4	script	Inn Keeper#Annie	4_M_RACHMAN1,{

	callfunc "F_InnMaid","[Annie]","Rachel Inn","ra_in01",375,58;
	warp "ra_in01",384,128; end;
}

//== Lighthalzen ===========================================
lhz_in02,230,284,4	script	Hotel Employee#01	4_M_04,{
	mes "[Hotel Employee]";
	mes "Welcome to";
	mes "the Royal Dragon,";
	mes "where you can find the";
	mes "finest accommodations";
	mes "and the best service.";
	next;
	switch(select("Save Point", "Rest - 5,000 zeny", "Cancel")) {
	case 1:
		savepoint "lhz_in02",209,275;
		mes "[Hotel Employee]";
		mes "Thank you, your";
		mes "Respawn Point has";
		mes "been saved here in";
		mes "the Royal Dragon.";
		close;
	case 2:
		if(Zeny < 5000) {
			mes "[Hotel Employee]";
			mes "I'm sorry, but";
			mes "you need 5,000 zeny";
			mes "in order to check in.";
			close;
		}
		Zeny -= 5000;
		mes "[Hotel Employee]";
		mes "Thank you~";
		mes "I hope you enjoy";
		mes "your stay in the";
		mes "Royal Dragon.";
		close2;
		warp "lhz_in02",219,150;
		percentheal 100,100;
		npcskill "AL_BLESSING",10,99,99;
		end;
	case 3:
		mes "[Hotel Employee]";
		mes "Thank you and";
		mes "have a nice day.";
		close;
	}
}

//== Hugel =================================================
hu_in01,246,107,3	script	Inn Maid#Receptionist	1_M_INNKEEPER,{

	callfunc "F_InnMaid","[Receptionist]","Hugel Inn","hu_in01",263,95;
	warp "hu_in01",267,5; end;
}

//== Veins =================================================
ve_in,157,219,5	script	Inn Master#Receptionist	4_M_SEAMAN,{
	mes "[Inn Master]";
	mes "Good day~";
	mes "Welcome to the";
	mes "most comfortable";
	mes "inn here in Veins~";
	next;
	switch(select("Save", "Take a Rest -> 5000 zeny", "Quit")) {
	case 1:
		mes "[Inn Master]";
		mes "Your Respawn Point";
		mes "has been saved in Veins.";
		mes "Enjoy your stay in town~";
		savepoint "ve_in",157,209;
		close;
	case 2:
		mes "[Inn Master]";
		if(Zeny < 5000){
			mes "I'm sorry, but I don't";
			mes "think you have enough";
			mes "money to check in. The";
			mes "service charge is 5,000 zeny.";
			close;
		}
		mes "Enjoy your stay~";
		close2;
		Zeny -= 5000;
		percentheal 100,100;
		warp "ve_in",184,228;
		end;
	case 3:
		mes "[Inn Master]";
		mes "Please come again.";
		close;
	}
}

//== Inn Function ==========================================
// Arguments:
// 0 - name of npc
// 1 - name of the inn
// 2 - map to save at
// 3 - x coordinate to save at
// 4 - y coordinate to save at
function	script	F_InnMaid	{
	mes getarg(0);
	mes "Welcome to";
	mes getarg(1) + ".";
	mes "How may I help you?";
	next;
	switch(select("Save", "Take a Rest -> 5000 zeny", "Cancel")) {
	case 1:
		mes getarg(0);
		mes "Your respawn point";
		mes "has been saved.";
		mes "Thank you,";
		mes "please come again.";
		savepoint getarg(2),getarg(3),getarg(4);
		close;
	case 2:
		mes getarg(0);
		if(Zeny < 5000){
			mes "I'm sorry, but the service charge is 5,000 zeny. Please make sure that you have enough money to check in next time, okay?";
			close;
		}
		mes "Thank you.";
		mes "I hope you";
		mes "enjoy your rest~";
		close2;
		Zeny -= 5000;
		percentheal 100,100;
		return;
	case 3:
		close;
	}
}
