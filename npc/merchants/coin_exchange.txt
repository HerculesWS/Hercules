//================= Hercules Script =======================================
//=       _   _                     _
//=      | | | |                   | |
//=      | |_| | ___ _ __ ___ _   _| | ___  ___
//=      |  _  |/ _ \ '__/ __| | | | |/ _ \/ __|
//=      | | | |  __/ | | (__| |_| | |  __/\__ \
//=      \_| |_/\___|_|  \___|\__,_|_|\___||___/
//================= License ===============================================
//= This file is part of Hercules.
//= http://herc.ws - http://github.com/HerculesWS/Hercules
//=
//= Copyright (C) 2012-2015  Hercules Dev Team
//= Copyright (C)  rAthena Dev Team
//= Copyright (C)  Euphy
//= Copyright (C)  eAthena Dev Team
//= Copyright (C)  Gepard
//=
//= Hercules is free software: you can redistribute it and/or modify
//= it under the terms of the GNU General Public License as published by
//= the Free Software Foundation, either version 3 of the License, or
//= (at your option) any later version.
//=
//= This program is distributed in the hope that it will be useful,
//= but WITHOUT ANY WARRANTY; without even the implied warranty of
//= MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//= GNU General Public License for more details.
//=
//= You should have received a copy of the GNU General Public License
//= along with this program.  If not, see <http://www.gnu.org/licenses/>.
//=========================================================================
//= Coin Merchants
//================= Description ===========================================
//= Coin redemption NPCs.
//================= Current Version =======================================
//= 1.2
//=========================================================================

//== Manuk & Splendide =====================================
-	script	::merchant_13_2	FAKE_NPC,{
	.@n$ = "["+strnpcinfo(NPC_NAME_VISIBLE)+"]";
	mes .@n$;
	if (isequipped(2782) && ep13_2_rhea == 100) {
		if (!checkweight(Knife,1)) {
			mes "It looks like you're carrying too many things.";
			mes "Why not put some of your items in storage and come back?";
			close;
		}
		mes "Hello.";
		mes "What are you looking for?";
		next;
		if (strnpcinfo(NPC_NAME_VISIBLE) == "Merchant of Manuk") {
			setarray .@items[2],12342,12343,12348;
			.@Book = 11019; //Manuk_Selling_Item
			.@coin = 6080; //Manuk_Coin
			.@i = select("View item description", "Manuk's Opportunity", "Manuk's Courage", "Manuk's Faith", "Cancel");
		}
		else {
			setarray .@items[2],12344,12345,12349;
			.@Book = 11018; //Splendide_Selling_Item
			.@coin = 6081; //Splendide_Coin
			.@i = select("View item description", "Buy Pinguicula's Fruit Jam", "Buy Luciola's Honey Jam", "Buy Cornus' Tears", "Do nothing");
		}
		mes .@n$;
		if (.@i == 1) {
			mes "Here are the item descriptions.";
			close2;
			readbook .@Book,1;
			end;
		}
		else if (.@i < 5) {
			mes "I can sell you 3 "+getitemname(.@items[.@i])+" for ^3131FF1 coin^000000.";
			next;
			switch(select("Purchase", "Do not purchase")) {
			case 1:
				if (countitem(.@coin)) {
					mes .@n$;
					mes "Thank you for coming.";
					delitem .@coin,1;
					getitem .@items[.@i],3;
					close;
				}
				else {
					mes .@n$;
					mes "You don't have enough coins.";
					close;
				}
			case 2:
				mes .@n$;
				break;
			}
		}
		mes "Come again if you change your mind.";
		close;
	}
	else {
		if (strnpcinfo(NPC_NAME_VISIBLE) == "Merchant of Manuk") {
			mes "Rtt od d";
			mes "Qwo hd is d irr";
		}
		else {
			mes "BurWehAla";
			mes "tasnarAndu Ie Ru";
		}
		close;
	}
}
man_in01,286,16,1	duplicate(merchant_13_2)	Merchant of Manuk	4_MAN_PIOM
spl_in01,110,326,5	duplicate(merchant_13_2)	Merchant of Splendide	4_F_FAIRYKID4
