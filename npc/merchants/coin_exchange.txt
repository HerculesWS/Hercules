//===== Hercules Script ======================================
//= Coin Merchants
//===== By: ==================================================
//= rAthena Dev Team
//===== Current Version: =====================================
//= 1.2
//===== Description: =========================================
//= [Official Conversion]
//= Coin redemption NPCs.
//===== Additional Comments: =================================
//= 1.0 First version
//= 1.1 Fixed checkweight. [Gepard]
//= 1.2 Optimized and merged into one file. [Euphy]
//============================================================

// Manuk & Splendide
//============================================================
-	script	::merchant_13_2	-1,{
	.@n$ = "["+strnpcinfo(1)+"]";
	mes .@n$;
	if (isequipped(2782) && ep13_2_rhea == 100) {
		if (!checkweight(1201,1)) {
			mes "It looks like you're carrying too many things.";
			mes "Why not put some of your items in storage and come back?";
			close;
		}
		mes "Hello.";
		mes "What are you looking for?";
		next;
		if (strnpcinfo(1) == "Merchant of Manuk") {
			setarray .@items[2],12342,12343,12348;
			.@Book = 11019; //Manuk_Selling_Item
			.@coin = 6080; //Manuk_Coin
			.@i = select("View item description:Manuk's Opportunity:Manuk's Courage:Manuk's Faith:Cancel");
		}
		else {
			setarray .@items[2],12344,12345,12349;
			.@Book = 11018; //Splendide_Selling_Item
			.@coin = 6081; //Splendide_Coin
			.@i = select("View item description:Buy Pinguicula's Fruit Jam:Buy Luciola's Honey Jam:Buy Cornus' Tears:Do nothing");
		}
		mes .@n$;
		if (.@i == 1) {
			mes "Here are the item descriptions.";
			close2;
			readbook .@Book,1;
			end;
		}
		else if (.@i < 5) {
			mes "I can sell you 3 "+getitemname(.@items[.@i])+" for ^3131FF1 coin^000000.";
			next;
			switch(select("Purchase:Do not purchase")) {
			case 1:
				if (countitem(.@coin)) {
					mes .@n$;
					mes "Thank you for coming.";
					delitem .@coin,1;
					getitem .@items[.@i],3;
					close;
				}
				else {
					mes .@n$;
					mes "You don't have enough coins.";
					close;
				}
			case 2:
				mes .@n$;
				break;
			}
		}
		mes "Come again if you change your mind.";
		close;
	}
	else {
		if (strnpcinfo(1) == "Merchant of Manuk") {
			mes "Rtt od d";
			mes "Qwo hd is d irr";
		}
		else {
			mes "BurWehAla";
			mes "tasnarAndu Ie Ru";
		}
		close;
	}
}
man_in01,286,16,1	duplicate(merchant_13_2)	Merchant of Manuk	4_MAN_PIOM
spl_in01,110,326,5	duplicate(merchant_13_2)	Merchant of Splendide	4_F_FAIRYKID4
