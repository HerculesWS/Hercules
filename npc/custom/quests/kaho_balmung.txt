//===== Hercules Script ======================================
//= Balmung & Lord Kaho's Horns Quest
//===== By: ==================================================
//= MagicalTux
//===== Current Version: =====================================
//= 1.1
//===== Description: =========================================
//= Custom quests for Balmung and Lord Kaho's Horns.
//= WARNING: The quests are very easy, so be careful!
//===== Additional Comments: =================================
//= 1.1 Cleaned and optimized. [Euphy]
//============================================================

prontera,158,356,4	script	Royal Messenger	8W_SOLDIER,{
	setarray .@Req1[0],1161,999,120,984,10,996,10,994,10,995,10,997,10;
	setarray .@Req2[0],5013,2298,1,1001,10,996,10,994,10,995,10,997,10;

	mes "[Royal Messenger]";
	mes "Welcome to Prontera.";
	mes "I am the messenger in charge of the Royal Quests.";
	next;
	if(select("Listen...", "Walk away")==2) close;
	mes "[Royal Messenger]";
	mes "There are two quests.";
	mes "Choose the one you like.";
	next;
	set .@i, select("Balmung", "Lord Kaho's Horns", "^777777Never mind.^000000");
	mes "[Royal Messenger]";
	if (.@i==3) {
		mes "Have a nice day.";
		close; }
	mes "You will need the following items:";
	for(set .@j,1; .@j<13; set .@j,.@j+2) {
		mes " ~ "+getd(".@Req"+.@i+"["+(.@j+1)+"]")+"x ^0055FF"+getitemname(getd(".@Req"+.@i+"["+.@j+"]"))+"^000000";
		if (!.@nr && countitem(getd(".@Req"+.@i+"["+.@j+"]")) < getd(".@Req"+.@i+"["+(.@j+1)+"]")) set .@nr,1; }
	mes " ~ 1,000,000z";
	next;
	mes "[Royal Messenger]";
	if (.@nr || Zeny < 1000000) {
		mes "Come back when you meet all the requirements.";
		close; }
	for(set .@j,1; .@j<13; set .@j,.@j+2)
		delitem getd(".@Req"+.@i+"["+.@j+"]"), getd(".@Req"+.@i+"["+(.@j+1)+"]");
	Zeny -= 1000000;
	mes "I see you already have all the items you need. Here's your "+getitemname(getd(".@Req"+.@i+"[0]"))+", as promised.";
	getitem getd(".@Req"+.@i+"[0]"),1;
	close;
}
