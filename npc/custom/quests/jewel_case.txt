//===== Hercules Script ======================================
//= Jewel Case Quest
//===== By: ==================================================
//= Lupus
//===== Current Version: =====================================
//= 1.0
//===== Description: =========================================
//= A quest for players at base level 99,
//= with Jewel Case as a reward.
//============================================================

lighthalzen,108,229,2	script	La Fenn	4_M_JPNOJI,{
	mes "[La Fenn]";
	if(BaseLevel<99) {
		if(Q_99LVL) {
			mes "You... You got younger!!! But you are defenitely YOU!";
			mes "I couldn't think you were able to meet my condition...";
			emotion e_hmm;
			next;
			mes "[La Fenn]";
			mes "Here is your prize. It's yours for good.";
			set Q_99LVL,0;
			getitem Accessory_Box,1; //Jewel Case
			emotion e_grat;
		} else {
			mes callfunc("F_Hi");
			mes "...";
			emotion e_yawn;
			next;
			mes "[La Fenn]";
			mes "I've got a riddle for a strong man of 2nd Class and 99 Base Level.";
			mes "*Yawn* If you meet some, please tell them about my riddle.";
		}
		close;
	}
	if(BaseLevel==99 && Upper==0) {
		if(Q_99LVL) {
			mes "Now go forth and get younger!";
			mes "If you solve my riddle, then I'll give you a magical Jewel Case with a rare accessory.";
		} else {
			mes callfunc("F_Hi")+" The Hero!";
			if(BaseExp<1000000) {
				mes "Now you're strong.";
				mes "But... not quite strong for my riddle.";
				if(rand(2)) {
					mes "Kill "+((1000000-BaseExp)/3765+1)+" more Seals and return.";
				} else {
					mes "Kill "+((1000000-BaseExp)/81+1)+" more Poporings and come back.";
				}
			} else {
				mes "Now you're REALLY strong.";
				mes "And if you get younger than now I'll give you my treasure.";
				set Q_99LVL,1;
				emotion e_no1;
			}
		}
		close;
	}
	emotion e_hmm;
	mes "...";
	close;
}
