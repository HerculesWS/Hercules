//===== Hercules Script ======================================
//= Dead Branch (+Bloody Branch) Quest
//===== By: ==================================================
//= GM-Yevon
//===== Current Version: =====================================
//= 1.1
//===== Description: =========================================
// Simple item trade-in quest, a person can get a Dead Branch
// by simply providing the items: Log x3 (7201), Wooden Heart x1 (7189),
// Trunk x5 (1019), and Wooden Gnarl x1 (7222).
//===== Additional Comments: =================================
//= 1.1 Optimized, added Bloody Branch with 0.01% chance [Lupus]
//============================================================

niflheim,204,179,3	script	Mister Mobry	2_M_MOLGENSTEIN,{

	mes "[Mister Mobry]";
	mes "Trees possess spirits you know...";
	next;

	mes "[Mister Mobry]";
	mes "In fact I know a secret about these spirits that can in some way... perhaps aid you...";
	next;

	mes "[Mister Mobry]";
	mes "I can create a spirited branch... That is, if you can provide me with the correct materials...";
	next;
	while (true) {
		switch (select("What Do I Need?", "Where Can I find this Crap?", "Make me a Dead Branch!", "Forget it...")) {
		case 1:
			mes "[Mister Mobry]";
			mes "Mwehehe... I see you want to control tree spirits am I right? Ok... I need:";
			mes "^1354453 Logs^000000";
			mes "^1354451 Wooden Heart^000000";
			mes "^1354455 Trunk^000000";
			mes "^1354451 Wooden Gnarl^000000";
			next;
			continue;
		case 2:
			mes "[Mister Mobry]";
			mes "You can find the following items from certain monsters heheh...";
			mes "^135445Logs can be found off of Tree Golems.^000000";
			mes "^135445Wooden Hearts the essence of Tree Golems.^000000";
			mes "^135445Trunks? Seriously, think wood...^000000";
			mes "^135445Wooden Gnarl... Gibbet...^000000";
			next;
			continue;
		case 3:
			mes "[Mister Mobry]";
			mes "Well...let us see what you brought me...";
			next;
			if (countitem(Log)<3) {
				mes "[Mister Mobry]";
				mes "^135445Logs^000000 are the body... I need more of them.";
				mes "Get me three logs and I'll make you the spirit...";
				close;
			}
			if (countitem(Heart_Of_Tree)<1) {
				mes "[Mister Mobry]";
				mes "The ^135445Wooden Heart^000000 is the essence of the spirit.";
				mes "Get me one wooden heart and I'll make you the spirit...";
				close;
			}
			if (countitem(Wooden_Block)<5) {
				mes "[Mister Mobry]";
				mes "What? You couldn't even find ^1354455 Trunks^000000?";
				mes "Kill Elder Willows or something... Geez... no trunks heh... pathetic.";
				close;
			}
			if (countitem(Tree_Knot)<1) {
				mes "[Mister Mobry]";
				mes "How can I make one without a ^135445Wooden Gnarl^000000?";
				mes "I said Gibbet... they are all around this place...";
				close;
			}
			delitem 7201,3;
			delitem 7189,1;
			delitem 1019,5;
			delitem 7222,1;
			mes "[Mister Mobry]";
			mes "Heh... Hope you have fun with the tree spirits... Careful now...";
			mes "Heh... careful now? What do I care if you di..... Never mind.";
			if (rand(1000)==0) {
				getitem Bloody_Dead_Branch,1; //Bloody Branch
				close;
			}
			getitem Branch_Of_Dead_Tree,1; //Dead Branch
			close;
		case 4:
			mes "[Mister Mobry]";
			mes "No spirit for you... then go!";
			close;
		}
	}
}
