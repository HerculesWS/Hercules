//================= Hercules Script =======================================
//=       _   _                     _
//=      | | | |                   | |
//=      | |_| | ___ _ __ ___ _   _| | ___  ___
//=      |  _  |/ _ \ '__/ __| | | | |/ _ \/ __|
//=      | | | |  __/ | | (__| |_| | |  __/\__ \
//=      \_| |_/\___|_|  \___|\__,_|_|\___||___/
//================= License ===============================================
//= This file is part of Hercules.
//= http://herc.ws - http://github.com/HerculesWS/Hercules
//=
//= Copyright (C) 2012-2015  Hercules Dev Team
//= Copyright (C)  L0ne_W0lf
//= Copyright (C)  Lupus
//= Copyright (C)  Samuray22
//= Copyright (C)  TonyMan
//=
//= Hercules is free software: you can redistribute it and/or modify
//= it under the terms of the GNU General Public License as published by
//= the Free Software Foundation, either version 3 of the License, or
//= (at your option) any later version.
//=
//= This program is distributed in the hope that it will be useful,
//= but WITHOUT ANY WARRANTY; without even the implied warranty of
//= MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//= GNU General Public License for more details.
//=
//= You should have received a copy of the GNU General Public License
//= along with this program.  If not, see <http://www.gnu.org/licenses/>.
//=========================================================================
//= Quest NPCs related to Lutie
//================= Description ===========================================
//= Blush, Cake Hat, Candle, Chef Hat, Raccoon Hat, Rainbow Eggshell, Spore
//= Hat, Wonder Nutshell Quests
//================= Current Version =======================================
//= 1.3b
//=========================================================================

xmas,117,295,4	script	Vending Machine Man	4_M_BIBI,{
	mes "[Titicupe]";
	mes "Hmm...?";
	mes "I, Titicupe, esteemed manager of the Toy Factory and genius inventor, has done it again! I've just created the world's most sophisticated vending machine!";
	next;
	switch(select("Talk to him about the Vending Machine", "Items in the Vending Machine", "Stop talking")) {
	case 1:
		mes "[Titicupe]";
		mes "Can you see the little snowman to the left side of me? Can you? If you look at his mouth closely, you'll see that it's been built quite largely. You might say, a bit too large for normal purposes...";
		next;
		mes "[Titicupe]";
		mes "You see, that snowman is actually an amazing vending machine that I invented out of my blood, sweat, tears and snow.";
		next;
		mes "[Titicupe]";
		mes "You can put all sorts of things inside its mouth, and if you insert the right items, you'll get some hats in return...";
		next;
		mes "[Titicupe]";
		mes "Fashionable hats that you've always dreamed of wearing...! Bwahahahaha~!";
		next;
		mes "[Titicupe]";
		mes "Oh, this creation of mine is so magnificent!";
		emotion e_kis2;
		next;
		mes "[Titicupe]";
		mes "And the headgears--! I... I can't contain myself!";
		next;
		mes "^3355FFToy factory manager Titicupe jumps and convulses with joy. Clearly he's insane, but it may be possible that he may be brilliant.^000000";
		emotion e_gasp;
		close;
	case 2:
		mes "[Titicupe]";
		mes "Oh right! You need to know what kind of items to put into the Vending Machine to get what you want, yes? Go ahead... Ask me~";
		emotion e_ic;
		next;
		switch(select("Raccoon Hat", "Spore Hat", "Wonder Nutshell", "Ranbow Eggshell", "Blush", "Chef Hat", "Candle", "Cake Hat", "End Conversation")) {
		case 1:
			mes "[Titicupe]";
			mes "Raccoon Hat?";
			mes "1 Kitty Band";
			mes "20 Dragon Scale";
			mes "200 Tough Scalelike Stem";
			mes "300 Sea-otter Fur";
			break;
		case 2:
			mes "[Titicupe]";
			mes "Spore Hat?";
			mes "850 Poison Spore";
			mes "300 Burnt Tree";
			mes "1 Tongue";
			break;
		case 3:
			mes "[Titicupe]";
			mes "Wonder Nutshell?";
			mes "1 Nut Shell";
			mes "500 Wing of Dragonfly";
			break;
		case 4:
			mes "[Titicupe]";
			mes "Rainbow Eggshell?";
			mes "1 Egg Shell";
			mes "1 Cobaltblue Dyestuffs";
			mes "50 Claw of Desert Wolf";
			break;
		case 5:
			mes "[Titicupe]";
			mes "Blush?!";
			mes "You!";
			mes "You know something...";
			mes "Heh heh~ Anyway";
			mes "100 Alice's Apron";
			break;
		case 6:
			mes "[Titicupe]";
			mes "Chef Hat?";
			mes "120 Piece of Cake";
			mes "1 White Dyestuffs";
			mes "330 Feather";
			mes "450 Dragon Scale";
			break;
		case 7:
			mes "[Titicupe]";
			mes "Candle?";
			mes "1 Bomb Wick";
			mes "50 Matchstick";
			mes "100 Royal Jelly";
			break;
		case 8:
			mes "[Titicupe]";
			mes "Cake Hat?";
			mes "10 Candy";
			mes "5 Candy Cane";
			mes "15 Well-baked Cookie";
			mes "20 Piece Of Cake";
			mes "10 Steel";
			break;
		case 9:
			mes "[Titicupe]";
			mes "Ask me anytime. Those kinds of questions are no problem for geniuses like me!";
		}
		next;
		mes "[Titicupe]";
		mes "So happy!!";
		mes "I'm a genius~!";
		mes "Ho ho ho";
		mes "Ho ho ho ho!";
		close;
	case 3:
		mes "[Titicupe]";
		mes "Sometimes, I can't believe how magnificent this invention of mine is...!";
		next;
		mes "[Titicupe]";
		mes "I...";
		mes "I love you";
		mes "Mister Snowman";
		mes "Vending Machine.";
		emotion e_kis2;
		next;
		mes "^3355FFToy factory manager Titicupe begins to jump around and emit screams of unbridled ecstacy. At this point, it's not difficult to doubt his sanity, as well as his genius.";
		close;
	}
}

xmas,115,297,4	script	Vending Machine	HIDDEN_NPC,{
	mes "[Audi]";
	mes "^555555Exciting item vending machine. Invented by great Titicupe. Select the desired item and place required items into snowmouth receptacle.^000000";
	next;
	switch(select("^FF3355Notice (Must Read)^000000", "Raccoon Hat", "Spore Hat", "Wonder Nutshell", "Rainbow Eggshell", "Blush", "Chef Hat", "Candle", "Cake Hat", "Cancel")) {
	case 1:
		mes "[Audi]";
		mes "Hi~ !";
		mes "I need to tell you one important thing. This vending machine is not equipped to differentiate between special items and ordinary items.";
		next;
		mes "[Audi]";
		mes "So if you use an item that has been ^FF3355upgraded, or has a card compounded to it^000000, any cards or upgrades will be lost in creating the new item.";
		next;
		mes "[Audi]";
		mes "So when you try to exchange and generate items, please put all the valuable items you won't be using into the Kafra Storage.";
		next;
		mes "[Audi]";
		mes "Please keep this in mind because this Vending Machine does not provide any returns, refunds or exchanges.";
		next;
		mes "[Audi]";
		mes "Have a great time!";
		mes "Thank you!";
		close;
	case 2:
		if ((countitem(Cat_Hairband) > 0) && (countitem(Dragon_Scale) > 19) && (countitem(Tough_Scalelike_Stem) > 199) && (countitem(Sea_Otter_Leather) > 299)) {
			mes "^3355FFYou gingerly place the items into the snowman's mouth.^000000";
			next;
			delitem Cat_Hairband,1;
			delitem Dragon_Scale,20;
			delitem Tough_Scalelike_Stem,200;
			delitem Sea_Otter_Leather,300;
			emotion e_oh;
			mes "^3355FF*Vroooooom~~*";
			mes "*Bzzzzzt*";
			mes "*choogachooga*";
			mes "*Kapang!*^000000";
			getitem Smokie_Hat,1; // Smokie_Hat
			next;
			mes "[Audi]";
			mes "^555555Thank you for coming by.";
			mes "Please come again!";
			mes "Oh, and tell a friend!^000000";
			close;
		}
		break;
	case 3:
		if ((countitem(Poison_Spore) > 849) && (countitem(Burn_Tree) > 299) && (countitem(Thin_N_Long_Tongue) > 0)) {
			mes "^3355FFYou gingerly place the items into the snowman's mouth.^000000";
			next;
			delitem Poison_Spore,850;
			delitem Burn_Tree,300;
			delitem Thin_N_Long_Tongue,1;
			emotion e_oh;
			mes "^3355FF*Vroooooom~~*";
			mes "*Bzzzzzt*";
			mes "*choogachooga*";
			mes "*Kapang!*";
			mes "*Wek Wek*^000000";
			getitem Spore_Hat,1; // Spore_Hat
			next;
			mes "^555555Thank you for coming by.";
			mes "Please come again!";
			mes "Oh, and tell a friend!^000000";
			close;
		}
		break;
	case 4:
		if ((countitem(Fruit_Shell) > 0) && (countitem(Dragon_Fly_Wing) > 499)) {
			mes "^3355FFYou gingerly place the items into the snowman's mouth.^000000";
			next;
			delitem Fruit_Shell,1;
			delitem Dragon_Fly_Wing,500;
			mes "^3355FF*Vroooooom~~*";
			mes "*Bzzzzzt*";
			mes "*choogachooga*";
			mes "*Kapang!*";
			mes "*BeepBoopBeepBoop*";
			mes "*Beeeeeeoop*^000000";
			emotion e_oh;
			getitem Mystery_Fruit_Shell,1; // Mystery_Fruit_Shell
			next;
			mes "[Audi]";
			mes "^555555Thank you for coming by.";
			mes "Please come again!";
			mes "Oh, and tell a friend!^000000";
			close;
		}
		break;
	case 5:
		if ((countitem(Egg_Shell) > 0) && (countitem(Cobaltblue_Dyestuffs) > 0) && (countitem(Claw_Of_Desert_Wolf) > 49)){
			mes "^3355FFYou gingerly place the items into the snowman's mouth.^000000";
			next;
			delitem Egg_Shell,1;
			delitem Cobaltblue_Dyestuffs,1;
			delitem Claw_Of_Desert_Wolf,50;
			emotion e_oh;
			mes "^3355FF*Vroooooom~~*";
			mes "*Bzzzzzt*";
			mes "*choogachooga*";
			mes "*OoooEeeeeeee~*^000000";
			getitem Mottled_Egg_Shell,1; // Mottled_Egg_Shell
			next;
			mes "[Audi]";
			mes "^555555Thank you for coming by.";
			mes "Please come again!";
			mes "Oh, and tell a friend!^000000";
			close;
		}
		break;
	case 6:
		if (countitem(Alices_Apron) > 99) {
			mes "^3355FFYou gingerly place all 100 Aprons into the snowman's mouth.^000000";
			next;
			delitem(Alices_Apron, 100);
			emotion e_oh;
			mes "^3355FF*Vroooooom~~*";
			mes "*Bzzzzzt*";
			mes "*choogachooga*";
			mes "*Kapang!*";
			mes "*ChoopChoop*";
			mes "*Chaaawah!*^000000";
			getitem Blush,1; // Blush
			mes "[Audi]";
			mes "^555555Thank you for coming by.";
			mes "Please come again!";
			mes "Oh, and tell a friend!^000000";
			next;
			mes "[Titicupe]";
			mes "You got Blush?! Huh. I guess you really like looking glamourous~! I bet you really appreciate my genius now, huh?";
			close;
		}
		break;
	case 7:
		if ((countitem(Piece_Of_Cake) > 119) && (countitem(White_Dyestuffs) > 0) && (countitem(Feather) > 329) && (countitem(Dragon_Scale) > 449)) {
			mes "^3355FFYou gingerly place the items into the snowman's mouth.^000000";
			next;
			delitem Piece_Of_Cake,120;
			delitem White_Dyestuffs,1;
			delitem Feather,330;
			delitem Dragon_Scale,450;
			emotion e_oh;
			mes "^3355FF*Vroooooom~~*";
			mes "*Bzzzzzt*";
			mes "*choogachooga*";
			mes "*Kapang!*";
			mes "*Muuuuuuugeeee*^000000";
			getitem Hat_Of_Cook,1; // Hat_Of_Cook
			next;
			mes "[Audi]";
			mes "^555555Thank you for coming by.";
			mes "Please come again!";
			mes "Oh, and tell a friend!^000000";
			close;
		}
		break;
	case 8:
		if ((countitem(Bomb_Wick) > 0) && (countitem(Matchstick) > 49) && (countitem(Royal_Jelly) > 99)) {
			mes "^3355FFYou gingerly place the items into the snowman's mouth.^000000";
			next;
			delitem Bomb_Wick,1;
			delitem Matchstick,50;
			delitem Royal_Jelly,100;
			emotion e_oh;
			mes "^3355FF*Vroooooom~~*";
			mes "*Bzzzzzt*";
			mes "*choogachooga*";
			mes "*Kapang!*^000000";
			getitem Candle,1; // Candle
			next;
			mes "[Audi]";
			mes "^555555Thank you for coming by.";
			mes "Please come again!";
			mes "Oh, and tell a friend!^000000";
			close;
		}
		break;
	case 9:
		if ((countitem(Candy) > 9) && (countitem(Candy_Striper) > 4) && (countitem(Well_Baked_Cookie) > 14) && (countitem(Piece_Of_Cake) > 19) && (countitem(Steel) > 9)) {
			mes "^3355FFYou gingerly place the items into the snowman's mouth.^000000";
			next;
			delitem Candy,10;
			delitem Candy_Striper,5;
			delitem Well_Baked_Cookie,15;
			delitem Piece_Of_Cake,20;
			delitem Steel,10;
			emotion e_oh;
			mes "^3355FF*Vroooooom~~*";
			mes "*Bzzzzzt*";
			mes "*choogachooga*";
			mes "*Kapang!*^000000";
			getitem Hat_Of_Cake,1; // Hat_Of_Cake
			next;
			mes "[Audi]";
			mes "^555555Thank you for coming by.";
			mes "Please come again!";
			mes "Oh, and tell a friend!^000000";
			close;
		}
		break;
	case 10:
		mes "[Audi]";
		mes "^555555Please...";
		mes "Insert...";
		mes "Items.^000000";
		close;
	}
	emotion e_X;
	mes "[Audi]";
	mes "^555555Error Error!";
	mes "Incorrect items!^000000";
	next;
	mes "[Titicupe]";
	mes "I...";
	mes "I guess a lot of people would do anything to get their hands on some Blush.";
	close;
}
