//================= Hercules Script =======================================
//=       _   _                     _
//=      | | | |                   | |
//=      | |_| | ___ _ __ ___ _   _| | ___  ___
//=      |  _  |/ _ \ '__/ __| | | | |/ _ \/ __|
//=      | | | |  __/ | | (__| |_| | |  __/\__ \
//=      \_| |_/\___|_|  \___|\__,_|_|\___||___/
//================= License ===============================================
//= This file is part of Hercules.
//= http://herc.ws - http://github.com/HerculesWS/Hercules
//=
//= Copyright (C) 2012-2015  Hercules Dev Team
//= Copyright (C)  Haru
//= Copyright (C)  L0ne_W0lf
//= Copyright (C)  Playtester
//= Copyright (C)  Lupus
//= Copyright (C)  Gepard
//=
//= Hercules is free software: you can redistribute it and/or modify
//= it under the terms of the GNU General Public License as published by
//= the Free Software Foundation, either version 3 of the License, or
//= (at your option) any later version.
//=
//= This program is distributed in the hope that it will be useful,
//= but WITHOUT ANY WARRANTY; without even the implied warranty of
//= MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//= GNU General Public License for more details.
//=
//= You should have received a copy of the GNU General Public License
//= along with this program.  If not, see <http://www.gnu.org/licenses/>.
//=========================================================================
//= Cool Event Corp. Staff
//================= Description ===========================================
//= Cool Event Corp. Staff (Save, Storage & Pushcart Service) in Arunafeltz
//= Region.
//================= Current Version =======================================
//= 2.0
//=========================================================================

//== Main Function =========================================
// Arguments:
// - 0: Used to determine which menu to display.
// - 1: Used to determine the location displayed in the save message.
function	script	F_ZondaStaff	{
	cutin "zonda_01",2;
	mes "[Cool Event Corp. Staff]";
	mes "Welcome to Cool Event Corp.";
	mes "Our staff is always working";
	mes "to surpass your expactations";
	mes "for quality service. So how";
	mes "may I assist you today?";
	next;
	switch (getarg(0)) {
		// Save, Storage, Pushcart, Storage Password (Hugel)
		case 0: setarray @Z_Menu0$, "Save", "Use Storage", "Rent a Pushcart", "Storage Password Service", "Cancel"; break;
		// Save, Storage, Pushcart, Storage Password (Veins, Rachel)
		default:
		case 1: setarray @Z_Menu0$, "Save", "Use Storage", "Teleport Service", "Rent a Pushcart", "Storage Password Service", "Cancel"; break;
	}
	.@menu$ = "";
	for (.@i = 0; .@i < getarraysize(@Z_Menu0$); ++.@i)
		.@menu$ += @Z_Menu0$[.@i] + ":";
	.@j = select (.@menu$) - 1;
	if (@Z_Menu0$[.@j] == "Save") {
		mes "[Cool Event Corp. Staff]";
		mes "Your Respawn Point";
		mes "has been saved here";
		mes getarg(1);
		mes "Thank you for using the";
		mes "Cool Event Corp. service~";
		return;
	} else if (@Z_Menu0$[.@j] == "Use Storage") {
		callfunc "F_KafStor",0,40,6,"[Cool Event Corp. Staff]";
		close2;
		cutin "", 255;
		end;
	} else if (@Z_Menu0$[.@j] == "Teleport Service") {
		callfunc "F_KafTele", 6, "[Cool Event Corp. Staff]";
		close2;
		cutin "", 255;
		end;
	} else if (@Z_Menu0$[.@j] == "Rent a Pushcart") {
		callfunc "F_KafCart", 6, 800, "[Cool Event Corp. Staff]";
		close2;
		cutin "", 255;
		end;
	} else if (@Z_Menu0$[.@j] == "Storage Password Service") {
		callfunc("F_SetKafCode","[Cool Event Corp. Staff]","Cool Event Corp.");
		//the function closes msg and removes cutins
	} else {
		mes "[Cool Event Corp. Staff]";
		mes "Cool Event Corp. is always";
		mes "striving to provide the best";
		mes "services for our customers.";
		mes "Help us become the best by";
		mes "providing us with your opinions";
		mes "and honest feedback. Thank you.";
		close2;
	}
	cutin "", 255;
	end;
}

hugel,88,168,4	script	Cool Event Corp. Staff::CoolEventCorpStaffHugel	4_M_ZONDAMAN,{
	callfunc "F_KafSet";
	callfunc "F_ZondaStaff", 0, "in the village of Hugel.";
	savepoint "hugel",96,155;
	close2;
	cutin "", 255;
	end;
}

rachel,109,138,5	script	Cool Event Corp. Staff::CoolEventCorpStaffRachel	4_M_ZONDAMAN,{
	callfunc "F_KafSet";
	callfunc "F_ZondaStaff", 1, "in the town of Rachel.";
	savepoint "rachel",113,137;
	close2;
	cutin "", 255;
	end;
}

veins,208,128,5	script	Cool Event Corp. Staff::CoolEventCorpStaffVeins	4_M_ZONDAMAN,{
	callfunc "F_KafSet";
	callfunc "F_ZondaStaff", 1, "in the town of Veins.";
	savepoint "veins",204,103;
	close2;
	cutin "", 255;
	end;
}
