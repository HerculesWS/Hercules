//================= Hercules Script =======================================
//=       _   _                     _
//=      | | | |                   | |
//=      | |_| | ___ _ __ ___ _   _| | ___  ___
//=      |  _  |/ _ \ '__/ __| | | | |/ _ \/ __|
//=      | | | |  __/ | | (__| |_| | |  __/\__ \
//=      \_| |_/\___|_|  \___|\__,_|_|\___||___/
//================= License ===============================================
//= This file is part of Hercules.
//= http://herc.ws - http://github.com/HerculesWS/Hercules
//=
//= Copyright (C) 2012-2020 Hercules Dev Team
//= Copyright (C) Silent
//= Copyright (C) Vicious
//= Copyright (C) Silentdragon
//= Copyright (C) Mass Zero
//= Copyright (C) L0ne_W0lf
//= Copyright (C) Lupus
//= Copyright (C) Poki
//= Copyright (C) Nana
//=
//= Hercules is free software: you can redistribute it and/or modify
//= it under the terms of the GNU General Public License as published by
//= the Free Software Foundation, either version 3 of the License, or
//= (at your option) any later version.
//=
//= This program is distributed in the hope that it will be useful,
//= but WITHOUT ANY WARRANTY; without even the implied warranty of
//= MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//= GNU General Public License for more details.
//=
//= You should have received a copy of the GNU General Public License
//= along with this program.  If not, see <http://www.gnu.org/licenses/>.
//=========================================================================
//= Valkyrie Realm
//================= Description ===========================================
//= Quest to become a Tranceded Class. (Advanced/Rebirthed)
//================= Current Version =======================================
//= 3.1
//=========================================================================

valkyrie,48,86,4	script	Valkyrie#	4_F_VALKYRIE,{
	if (ADVJOB != 0 || Upper == 1) {
		mes "[Valkyrie]";
		mes "Welcome";
		mes "to Valhalla,";
		mes "the Hall of Honor.";
		next;
		mes "[Valkyrie]";
		mes "Please make";
		mes "yourself comfortable";
		mes "while you are here.";
		mes "Honor to the warriors!";
		close;
	}
	else if (BaseLevel > 98 && JobLevel > 49 && Class >= Job_Knight && Class <= Job_Crusader2) {
		mes "[Valkyrie]";
		mes "Welcome";
		mes "to Valhalla,";
		mes "the Hall of Honor.";
		next;
		mes "[Valkyrie]";
		mes "You will now end";
		mes "your present life and";
		mes "begin an entirely new life.";
		mes "Honor to the warriors!";
		next;
		if (Weight > 0 || Zeny > 0 || checkcart() || checkfalcon() || checkmount()) {
			mes "[Valkyrie]";
			mes "There are a few things you must";
			mes "do before we start. You must";
			mes "first empty your mind and body.";
			mes "Honor comes when you abandon";
			mes "all your selfish desires...";
			next;
			mes "[Valkyrie]";
			mes "You cannot take anything with you to the next life. Your items, zeny, pets and Pushcart all have to be left behind.";
			next;
			mes "[Valkyrie]";
			mes "When you are ready";
			mes "please return to me,";
			mes "brave adventurer.";
			close2;
			warp "yuno_in02",93,205;
			end;
		}
		else {
			mes "[Valkyrie]";
			mes "I see you've already";
			mes "released yourself from";
			mes "all worldy attachments,";
			mes ""+ strcharinfo(PC_NAME) +".";
			next;
			mes "[Valkyrie]";
			mes "That's an admirable attitude for an adventurer such as yourself. Honor comes when you abandon all personal desires for the sake of mankind.";
			next;
			if (SkillPoint) {
				mes "[Valkyrie]";
				mes "Hmm... I sense that you have";
				mes "some lingering attachment or";
				mes "unfinished business in your";
				mes "current life. Take care of that,";
				mes "and bring closure to your present life.";
				close2;
				warp "yuno_in02",93,205;
				end;
			}
			mes "[Valkyrie]";
			mes "Now, let me remove all";
			mes "of your present memories...";
			mes "However, you will be able to";
			mes "remember the most honorable";
			mes "moments of this life.";
			next;
			mes "[Valkyrie]";
			mes "With one,";
			mes "I will ask the";
			mes "goddess Urd to remove";
			mes "all of your present";
			mes "memories.";
			next;
			mes "[Valkyrie]";
			mes "With two,";
			mes "I will ask the";
			mes "goddess Verdandi to keep";
			mes "and record the most honorable moments of your present life.";
			next;
			mes "[Valkyrie]";
			mes "With three,";
			mes "I will ask the";
			mes "goddess Skuld to";
			mes "guide you to your";
			mes "next life.";
			next;
			mes "[Valkyrie]";
			mes "One...";
			callfunc "F_ClearJobVar";
			next;
			mes "[Valkyrie]";
			mes "One...";
			mes "Two......";
			next;
			mes "[Valkyrie]";
			mes "One...";
			mes "Two......";
			mes "And Three.";
			ADVJOB = Class+Job_Novice_High; //memo the target 3rd Job ID
			if(ADVJOB == Job_Lord_Knight2) ADVJOB = Job_Lord_Knight;
			if(ADVJOB == Job_Paladin2) ADVJOB = Job_Paladin;
			jobchange Job_Novice_High;
			resetlvl(1);
			MISC_QUEST |= 1024; //<-reset Skill Reset Event
			skill 142,1,0;
			skill 143,1,0;
			completequest 1000;
			next;
			mes "[Valkyrie]";
			mes "Congratulations.";
			mes "You are now reborn";
			mes "into a brand new life.";
			mes "Please take these small gifts";
			mes "in preparation for your new adventures.";
			getitem Knife_,1;
			getitem Cotton_Shirt_,1;
			next;
			mes "[Valkyrie]";
			mes "I wish that the release the goddess Urd has granted you proves to be a blessing. I hope that the memories Verdandi has recorded will always honor you.";
			next;
			mes "[Valkyrie]";
			mes "And I pray that the new life to which the goddess Skuld will guide you will be even more honorable than your last.";
			close2;
			switch (ADVJOB) {
			case 4008:
			case 4015:
				if (RENEWAL) warp "izlude",129,97;
				else warp "izlude",94,103;
				break;
			case 4009:
			case 4016:
				warp "prontera",273,354; break;
			case 4010:
			case 4017:
				warp "geffen",120,60; break;
			case 4011:
			case 4019:
				warp "alberta",116,57; break;
			case 4012:
			case 4020:
			case 4021:
				warp "payon",69,100; break;
			case 4013:
			case 4018:
				warp "morocc",154,50; break;
			default:
				warp "yuno_in02",93,205; break;
			}
			end;
		}
	}
	else {
		mes "[Valkyrie]";
		mes "Welcome";
		mes "to Valhalla,";
		mes "the Hall of Honor.";
		next;
		mes "[Valkyrie]";
		mes "Unfortunately, you have not yet been invited here. I ask you to leave immediately. Honor to the warriors!";
		close2;
		warp "yuno_in02",93,205;
		end;
	}
}

yuno_in02,88,164,5	script	Metheus Sylphe#Library	2_M_SAGE_B,{
	if (BaseLevel > 98 && JobLevel > 49 && Class >= Job_Knight && Class <= Job_Crusader2) {
		if (valkyrie_Q == 0) {
			mes "[Metheus Sylphe]";
			mes "Welcome to the Library of the Schweicherbil Magic Academy.";
			if (RENEWAL) {
				if (questprogress(5160) != 0) { // Quest #5160 - Library Mistake
					mes("I assume you have come here to confirm the status of the library guards report?");
					next();
					if (select((countitem(Captured_Book) > 0 ? "I'm here for the book." : "Tell me the report."), "Donate Zeny") == 1) {
						if (countitem(Captured_Book) != 0) {
							mes("[Metheus Sylphe]");
							mes("Just now the libary guard report aren't helpful, but there is a reason for that...");
							next();
							mes("[Metheus Sylphe]");
							mes("Most importantly, you found the escaped book!");
							next();
							mes("[Metheus Sylphe]");
							mes("As promised, I'll allow you to read the book of Ymir, you may now go in and read the book.");
							if (countitem(Captured_Book) == 0)
								close();
							delitem(Captured_Book, countitem(Captured_Book));
							valkyrie_Q = 1;
							if (questprogress(5160) != 0)
								completequest 5160;
							close();
						}
						mes("[Metheus Sylphe]");
						mes("This is a note from the library guards we were searching for.");
						next();
						mes("[Metheus Sylphe]");
						mes("Search for the escaped book near the Juno Fields.");
						next();
						mes("[Metheus Sylphe]");
						mes("However, there is no detailed description of the location. I'm glad that the search range was reduced.");
						next();
						mes("[Metheus Sylphe]");
						mes("Search for the escaped book near the Juno Fields.");
						next();
						mes("[Metheus Sylphe]");
						mes("Why don't you try search around Kiel Heir Area first.");
						next();
						mes("[Metheus Sylphe]");
						mes("Because there are more than one book escaped.");
						close();
					}
					if (Zeny < 1285000) {
						mes("[Metheus Sylphe]");
						mes("It's all good, but I don't think there's enough Zeny to make donation. Donation requires 1,285,000 Zeny.");
						close();
					}
					mes("[Metheus Sylphe]");
					mes("Ah?! Did you give up searching for the escaped books?");
					next();
					if (select("Unable to tell.", "Just Donate..") != 2) {
						mes("[Metheus Sylphe]");
						mes("Haha. I thought you picked up Zeny anywhere.");
						close();
					}
					Zeny -= 1285000;
					valkyrie_Q = 1;
					setquest(1000);
					if (questprogress(5160) != 0)
						completequest(5160);
					mes("[Metheus Sylphe]");
					mes("Thank you, your donation will be used for a good cause. You may now go in and read the book.");
					close();
				}
			}
			mes "I assume you have come here";
			mes "to read the 'Book of Ymir.'";
			next;
			mes "[Metheus Sylphe]";
			mes "Unfortunately, the original copy of the book has been damaged over time. We currently only allow the public to view a copy of the book.";
			next;
			mes "[Metheus Sylphe]";
			mes "Also, in order to preserve the original 'Book of Ymir,' we have decided to accept donations from people who wish to read the copy we have provided.";
			next;
			mes "[Metheus Sylphe]";
			mes "The suggested";
			mes "donation amount is";
			mes "1,285,000 zeny.";
			next;
			switch (select("Donate.", (RENEWAL ? "Can't we just see it?" : ""), "Cancel.")) {
			case 1:
				if (Zeny >= 1285000) {
					Zeny -= 1285000;
					valkyrie_Q = 1;
					setquest(1000);
					mes "[Metheus Sylphe]";
					mes "Thank you, your donation will be used for a good cause. You may";
					mes "now go in and read the book.";
					close;
				}
				mes "[Metheus Sylphe]";
				mes "Unfortunately, you don't seem to possess enough zeny at the moment. Please check your funds and come back again.";
				close;
			case 2:
				mes("[Metheus Sylphe]");
				mes("It is to preserve the original 'Book of Ymir,' we have decided to accept donations from people who wish to read the copy we have provided.");
				next();
				mes("[Metheus Sylphe]");
				mes("Although I understand your feeling, but that's the rule.");
				next();
				select("But, I dont have enough Zeny.");
				mes("[Metheus Sylphe]");
				mes("The rules stated that everyone must made a donation, if they want to read the book...");
				next();
				mes("[Metheus Sylphe]");
				mes("I guess sometime when life is difficult, we may not follow with the principle?");
				next();
				if (select("I must follow the principle!", "I'll do anything.") != 2) {
					close();
				}
				mes("[Metheus Sylphe]");
				mes("I see enthusiasm within you, but I couldn't let you enter for free.");
				next();
				mes("[Metheus Sylphe]");
				mes("So help me resolve the problems happen in the Schweicherbil Magic Academy's library.");
				mes("I'll help you to read the book of Ymir.");
				next();
				while (true) {
					switch (select("What happened?", "Where does it escaped?", "I'll find it.", "Ignore")) {
					case 1:
						mes("[Metheus Sylphe]");
						mes("This is really a headache.");
						mes("The library consists of a lot undisclosed books.");
						next();
						mes("[Metheus Sylphe]");
						mes("For example, ^0055FFRideword Hat^000000 that would attack humans can't be disclose to the public.");
						next();
						mes("[Metheus Sylphe]");
						mes("But not long ago, part of the undisclosed books escaped from the library...");
						next();
						mes("[Metheus Sylphe]");
						mes("Although the library guards are currently searching for it, but failed due to lack of manpower.");
						next();
						continue;
					case 2:
						mes("[Metheus Sylphe]");
						mes("We're still trying to figure out where it escaped to.");
						mes("I'll inform you when the library guards have any news for it.");
						next();
						continue;
					case 3:
						mes("[Metheus Sylphe]");
						mes("Do you already know the problem now? That's weird. Did you know anything else?");
						next();
						switch (select("Sorry.", "Worry about gift for girlfriend?", "Worry about the lunch?", "Worry about the escaped books?")) {
						case 1:
							mes("[Metheus Sylphe]");
							mes("It seemed to be too. I think we need to know what are the problem first.");
							next();
							continue;
						case 2:
							mes("[Metheus Sylphe]");
							mes("Isn't it polite to ask if you already have a girlfriend? But this isn't a problem.");
							next();
							continue;
						case 3:
							mes("[Metheus Sylphe]");
							mes("It's not a problem these days because I'm not having lunch yet. You should listen to the library problem, not my problem.");
							next();
							continue;
						case 4:
							mes("[Metheus Sylphe]");
							mes("Ah! You knew the problem already?");
							mes("or the news already leaked to public?");
							next();
							mes("[Metheus Sylphe]");
							mes("Wait a moment, the library guard has returned with the report.");
							next();
							mes("[Metheus Sylphe]");
							mes("Let's hear the report before you left!");
							if (questprogress(5160) == 0)
								setquest(5160); // Quest #5160 - Library Mistake
							close();
						}
					case 4:
						close();
					}
				}
				end;
			default:
				break;
			}
			mes "[Metheus Sylphe]";
			mes "Take your time, and";
			mes "enjoy your travels.";
			close;
		}
		mes("[Metheus Sylphe]");
		if (questprogress(5160) != 0)
			mes("Once again, thank you for your generous assistance. Feel free to read a carbon copy of the 'Book of Ymir' at your leisure.");
		else
			mes("Once again, thank you for your generous donation. Feel free to read a carbon copy of the 'Book of Ymir' at your leisure.");
		close();
	}
	mes "[Metheus Sylphe]";
	mes "Welcome to the Library of the Schweicherbil Magic Academy.";
	mes "Here, we have a countless number of books. Please take your time and look around.";
	close;

OnKill:
	if (questprogress(5160) == 1) // Quest #5160 - Library Mistake
		getitem(Captured_Book, 1);
	end;
}

yuno_in02,93,207,1	script	Book of Ymir	HIDDEN_NPC,{
	if (ADVJOB != 0 || Upper == 1) {
		mes "[The Book of Ymir]";
		mes "...The entrance to the Hall of Honor is open to anyone who has moved forward into their next life. It is there to help heroes decide what they want to do, and can lead them to anywhere in this world.";
		next;
		mes "[The Book of Ymir]";
		mes "In the Hall of Honor, everything is prepared for heroes. It is rumored that any wish that cannot be fulfilled in our reality can be realized in the Hall of Honor.";
		next;
		if (select("Stop reading.", "Continue reading.") == 1) {
			mes "[The Book of Ymir]";
			mes ".....";
			close;
		}
		mes "[The Book of Ymir]";
		mes "There is a forgotten path which leads to the Hall of Honor, the closest place to the heavens. The ordinary will never discover this place...";
		close2;
		warp "valkyrie",48,8;
		end;
	}
	else if (BaseLevel > 98 && JobLevel > 49 && Class >= Job_Knight && Class <= Job_Crusader2) {
		if (valkyrie_Q != 0) {
			mes "[The Book of Ymir]";
			mes "...Therefore, ancient heroes were";
			mes "always in anguish, knowing that";
			mes "eventually, they were mortal and";
			mes "would pass from this realm...";
			next;
			mes "[The Book of Ymir]";
			mes "There were no documents,";
			mes "songs, or remaining folklore that had any information on life after death. However, I recently uncovered an old scroll";
			mes "about Valkyrie...";
			next;
			mes "[The Book of Ymir]";
			mes "Valkyrie...";
			mes "The legendary";
			mes "guardian angel.";
			mes "Angel of Ragnarok.";
			next;
			mes "[The Book of Ymir]";
			mes "Adventurers of great strength";
			mes "and bravery will be lead by";
			mes "Valkyrie to Valhalla, the Hall";
			mes "of Honor. There, they will be";
			mes "given a new life.";
			next;
			mes "[The Book of Ymir]";
			mes "Reborn, they will live again as";
			mes "even greater heroes that will";
			mes "brighten the world. Bodies that";
			mes "were exhausted will be filled";
			mes "with energy...";
			next;
			mes "[The Book of Ymir]";
			mes "And their souls will be given abilities with the heart of Ymir. However, the heart of Ymir was totally destroyed and scattered all over the world after the battle for Rune-Midgarts.";
			next;
			mes "[The Book of Ymir]";
			mes "I have found a small amount of";
			mes "Ymir heart pieces over a long";
			mes "long period of time. But I can't";
			mes "confirm if the story of Valkyrie";
			mes "and Valhalla is true just";
			mes "through scientific tests.";
			next;
			mes "[The Book of Ymir]";
			mes "So, I am leaving this record in hope that someone in the future";
			mes "can confirm that Valkyrie and Valhalla actually exist...";
			next;
			valkyrie_Q = 2;
			if(!questprogress(1000)) {
				setquest 1000;
			}
			mes "[The Book of Ymir]";
			mes "Let the heroes live new lives";
			mes "so they can protect the world";
			mes "from danger. And then...";
			close;
		}
		mes "[The Book of Ymir]";
		mes "...";
		close;
	}
	mes "[The Book of Ymir]";
	mes "...";
	close;
}

yuno_in05,49,43,1	script	Heart of Ymir	HIDDEN_NPC,{
	if (BaseLevel > 98 && JobLevel > 49 && Class >= Job_Knight && Class <= Job_Crusader2) {
		if (valkyrie_Q == 2) warp "valkyrie",48,8;
	}
	end;
}

valkyrie,44,33,5	script	Teleporter	4_F_TELEPORTER,{
	if (Upper != 1) {
		.@karma_d = rand(1,10);
		if (.@karma_d > 4) {
			mes "[Teleporter]";
			mes "Congratulations.";
			mes "Honor to the warriors!";
			close;
		}
		mes "[Teleporter]";
		mes "Please refrain";
		mes "from touching any";
		mes "of the exhibitions.";
		mes "..........";
		close;
	}
	else {
		mes "[Teleporter]";
		mes "Honorable one,";
		mes "which place do you wish to go?";
		next;
		switch(select("Prontera", "Morroc", "Payon", "Geffen", "Alberta", "Izlude", "Al De Baran", "Comodo", "Juno")) {
		case 1: callsub S_Warpchar,"prontera",116,72;
		case 2: callsub S_Warpchar,"morocc",156,46;
		case 3: callsub S_Warpchar,"payon",69,100;
		case 4: callsub S_Warpchar,"geffen",120,39;
		case 5: callsub S_Warpchar,"alberta",117,56;
		case 6:
			if (RENEWAL) callsub S_Warpchar,"izlude",129,97;
			else callsub S_Warpchar,"izlude",94,103;
		case 7: callsub S_Warpchar,"aldebaran",91,105;
		case 8: callsub S_Warpchar,"comodo",209,143;
		case 9: callsub S_Warpchar,"yuno",328,101;
		}
	}
S_Warpchar:
	mes "[Teleporter]";
	mes "Have a nice trip.";
	close2;
	savepoint getarg(0),getarg(1),getarg(2);
	warp getarg(0),getarg(1),getarg(2);
	end;
}

//== Warps =================================================
valkyrie,48,73,0	warp	valk01#1	1,1,valkyrie,48,64
valkyrie,48,66,0	warp	valk01#2	1,1,valkyrie,48,75
