//===== Hercules Script ======================================
//= Universal Rental NPC
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.2
//===== Description: =========================================
//= One-click automatic mount rentals.
//= Replaced 'close' with 'end' [Streusel]
//= Fixed 'close' issues. [Joseph]
//============================================================

prontera,124,201,1	script	Universal Rental NPC	4_F_JOB_BLACKSMITH,{
	if (ismounting()) {
		message strcharinfo(0),"You must first remove your mount.";
		end;
	}
	else if ((eaclass()&EAJ_THIRDMASK) == EAJ_RANGER && !countitem(6124)) {
		if (!checkfalcon() && getskilllv("HT_FALCON") && !checkoption(Option_Wug) && !checkoption(Option_Wugrider)) {
			if(select(" ~ Falcon: ~ Warg")==1) setfalcon;
			else getitem 6124,1;
			specialeffect2 EF_TEIHIT3;
			close;
		}
		else getitem 6124,1;
	}
	else if ((eaclass()&EAJ_THIRDMASK) == EAJ_MECHANIC && !checkcart() && getskilllv("MC_PUSHCART")) {
		if (!checkmadogear() && getskilllv("NC_MADOLICENCE")) {
			if(select(" ~ Cart: ~ Mado")==1) setcart;
			else setmadogear;
			specialeffect2 EF_TEIHIT3;
			close;
		}
		else setcart;
	}
	else if (!checkcart() && getskilllv("MC_PUSHCART")) setcart;
	else if (!checkfalcon() && getskilllv("HT_FALCON") && !checkoption(Option_Wug) && !checkoption(Option_Wugrider)) setfalcon;
	else if (!checkriding() && getskilllv("KN_RIDING")) {
		if ((eaclass()&EAJ_THIRDMASK) == EAJ_RUNE_KNIGHT) setdragon;
		else setriding;
	}
	else if (!checkmadogear() && getskilllv("NC_MADOLICENCE")) setmadogear;
	else {
		message strcharinfo(0),"You do not meet requirements to rent.";
		end;
	}
	specialeffect2 EF_TEIHIT3;
	end;
}

//============================================================
// Old changelog
//============================================================
//= A Free PecoPeco and Falcon Breeder
//= 1.1 FIXED checkriding/cart/falcon funcs [Lupus]
//= 1.2 Added Rebirth/Advanced Class support [Mass Zero]
//= 1.3 Simplified the checks of job [Silentdragon]
//= 1.3a Fixed a Typo Error. [Samuray22]
//=      Thanks to ~AnnieRuru~.
//= 1.4 Optimized Script. No need for 50 million Labels. [Spre]
//============================================================
